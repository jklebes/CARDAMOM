!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! CARbon DAta MOdel fraMework (CARDAMOM) and DALEC terrestrial ecosystem model suite
! CARDAMOM is a Bayesian model-data fusion software framework. CARDAMOM is used to
! assimilate observations and ecological theory to retrieve parameters for the
! DALEC suite of intermediate complexity terrestrial ecosystem models. DALEC can be
! used as a fully integrated component of CARDAMOM or independently.
! Copyright (C) 2024  University of Edinburgh,
!                     Mathew Williams (mat.williams@ed.ac.uk),
!                     T. Luke Smallman (t.l.smallman@ed.ac.uk)
! UoE = University of Edinburgh

! This program is free software: you can redistribute it and/or modify
! it under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version.

! This program is distributed in the hope that it will be useful,
! but WITHOUT ANY WARRANTY; without even the implied warranty of
! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
! GNU General Public License for more details.

! You should have received a copy of the GNU General Public License
! along with this program.  If not, see <https://www.gnu.org/licenses/>.

!!!!!!!!!!!! File specific description !!!!!!!!!!
! This file contains the source code of DALEC.A3.C3.H2.M1.015
!
! This code contains a variant of the Data Assimilation Linked ECosystem (DALEC) model.
! This version of DALEC is derived from the following primary references:
! Sus et al., (2010), https://doi.org/10.1016/j.agee.2010.06.012.
! Bloom & Williams (2015), https://doi.org/10.5194/bg-12-1299-2015.
! Smallman et al., (2017), https://doi.org/10.1002/2016JG003520.
! Smallman & Williams (2019) https://doi.org/10.5194/gmd-12-2227-2019.
! This code is based on that created by A. A. Bloom (UoE, now at JPL, USA) and
! Oliver Sus (UoE, now at EUMETSAT, Darmstadt).
! Subsequent modifications by:
! T. L. Smallman (University of Edinburgh, t.l.smallman@ed.ac.uk)
! See function / subroutine specific comments for exceptions and contributors
!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

module CARBON_MODEL_MOD

  implicit none

  !!!!!!!!!!!
  ! Authorship contributions
  !
  ! This code contains a variant of the Data Assimilation Linked ECosystem (DALEC) model.
  ! This version of DALEC is derived from the following primary references:
  ! Sus et al., (2010), https://doi.org/10.1016/j.agee.2010.06.012.
  ! Bloom & Williams (2015), https://doi.org/10.5194/bg-12-1299-2015.
  ! Smallman et al., (2017), https://doi.org/10.1002/2016JG003520.
  ! Smallman & Williams (2019) https://doi.org/10.5194/gmd-12-2227-2019.
  ! This code is based on that created by A. A. Bloom (UoE, now at JPL, USA) and
  ! Oliver Sus (UoE, now at EUMETSAT, Darmstadt).
  ! Subsequent modifications by:
  ! T. L. Smallman (University of Edinburgh, t.l.smallman@ed.ac.uk)
  ! See function / subroutine specific comments for exceptions and contributors
  !!!!!!!!!!!

  ! make all private
  private

  ! explicit publics
  public :: CARBON_MODEL     &
           ,top_soil_depth   &
           ,nos_soil_layers  &
           ,sw_par_fraction  &
           ,mVs , initialize_mv, &
           model_working_variables

  !!!!!!!!!
  ! Parameters
  !!!!!!!!!

  ! useful technical parameters

  double precision, parameter :: vsmall = tiny(0d0)*1d3 & ! *1d3 to add a little breathing room
                                ,vlarge = huge(0d0)

  integer, parameter :: nos_root_layers = 2, nos_soil_layers = nos_root_layers + 1
  double precision, parameter :: pi = 3.1415927d0,  &
                               pi_1 = 0.3183099d0,  & ! pi**(-1d0)
                                pi2 = 9.869604d0,   & ! pi**2d0
                             two_pi = 6.283185d0,   & ! pi*2d0
                         deg_to_rad = 0.01745329d0, & ! pi/180d0
                sin_dayl_deg_to_rad = 0.3979486d0,  & ! sin( 23.45d0 * deg_to_rad )
                            gravity = 9.8067d0,     & ! acceleration due to gravity, ms-1
                              boltz = 5.670400d-8,  & ! Boltzmann constant (W.m-2.K-4)
                         emissivity = 0.96d0,       &
                        emiss_boltz = 5.443584d-08, & ! emissivity * boltz
                    sw_par_fraction = 0.5d0,        & ! fraction of short-wave radiation which is PAR
                             freeze = 273.15d0,     &
                         gs_H2O_CO2 = 1.646259d0,   & ! The ratio of H20:CO2 diffusion for gs (Jones appendix 2)
                       gs_H2O_CO2_1 = 0.6074378d0,  & ! gs_H2O_CO2 ** (-1d0)
                  gs_H2Ommol_CO2mol = 0.001646259d0,& ! gs_H2O_CO2 * 1d-3
              gs_H2Ommol_CO2mol_day = 142.2368d0,   & ! The ratio of H20:CO2 diffusion for gs, including seconds per day correction
                         gb_H2O_CO2 = 1.37d0,       & ! The ratio of H20:CO2 diffusion for gb (Jones appendix 2)
            partial_molar_vol_water = 18.05d-6,     & ! partial molar volume of water, m3 mol-1 at 20C
                     mol_to_g_water = 18d0,         & ! molecular mass of water
                   mmol_to_kg_water = 1.8d-5,       & ! milli mole conversion to kg
                       mol_to_g_co2 = 12d0,         & ! molecular mass of CO2 (g)
                         umol_to_gC = 1.2d-5,       & ! conversion of umolC -> gC
                         gC_to_umol = 83333.33d0,   & ! conversion of gC -> umolC; umol_to_gC**(-1d0)
                       g_to_mol_co2 = 0.08333333d0, &
  !snowscheme       density_of_water = 998.9d0,         & ! density of !water kg.m-3
                     gas_constant_d = 287.04d0,     & ! gas constant for dry air (J.K-1.mol-1)
                               Rcon = 8.3144d0,     & ! Universal gas constant (J.K-1.mol-1)
                          vonkarman = 0.41d0,       & ! von Karman's constant
                        vonkarman_1 = 2.439024d0,   & ! 1 / von Karman's constant
                        vonkarman_2 = 0.1681d0,     & ! von Karman's constant^2
                              cpair = 1004.6d0        ! Specific heat capacity of air; used in energy balance J.kg-1.K-1

  ! photosynthesis / respiration parameters
  double precision, parameter :: &
                      kc_saturation = 310d0,        & ! CO2 half saturation, saturation value
                   kc_half_sat_conc = 23.956d0,     & ! CO2 half sat, half sat
                 co2comp_saturation = 36.5d0,       & ! CO2 compensation point, saturation
              co2comp_half_sat_conc = 9.46d0,       & ! CO2 comp point, half sat
                                                      ! Each of these are temperature sensitivty
                        Rg_fraction = 0.21875d0,    & ! fraction of C allocation towards each pool
                                                      ! lost as growth respiration
                                                      ! (i.e. 0.28 .eq. xNPP)
                    one_Rg_fraction = 1d0 - Rg_fraction

  ! hydraulic parameters
  double precision, parameter :: &
                         tortuosity = 2.5d0,        & ! tortuosity
                             gplant = 5d0,          & ! plant hydraulic conductivity (mmol m-1 s-1 MPa-1)
                        root_resist = 10d0,         & ! Root resistivity (MPa s g mmolâˆ’1 H2O), default 25, crops 10
                        root_radius = 0.00029d0,    & ! root radius (m) Bonen et al 2014 = 0.00029
                                                      ! Williams et al 1996 = 0.0001
                      root_radius_1 = root_radius**(-1d0), &
                root_cross_sec_area = pi * root_radius**2, & ! root cross sectional area (m2)
                                                             ! = pi * root_radius * root_radius
                       root_density = 0.31d6,       & ! root density (g biomass m-3 root)
                                                      ! 0.5e6 Williams et al 1996
                                                      ! 0.31e6 Bonan et al 2014
            root_mass_length_coef_1 = (root_cross_sec_area * root_density)**(-1d0), &
                 const_sfc_pressure = 101325d0,     & ! (Pa)  Atmospheric surface pressure
                               head = 0.009807d0,   & ! head of pressure (MPa/m)
                             head_1 = 101.968d0       ! inverse head of pressure (m/MPa)

  ! structural parameters
  double precision, parameter :: &
                      canopy_height = 1d0,              & ! canopy height assumed to be 1 m for arable crops
                       tower_height = canopy_height + 2d0, & ! tower (observation) height assumed to be 2 m above canopy
                           min_wind = 0.2d0,            & ! minimum wind speed at canopy top
                          min_layer = 0.03d0,           & ! minimum thickness of the third rooting layer (m)
                        soil_roughl = 0.00085d0,        & ! soil roughness length (m), Meier et al., (2022), https://doi.org/10.5194/gmd-15-2365-2022
                       min_drythick = soil_roughl*10d0, & ! minimum dry thickness depth (m) 0.01 WRF-SPA
                     top_soil_depth = 0.30d0,           & ! thickness of the top soil layer (m)
                           min_root = 5d0,              & ! minimum root biomass (gBiomass.m-2)
                            min_lai = 0.01d0,           & ! minimum LAI assumed for aerodynamic conductance calculations (m2/m2)
                        min_storage = 0.1d0               ! minimum canopy water (surface) storage (mm)

  ! timing parameters
  double precision, parameter :: &
                   seconds_per_hour = 3600d0,         & ! Number of seconds per hour
                    seconds_per_day = 86400d0,        & ! Number of seconds per day
                  seconds_per_day_1 = 1.157407d-05      ! Inverse of seconds per day

  ! ACM-GPP-ET parameters
  double precision, parameter :: &
                       Vc_minT = -6.991d0,     & ! Temperature at which all photosynthetic activity is shutdown
                       Vc_coef = 0.1408d0,     & ! Temperature above Vc_minT that 50% limitation of cold shutdown occurs
                   ! Assumption that photosythesis will be limited by Jmax temperature response
                   pn_max_temp = 57.05d0,      & ! Maximum daily max temperature for photosynthesis (oC)
                   pn_min_temp = -1d6,         & ! Minimum daily max temperature for photosynthesis (oC)
                   pn_opt_temp = 30d0,         & ! Optimum daily max temperature for photosynthesis (oC)
                   pn_kurtosis = 0.172d0,      & ! Kurtosis of Jmax temperature response
                ko_half_sat_25C = 157.46892d0, & ! photorespiration O2 half sat(mmolO2/mol), achieved at 25oC
           ko_half_sat_gradient = 14.93643d0,  & ! photorespiration O2 half sat gradient
                kc_half_sat_25C = 319.58548d0, & ! carboxylation CO2 half sat (umolCO2/mol), achieved at 25oC
           kc_half_sat_gradient = 24.72297d0,  & ! carboxylation CO2 half sat gradient
                co2comp_sat_25C = 36.839214d0, & ! carboxylation CO2 compensation point(umolCO2/mol), saturation
               co2comp_gradient = 9.734371d0,  & ! carboxylation CO2 comp point, achieved at oC
                                                 ! Each of these are temperature sensitivty
                            e0 = 3.2d+00,      & ! Quantum yield (gC/MJ/m2/day PAR), SPA apparent yield
                minlwp_default =-1.808224d+00, & ! minimum leaf water potential (MPa). NOTE: actual SPA = -2 MPa
      soil_iso_to_net_coef_LAI =-2.717467d+00, & ! Coefficient relating soil isothermal net radiation to net.
       soil_iso_to_net_coef_SW =-3.500964d-02, & ! Coefficient relating soil isothermal net radiation to net.
         soil_iso_to_net_const = 3.455772d+00, & ! Constant relating soil isothermal net radiation to net
     canopy_iso_to_net_coef_SW = 1.480105d-02, & ! Coefficient relating SW to the adjustment between isothermal and net LW
       canopy_iso_to_net_const = 3.753067d-03, & ! Constant relating canopy isothermal net radiation to net
    canopy_iso_to_net_coef_LAI = 2.455582d+00, & ! Coefficient relating LAI to the adjustment between isothermal and net LW
                          iWUE = 4.6875d-04      ! Intrinsic water use efficiency (umolC/mmolH2O-1/m2leaf/s-1)

  ! Module level variables for the Sellers (1985) 2-stream radiative transfer scheme approximation
  integer, parameter :: no_wavelength = 2 ! Number of wavelenths (order NIR, PAR)
  double precision, parameter :: & !Vc = 0.75d0,  & ! Clumping factor / vegetation cover (1 = uniform, 0 totally clumped, mean = 0.75)
                                                 ! He et al., (2012) http://dx.doi.org/10.1016/j.rse.2011.12.008
               soil_nir_reflectance = 0.023d0, & ! Soil reflectance to near infrared radiation
               soil_par_reflectance = 0.033d0, & ! Soil reflectance to photosynthetically active radiation
             canopy_nir_reflectance = 0.38d0,  & ! Canopy NIR reflectance (Default 0.43, Sitka Spruce 0.16, grass/crop ~ 0.38)
             canopy_par_reflectance = 0.11d0,  & ! Canopty PAR reflectance (Default 0.16, Sitka Spruce 0.07, grass/crop ~ 0.11)
           canopy_nir_transmittance = 0.26d0,  & ! Canopy NIR transmittance
           canopy_par_transmittance = 0.16d0,  & ! Canopty PAR transmittance
                    newsnow_nir_abs = 0.27d0,  & ! NIR absorption fraction
                    newsnow_par_abs = 0.05d0,  & ! PAR absorption fraction
                      !nirrefl_crop = 0.50d0,  & ! NIR reflectance for dead crop (Nagler et al., 2003)
                      !parrefl_crop = 0.30d0,  & ! PAR reflectance for dead crop (Nagler et al., 2003)
         leaf_distribution_deviance = 0.01d0     ! Deviation from spherical, min absolute value (0.01) required for numerical security.
                                                 ! Leaf angle distribution, quantified as the deviation from a spherical distribution.
                                                 ! The default assumption in many models, including SPA, is that leaves have a spherical distribution (=0).
                                                 ! However, =-1 would indicate vertical leaves, while =+1 are horizontal leaves.
                                                 ! See note book or references given above for the complete integral equation

  !
  ! some hardcoded crop parameters
  !

  double precision, parameter :: resp_rate_temp_coeff = 0.0334798d0,& ! exponential temperature response for heterotrophic respiration (0.0334798 = Q10 of 1.4, 0.0693d0 = Q10 of 2)
                                               lv_res = 0.5d0,      & ! residue fraction of leaves left post harvest (default = 0.1)
                                               st_res = 0.5d0,      & ! residue fraction of stem left post harvest (default = 0.1)
                                                LAICR = 4d0,        & ! LAI above which self shading turnover occurs
                                          rel_gso_max = 0.35d0,     & ! allocation to storage organ relative to GPP
                               resp_cost_labile_trans = 0.21875d0     ! labile lost to respiration per gC labile to GPP


  !!!!!!!!!
  ! Module level variables
  !!!!!!!!!
type model_working_variables
  logical :: do_iWUE = .true. ! Use iWUE or WUE for stomatal optimisation
  double precision :: minlwp = minlwp_default
  ! hydraulic model variables
  integer :: water_retention_pass, soil_layer
  double precision, dimension(nos_soil_layers) :: &
                   soil_frac_clay,soil_frac_sand, & ! clay and soil fractions of soil
                                     infiltrated    ! surface water infiltrated (kgH2O.m-2.d-1)
  double precision, dimension(nos_root_layers) :: uptake_fraction, & ! fraction of water uptake from each root layer
                                                           demand, & ! maximum potential canopy hydraulic demand
                                            water_flux_mmolH2Om2s, & ! potential transpiration flux (mmolH2O.m-2.s-1)
                                        conductance_mmolH2OMPam2s    ! Effective hydraulic resistance of each layer (mmolH2O.MPa-1.m-2.s-1)
  double precision, dimension(nos_soil_layers+1) :: SWP, & ! soil water potential (MPa)
                                      soil_conductivity, & ! soil conductivity
                                            waterchange, & ! net water change by specific soil layers (m)
                                        water_grav_flow, & ! flow of water under gravity FROM each soil layer (kgH2O/m2/d)
                                         field_capacity, & ! soil field capacity (m3.m-3)
                                 field_capacity_initial, &
                                         soil_waterfrac, & ! soil water content (m3.m-3)
                                               porosity, & ! soil layer porosity, (fraction)
                                       porosity_initial, &
                                        layer_thickness, & ! thickness of soil layers (m)
                        cond1, cond2, cond3, potA, potB    ! Saxton equation values

  double precision :: root_reach, root_biomass, &
                             fine_root_biomass, & ! root depth, coarse+fine, and fine root biomass
                              total_water_flux, & ! potential transpiration flux (kgH2O.m-2.day-1)
                                      drythick, & ! estimate of the thickness of the dry layer at soil surface (m)
                                          wSWP, & ! soil water potential weighted by canopy supply (MPa)
                                          rSWP, & ! soil water potential weighted by root presence (MPa)
                                          Reff, & ! Effective total hydraulic resistance (MPa.m2.s.mmolH2O-1)
                                     max_depth, & ! maximum possible root depth (m)
                                        root_k, & ! biomass to reach half max_depth
                                        runoff, & ! runoff (kgH2O.m-2.day-1)
                                     underflow, & ! drainage from the bottom of soil column (kgH2O.m-2.day-1)
                      new_depth,previous_depth, & ! depth of bottom of soil profile
                                   canopy_wind, & ! wind speed (m.s-1) at canopy top
                                         ustar, & ! friction velocity (m.s-1)
                                      ustar_Uh, &
                                air_density_kg, & ! air density kg/m3
                                ET_demand_coef, & ! air_density_kg * vpd_kPa * cpair
                                        roughl, & ! roughness length (m)
                                  displacement, & ! zero plane displacement (m)
                                         meant, & ! mean air temperature (oC)
                                         leafT, & ! canopy temperature (oC)
                              mean_annual_temp, &
                            canopy_swrad_MJday, & ! canopy_absorbed shortwave radiation (MJ.m-2.day-1)
                              canopy_par_MJday, & ! canopy_absorbed PAR radiation (MJ.m-2.day-1)
                              soil_swrad_MJday, & ! soil absorbed shortwave radiation (MJ.m-2.day-1)
                              canopy_lwrad_Wm2, & ! canopy absorbed longwave radiation (W.m-2)
                                soil_lwrad_Wm2, & ! soil absorbed longwave radiation (W.m-2)
                                 sky_lwrad_Wm2, & ! sky absorbed longwave radiation (W.m-2)
                          stomatal_conductance, & ! stomatal conductance (mmolH2O.m-2ground.s-1)
                         potential_conductance, & ! potential stomatal conductance (mmolH2O.m-2ground.s-1)
                           minimum_conductance, & ! potential stomatal conductance (mmolH2O.m-2ground.s-1)
                       aerodynamic_conductance, & ! bulk surface layer conductance (m.s-1)
                              soil_conductance, & ! soil surface conductance (m.s-1)
                             convert_ms1_mol_1, & ! Conversion ratio for m.s-1 -> mol.m-2.s-1
                            convert_ms1_mmol_1, & ! Conversion ratio for m/s -> mmol/m2/s
                           air_vapour_pressure, & ! Vapour pressure of the air (kPa)
                                        lambda, & ! latent heat of vapourisation (J.kg-1)
                                         psych, & ! psychrometric constant (kPa K-1)
                                         slope, & ! Rate of change of saturation vapour pressure with temperature (kPa.K-1)
                        water_vapour_diffusion, & ! Water vapour diffusion coefficient in (m2/s)
                             dynamic_viscosity, & ! dynamic viscosity (kg.m-2.s-1)
                           kinematic_viscosity, & ! kinematic viscosity (m2.s-1)
                                  snow_storage, & ! snow storage (kgH2O/m2)
                                canopy_storage, & ! water storage on canopy (kgH2O.m-2)
                          intercepted_rainfall    ! intercepted rainfall rate equivalent (kgH2O.m-2.s-1)

  ! Module level variables for the Sellers (1985) 2-stream radiative transfer scheme approximation
  double precision ::        leaf_angle, & ! Mean leaf angle deviation from the horizontal (radians)
                              cos2theta, & ! Analytical correction for leaf angle (radians) on light scattering within the canopy
                                 Vc, Vg, & ! Define the vegetated and covered soil (i.e. by litter) fractions
                                mu_obar, & ! The average inverse diffuse optical depth per unit leaf area.
                                  O1,O2    ! Empirical coefficients related to the leaf angle distribution
  double precision, dimension(no_wavelength) :: &
                     canopy_reflectance = (/canopy_nir_reflectance,canopy_par_reflectance/), & !
                   canopy_transmittance = (/canopy_nir_transmittance,canopy_par_transmittance/), & !
                       soil_reflectance = (/soil_nir_reflectance,soil_par_reflectance/), & !
                      canopy_scattering, & ! Canopy scattering of incident light, varied by wavelength
                                     bb, & ! Downward scatting of diffuse radiation
                                     cc, & ! Upward scattering as diffuse radiation, a function of canopy_transmittance, canopy_reflectance and leaf angle.
                                     hh, & ! Extinction coefficient for diffuse radiation
                                   beta, & ! The upward scattering fraction / coefficient for diffuse radiation.
                                  beta0    ! The upward scattering fraction / coefficient for direct radiation.

  ! Module level variables for ACM_GPP_ET parameters
  double precision ::   delta_gs, & ! day length corrected gs increment mmolH2O/m2/dayl
                            ceff, & ! canopy efficency, ceff = avN*NUE
                             avN, & ! average foliar N (gN/m2)
                       iWUE_step, & ! Intrinsic water use efficiency for that day (gC/m2leaf/dayl/mmolH2Ogs)
                             NUE, & ! Photosynthetic nitrogen use efficiency at optimum temperature (oC)
                                    ! ,unlimited by CO2, light and photoperiod (gC/gN/m2leaf/day
metabolic_limited_photosynthesis, & ! temperature, leaf area and foliar N limiterd photosynthesis (gC/m2/day)
    light_limited_photosynthesis, & ! light limited photosynthesis (gC/m2/day)
                              ci, & ! Internal CO2 concentration (ppm)
                          gb_mol, & ! Canopy boundary layer conductance (molCO2/m2/day)
                        rb_mol_1, & ! Canopy boundary layer resistance (day/m2/molCO2)
                    co2_half_sat, & ! CO2 at which photosynthesis is 50 % of maximum (ppm)
                  co2_comp_point    ! CO2 at which photosynthesis > 0 (ppm)

  ! Module level variables for step specific met drivers
  double precision :: mint, & ! minimum temperature (oC)
                      maxt, & ! maximum temperature (oC)
        airt_zero_fraction, & ! fraction of air temperature above freezing
                     swrad, & ! incoming short wave radiation (MJ/m2/day)
                       co2, & ! CO2 (ppm)
                       doy, & ! Day of year
                  rainfall, & ! rainfall (kgH2O/m2/s)
                  snowfall, &
                 snow_melt, & ! snow melt (kgH2O/m2/s)
                  wind_spd, & ! wind speed (m/s)
                   vpd_kPa, & ! Vapour pressure deficit (kPa)
 leaf_canopy_light_scaling, & ! approximate scaling factor from leaf to canopy for gpp and gs
  leaf_canopy_wind_scaling, & ! approximate scaling factor from leaf to canopy for gb
                     lai_1, & ! inverse of LAI
                       lai    ! leaf area index (m2/m2)

  ! Module level variables for step specific timing and location information
  integer :: steps_per_year
  double precision ::       seconds_per_step, & !
                               days_per_step, & !
                             days_per_step_1, & !
                          mean_days_per_step, &
                                dayl_seconds, & ! day length in seconds
                              dayl_seconds_1, &
                         dayl_hours_fraction, &
                                  dayl_hours, & ! day length in hours
                                    latitude, & ! latitude ()-90/90)
                            latitude_radians, & ! latitude in radians
                        sin_latitude_radians, & ! sin(latitude_radians)
                        cos_latitude_radians, & ! cos(latitude_radians)
                          sunset_solar_angle, & ! Solar angle at sunset hour
                                 declination, & ! Solar declination, function of day of year
                   cosine_solar_zenith_angle    ! Cosine zenith angle of the timestep

  double precision, dimension(:), allocatable :: deltat_1, & ! inverse of decimal days
                                               meant_time, &
                                  airt_zero_fraction_time, &
                                          daylength_hours, &
                                        daylength_seconds, &
                                      daylength_seconds_1





  ! variables local to this module..
  integer ::   plough_day, & ! day-of-year when field is ploughed  (default)
                  sow_day, & ! day-of-year when field is sown      (default)
              harvest_day, & ! day-of-year when field is harvested (default)
                    stmob, & ! remoblise stem C to labile (1 = on)
   turnover_labile_switch    ! begin turnover of labile C

  logical :: vernal_calcs, &  ! do vernalisation calculations?
                 ploughed, &  !
          use_seed_labile, & != .False. ! whether to use seed labile for growth
                     sown, & != .False. ! has farmer sown crop yet?
                  emerged    != .False. ! has crop emerged yet?

  double precision ::             gpp_acm, & ! gross primary productivity (gC.m-2.day-1)
                      stock_storage_organ, & ! storage organ C pool, i.e. the desired crop (gC.m-2)
                       stock_dead_foliage, & ! dead but still standing foliage (gC.m-2)
                          stock_resp_auto, & ! autotrophic respiration pool (gC.m-2)
                             stock_labile, & ! labile C pool (gC.m-2)
                            stock_foliage, & ! foliage C pool (gC.m-2)
                               stock_stem, & ! stem C pool (gC.m-2)
                              stock_roots, & ! roots C pool (gC.m-2)
                             stock_litter, & ! litter C pool (gC.m-2)
                      stock_soilOrgMatter, & ! SOM C pool (gC.m-2)
                                resp_auto, & ! autotrophic respiration (gC.m-2.d-1)
                            resp_h_litter, & ! litter heterotrophic respiration (gC.m-2.d-1)
                     resp_h_soilOrgMatter, & ! SOM heterotrophic respiration (gC.m-2.d-1)
                                      npp, & ! net primary productivity (gC.m-2.d-1)
                                nee_dalec, & ! net ecosystem exchange (gC.m-2.d-1)
                                       DS, & ! Developmental state and initial condition
                                      LCA, & ! leaf mass area (gC.m-2)
               alloc_to_storage_organ_old, & ! rolling average allocation of GPP to storage organ (gC.m-2.d-1)
                       decomposition_rate, & ! decomposition rate (frac / day)
                       frac_GPP_resp_auto, & ! fraction of GPP allocated to autotrophic carbon pool
                    turnover_rate_foliage, & ! turnover rate of foliage (frac/day)
                       turnover_rate_stem, & ! same for stem
                     turnover_rate_labile, & ! same for labile
                  turnover_rate_resp_auto, & ! same for autotrophic C pool
               mineralisation_rate_litter, & ! mineralisation rate of litter
        mineralisation_rate_soilOrgMatter, & ! mineralisation rate of SOM
                                    PHUem, & ! emergance value for phenological heat units
                                      PHU, & ! phenological heat units
                                   DR_pre, & ! development rate coefficient DS 0->1
                                  DR_post, & ! development rate coefficient DS 1->2
                                     tmin, & ! min temperature for development
                                     tmax, & ! max temperature for development
                                     topt, & ! optimum temperature for development
                                   tmin_v, & ! min temperature for vernalisation
                                   tmax_v, & ! max temperature for vernalisation
                                   topt_v, & ! optimim temperature for vernalisation
                                  doptmin, & ! difference between optimum and minimum cardinal temperatures
                                  dmaxmin, & ! difference between maximum and minimum cardinal temperatures
                                doptmin_v, & ! difference between optimum and minimum vernalisation temperatures
                                dmaxmin_v, & ! difference between maximum and minimum vernalisation temperatures
                                      VDh, & ! effective vernalisation days when plants are 50 % vernalised
                                     VDh5, & ! VDh raised to the 5th power, precomputed
                                       VD, & ! count of vernalisation days
                                 RDRSHMAX, & ! maximum rate of self shading turnover (frac/day)
                                     PHCR, & ! critical value of photoperiod for development
                                     PHSC, & ! photoperiod sensitivity
                                     raso, & ! rolling average for alloc to storage organ
                                 max_raso, & ! maximum value for rolling average alloc to storage organ
                                    BM_EX, & ! biomass extracted in addition to the storage organ
                 HARVESTextracted_foliage, & ! Foliage removed by harvest activity
                    HARVESTextracted_stem, & ! Stem removed by harvest activity
            HARVESTextracted_dead_foliage, & ! Dead still standing foliage removed by harvest activity
                  HARVESTextracted_labile, & ! Labile removed by harvest activity
                    HARVESTlitter_foliage, & ! Foliage converted to litter by harvest
                       HARVESTlitter_stem, & ! Stem converted to litter by harvest
               HARVESTlitter_dead_foliage, & ! Dead standing foliage converted to litter by harvest
                  HARVESTlitter_resp_auto, & ! Autotrophic pool converted to litter by harvest
                     HARVESTlitter_labile, & ! Labile converted to litter by harvest
                       PLOUGHlitter_roots, & ! Plough induced litter generation from roots
                                       HI, & ! Harvest index, the ratio of yield to shoot C
                                    yield, & ! crop yield (gC.m-2)
                       alloc_to_resp_auto, & ! amount of carbon to allocate to autotrophic respiration pool
                      turnover_rate_roots, & ! turnover over rate of roots interpolated each time step
                                  gso_max, & !
                             max_raso_old, & !
                                 raso_old, & !
                  resp_cost_labile_to_npp, & ! respiratory cost of moving carbon..from labile to NPP pool
                  resp_cost_npp_to_labile, & ! ..from remaining NPP to labile pool
              resp_cost_foliage_to_labile, & ! ..from foliage to labile pool
                 resp_cost_stem_to_labile, & ! ..from stem to labile pool
                                resp_rate, & ! rate of respiration at given temperature
                                   Cshoot, & !
                                       DR, & !
                          fol_frac_intpol, & !
                         stem_frac_intpol, & !
                                 fP,fT,fV, & !
                        foliage_to_labile, & !
                           stem_to_labile, & !
                         root_frac_intpol, & !
                   alloc_to_storage_organ, & !
                       litterfall_foliage, & !
                          litterfall_stem, & !
                         litterfall_roots, & !
                            decomposition, & !
                                npp_shoot, & !
                        alloc_from_labile, & !
                          alloc_to_labile, & !
                           alloc_to_roots, & !
                         alloc_to_foliage, & !
                            alloc_to_stem, & !
                                    RDRSH, & !
                                    RDRDV, & !
                                      RDR
  end type

  type(model_working_variables), allocatable, dimension(:):: mVs

    contains

  subroutine initialize_mv(mV, nodays, nomet, nopars, met, deltat, lat, soil_frac_sand, soil_frac_clay)
      !! For a single chain's model_working_varibles type object mV, allocate arrays
        !! and calculate initial values.
        use cardamom_structures, only: DATAin
        implicit none
        type(model_working_variables), intent(out):: mV
        integer, intent(in):: nodays, nomet, nopars
        double precision, intent(in) :: met(nomet, nodays)     
        double precision, intent(in) :: deltat(nodays)
        double precision, intent(in) :: lat
        double precision, intent(in), dimension(:), optional :: soil_frac_sand, soil_frac_clay
          !! Needed as arguments from r_interface , otherwise taken from DATAin


        integer:: n

        if (present(soil_frac_sand)) then
          mV%soil_frac_sand = soil_frac_sand 
        else
          mV%soil_frac_sand = DATAin%soil_frac_sand 
        endif
        if (present(soil_frac_clay)) then
          mV%soil_frac_clay = soil_frac_clay
        else
          mV%soil_frac_clay = DATAin%soil_frac_clay
        endif

        allocate(mV%deltat_1(nodays))
        mV%deltat_1 = deltat**(-1d0)
        ! zero variables not done elsewhere
        mV%water_flux_mmolH2Om2s = 0d0
        ! initialise some time invarient parameters
        call saxton_parameters(mV%soil_frac_clay,mV%soil_frac_sand, mV)
        call initialise_soils(mV%soil_frac_clay,mV%soil_frac_sand, mV)
        ! call update_soil_initial_conditions(pars(24), mV)
        ! save the initial conditions for later
        mV%field_capacity_initial = mV%field_capacity
        mV%porosity_initial = mV%porosity
   
  end subroutine initialize_mv


  subroutine destroy_mv(mV)
    !! deallocate arrays in mV
    type(model_working_variables):: mV
        deallocate(mV%deltat_1)
    end subroutine

  !
  !--------------------------------------------------------------------
  !
  subroutine CARBON_MODEL(start,finish,met,pars,deltat,nodays,lat &
                         ,FLUXES,POOLS,DIAGS &
                         ,nopars,nomet,nopools,nofluxes,nodiags &
                          ,mV)

    !
    ! The Data Assimilation Linked Ecosystem Carbon - CROP - BUCKET (DALEC_CROP_BUCKET) model.
    ! modified from Sus et al., (2010)
    !
    ! The Aggregated Canopy Model for Gross Primary Productivity and Evapotranspiration (ACM-GPP-ET)
    ! simulates coupled photosynthesis-transpiration (via stomata), soil and intercepted canopy evaporation and
    ! soil water balance (4 layers).
    !
    ! This version was coded by T. Luke Smallman (t.l.smallman@ed.ac.uk)
    ! Version 1.0: 15/07/2014
    ! Version 2.0: 15/11/2018 - Addition of the BUCKET model via ACM2 to include the water cycle
    ! Version 2.1: 21/09/2023 - BUCKET updated to current DALEC.4. standard plus the addition of the Sellers RTM
     
    use cardamom_structures, only: CI ! get former arguments: stock_seed_labile,DS_shoot,DS_root,fol_frac    &
                         ! ,stem_frac,root_frac,DS_LRLV,LRLV,DS_LRRT,LRRT, from here (read-only) 
    implicit none

      type(model_working_variables) :: mV

    ! declare input variables
    integer, intent(in) :: start    &
                          ,finish   &
                          ,nopars     & ! number of parameters in vector
                          ,nomet      & ! number of meteorological fields
                          ,nofluxes   & ! number of model fluxes
                          ,nopools    & ! number of model pools
                          ,nodays     & ! number of days in simulation
                          ,nodiags      ! number of model diagnostics

    double precision, intent(in) :: met(nomet,nodays)   & ! met drivers
                         ,deltat(nodays)                & ! time step in decimal days
                         ,pars(nopars)                  & ! number of parameters
                         ,lat                             ! site latitude (degrees)


    double precision, dimension((nodays+1),nopools), intent(inout) :: POOLS ! vector of ecosystem pools
    double precision, dimension(nodays,nofluxes), intent(inout) :: FLUXES ! vector of ecosystem fluxes
    double precision, dimension(nodays,nodiags), intent(inout) :: DIAGS ! vector of ecosystem diagnostics

    ! declare local variables
    double precision :: airt_weighting(3) &
                           ,transpiration &
                         ,soilevaporation &
                          ,wetcanopy_evap &
                         ,snowsublimation &
                                    ,infi   ! used to calculate infinity for diagnositc

    integer :: nxp,n

    ! met drivers are:
    ! 1st  run day
    ! 2nd  min daily temp (oC)
    ! 3rd  max daily temp (oC)
    ! 4th  Radiation (MJ.m-2.day-1)
    ! 5th  CO2 (ppm)
    ! 6th  DOY
    ! 7th  precipitation (kgH2O.m-2.s-1)
    ! 8th  NOT IN USE
    ! 9th  NOT IN USE
    ! 10th NOT IN USE
    ! 11th NOT IN USE
    ! 12th NOT IN USE
    ! 13th NOT IN USE
    ! 14th avg daily temperature (oC)
    ! 15th avg daily wind speed (m.s-1)
    ! 16th vapour pressure deficit (Pa)

    ! POOLS are:
    ! 1  = labile (gC/m2) (initial value: p18)
    ! 2  = foliar (gC/m2) (initial value: p19)
    ! 3  = root (gC/m2) (initial value: p20)
    ! 4  = stem (gC/m2) (initial value: p21)
    ! 5  = litter (gC/m2) (initial value: p22)
    ! 6  = som (gC/m2) (initial value: p23)
    ! 7  = autotrophic (gC/m2) (initial value: p24)
    ! 8  = surface soil water 0-30 cm (mm) (initial value: p38)
    ! 9  = storage organ C (gC/m2) (initial value: p25)
    ! 10 = dead still standing foliage (gC/m2)

    ! FLUXES are:
    ! 1  = GPP (gC/m2/day)
    ! 2  = temperature rate modifier (unitless)
    ! 3  = total autotrophic respiration - maintenance + growth (gC/m2/day)
    ! 4  = GPP allocation to foliage (gC/m2/day)
    ! 5  = GPP allocation to labile and tissue remobilisation into labile (gC/m2/day)
    ! 6  = GPP allocation to roots (gC/m2/day)
    ! 7  = GPP allocation to stem (gC/m2/day)
    ! 8  = labile->NPP transfer (gC/m2/day)
    ! 9  = allocation to storage organ (gC/m2/day)
    ! 10 = leaf litter production (gC/m2/day)
    ! 11 = stem litter production (gC/m2/day)
    ! 12 = root litter production (gC/m2/day)
    ! 13 = heterotrophic respiration from litter (gC/m2/day)
    ! 14 = heterotrophic respiration from som (gC/m2/day)
    ! 15 = litter decomposition to som (gC/m2/day)
    ! 16 = GPP allocation to autotrophic pool (gC/m2/day)
    ! 17 = NOT IN USE
    ! 18 = NOT IN USE
    ! 19 = evapotranspiration (kgH2O/m2/day)
    ! 20 = NOT IN USE
    ! 21 = harvest yield - storage organ C extracted (gC/m2/day)
    ! 22 = biomass extracted in addition to yield at harvest (gC/m2/day)
    ! 23 = respiration from autotrophic pool allocation (gC/m2/day)
    ! 24 = respiration from labile->NPP translocation (gC/m2/day)
    ! 25 = respiration from NPP->labile translocation (gC/m2/day)
    ! 26 = respiration from foliage remobilisation to labile (gC/m2/day)
    ! 27 = respiration from stem remobilisation to labile (gC/m2/day)
    ! 28 = foliage extracted from harvest (gC/m2/day)
    ! 29 = stem extracted from harvest (gC/m2/day)
    ! 30 = dead standing foliage extracted from harvest (gC/m2/day)
    ! 31 = labile extracted from harvest (gC/m2/day)
    ! 32 = foliage added to litter from harvest (gC/m2/day)
    ! 33 = stem added to litter from harvest (gC/m2/day)
    ! 34 = dead standing foliage added to litter from harvest (gC/m2/day)
    ! 35 = autotrophic pool added to litter from harvest (gC/m2/day)
    ! 36 = labile added to litter from harvest (gC/m2/day)
    ! 37 = roots added to litter from ploughing (gC/m2/day)
    ! 38 = transpiration (kgH2O/m2/day)
    ! 39 = soil evaporation (kgH2O/m2/day)
    ! 40 = wet canopy evaporation (kgH2O/m2/day)
    ! 41 = surface runoff (kgH2O/m2/day)
    ! 42 = drainage from bottom of soil column (kgH2O/m2/day)
    ! 43 = drainage from surface to 2nd soil layer (kgH2O/m2/day)
    ! 44 = infiltration into top soil layer (kgH2O/m2/day)
    ! 45 = fraction of transpiration from 1st rooting layer (0-1)
    ! 46 = fraction of transpiration from 2nd rooting layer (0-1)
    ! 47 = infiltration into middle soil layer (kgH2O/m2/day)
    ! 48 = infiltration into bottom soil layer (kgH2O/m2/day)

    ! PARAMETERS are:
    ! p(1)  = litter decomposition rate (fraction/day)
    ! p(2)  = fraction of GPP allocated to autotrophic C pool (fraction)
    ! p(3)  = maximum development rate coefficient DS (0->1) (day-1)
    ! p(4)  = maximum development rate coefficient DS (1->2) (day-1)
    ! p(5)  = turnover rate of foliage (fraction/day)
    ! p(6)  = turnover rate of stem (fraction/day)
    ! p(7)  = maximum foliage turnover rate due to self-shading (fraction/day)
    ! p(8)  = effective vernalisation days when plant is 50% vernalised (days)
    ! p(9)  = litter turnover rate, temperature adjusted (fraction/day)
    ! p(10) = som turnover rate, temperature adjusted (fraction/day)
    ! p(11) = photosynthetic nitrogen use efficiency (gC/gN/m2/day)
    ! p(12) = sow day (day of year)
    ! p(13) = phenological heat units required for emergence
    ! p(14) = growing season length - harvest day = sow day + this value (days)
    ! p(15) = canopy nitrogen dilution intercept (gN/m2)
    ! p(16) = canopy nitrogen dilution coefficient
    ! p(17) = leaf mass per area LMA (gC/m2)
    ! p(18) = initial labile C pool (gC/m2)
    ! p(19) = initial foliar C pool (gC/m2)
    ! p(20) = initial root C pool (gC/m2)
    ! p(21) = initial stem C pool (gC/m2)
    ! p(22) = initial litter C pool (gC/m2)
    ! p(23) = initial som C pool (gC/m2)
    ! p(24) = initial autotrophic pool C (gC/m2)
    ! p(25) = initial storage organ C pool (gC/m2)
    ! p(26) = minimum temperature for development (K)
    ! p(27) = maximum temperature for development (K)
    ! p(28) = optimum temperature for development (K)
    ! p(29) = minimum temperature for vernalisation (K)
    ! p(30) = maximum temperature for vernalisation (K)
    ! p(31) = optimum temperature for vernalisation (K)
    ! p(32) = critical photoperiod for development (hours)
    ! p(33) = photoperiod sensitivity
    ! p(34) = turnover rate of labile C (fraction/day)
    ! p(35) = turnover rate of autotrophic C (fraction/day)
    ! p(36) = root biomass for 50% of maximum rooting depth (gBiomass/m2)
    ! p(37) = maximum rooting depth (m)
    ! p(38) = initial soil water fraction (m3/m3)

    ! Set some initial states for the io variables
    infi = 0d0 ; FLUXES = 0d0 ; POOLS = 0d0 ; DIAGS = 0d0
    ! Reset hydrology variables
    mV%intercepted_rainfall = 0d0 ; mV%canopy_storage = 0d0 ; mV%snow_storage = 0d0
    transpiration = 0d0 ; soilevaporation = 0d0 ; wetcanopy_evap = 0d0 ; snowsublimation = 0d0
    ! Reset radiation variabes
    mV%canopy_swrad_MJday = 0d0 ; mV%canopy_par_MJday = 0d0 ; mV%soil_swrad_MJday = 0d0
    mV%canopy_lwrad_Wm2 = 0d0 ; mV%soil_lwrad_Wm2 = 0d0 ; mV%sky_lwrad_Wm2 = 0d0
    ! Reset conductance variables
    mV%soil_conductance = 0d0

    ! Generate some generic location specific variables for radiation balance
    !call calculate_radiation_commons(lat,pars(40:45))
    call calculate_radiation_commons(lat, mV)

    ! load ACM-GPP-ET parameters
    mV%NUE = pars(11)       ! Photosynthetic nitrogen use efficiency at optimum temperature (oC)
                         ! ,unlimited by CO2, light and photoperiod (gC/gN/m2leaf/day)
    mV%avN = pars(15) ! foliar N, initial value
    mV%ceff = mV%avN*mV%NUE

    ! plus ones being calibrated
    mV%root_k = pars(36) ; mV%max_depth = pars(37)

    ! parameters from file
    mV%decomposition_rate                = pars(1)  ! decomposition rate (day)
    mV%frac_GPP_resp_auto                = pars(2)  ! fraction of GPP allocated to autotrophic carbon pool
    mV%DR_pre                            = pars(3)  ! development rate coefficient DS (0->1)
    mV%DR_post                           = pars(4)  ! development rate coefficient DS (1->2)
    mV%turnover_rate_foliage             = pars(5)  ! turnover_rate of foliage (day)
    mV%turnover_rate_stem                = pars(6)  ! turnover rate of stem (day)
    mV%RDRSHMAX                          = pars(7)  ! maximum rate of foliar turnover due to self shading (day)
    mV%VDh                               = pars(8)  ! effective vernalisation days when plants are 50 % vernalised
    mV%VDh5                              = mV%VDh**5   ! precomputed 5th power of VDh
    mV%mineralisation_rate_litter        = pars(9)  ! mineralisation rate litter (day)
    mV%mineralisation_rate_soilOrgMatter = pars(10) ! mineralisation rate som (day)
    mV%sow_day                           = nint(mod(pars(12),365.25d0)) ! sow day (doy)
    mV%PHUem                             = pars(13) ! phenological heat units required for emergence
    mV%harvest_day                       = nint(mod(mV%sow_day + pars(14),365.25d0)) ! harvest day (doy)
    mV%plough_day                        = nint(mod(pars(12)-2d0,365.25d0)) ! plough day (doy)
    mV%LCA                               = pars(17) ! leaf mass area (gC.m-2)
    mV%tmin                              = pars(26)-273.15d0 ! min temperature for development
    mV%tmax                              = pars(27)-273.15d0 ! max temperature for development
    mV%topt                              = pars(28)-273.15d0 ! optimum temperature for development
    mV%tmin_v                            = pars(29)-273.15d0 ! min temperature for vernalisation
    mV%tmax_v                            = pars(30)-273.15d0 ! max temperature for vernalisation
    mV%topt_v                            = pars(31)-273.15d0 ! optimim temperature for vernalisation
    mV%doptmin   = mV%topt   - mV%tmin     ! difference between optimum and minimum cardinal temperatures
    mV%dmaxmin   = mV%tmax   - mV%tmin     ! difference between maximum and minimum cardinal temperatures
    mV%doptmin_v = mV%topt_v - mV%tmin_v   ! difference between optimum and minimum vernalisation temperatures
    mV%dmaxmin_v = mV%tmax_v - mV%tmin_v   ! difference between maximum and minimum vernalisation temperatures
    mV%PHCR                              = pars(32) ! critical value of photoperiod for development
    mV%PHSC                              = pars(33) ! photoperiod sensitivity
    mV%turnover_rate_labile              = pars(34) ! turnover rate labile C (day)
    mV%turnover_rate_resp_auto           = pars(35) ! turnover rate of autotrophic carbon for respiration (day)

    ! load stocks in first time step
    mV%stock_labile                      = pars(18) ! labile C
    mV%stock_foliage                     = pars(19) ! foliar C
    mV%stock_roots                       = pars(20) ! root C
    mV%stock_stem                        = pars(21) ! stem / wood C
    mV%stock_litter                      = pars(22) ! litter C
    mV%stock_soilOrgMatter               = pars(23) ! som C
    mV%stock_resp_auto                   = pars(24) ! autotrophic resp pool
    mV%stock_storage_organ               = pars(25) ! storage organ (i.e. desired crop)
    mV%stock_dead_foliage                = 0d0      ! dead but still standing foliage

    ! assigning initial conditions
    POOLS(1,1) = mV%stock_labile
    POOLS(1,2) = mV%stock_foliage
    POOLS(1,3) = mV%stock_roots
    POOLS(1,4) = mV%stock_stem
    POOLS(1,5) = mV%stock_litter
    POOLS(1,6) = mV%stock_soilOrgMatter
    POOLS(1,7) = mV%stock_resp_auto
    ! POOLS(1,8) ! WATER IN ROOT ZONE ASSIGNED LATER
    POOLS(1,9) = mV%stock_storage_organ
    POOLS(1,10) = mV%stock_dead_foliage

    ! logical switches
    mV%vernal_calcs    = .true.
    mV%ploughed        = .false.
    mV%sown            = .false.
    mV%use_seed_labile = .true.
    mV%emerged         = .false.

    ! pair incoming variables to local module levels
    ! finally set some initial conditions
    mV%yield = 0d0
    mV%DS = -1d0
    mV%DR = 0d0
    mV%fV = 0d0 ; mV%fT = 0d0 ; mV%fP = 0d0
    mV%alloc_to_storage_organ_old = 0d0
    mV%PHU = 0d0 ; mV%VD = 0d0
    mV%BM_EX = 0d0 ; mV%HI = 0d0
    mV%stock_dead_foliage = 0d0
    mV%alloc_to_labile = 0d0
    mV%stmob = 0
    mV%max_raso = 0d0
    mV%raso = 0d0
    mV%RDRDV = 0d0
    mV%max_raso_old = 0d0
    mV%raso_old  = 0d0
    mV%fol_frac_intpol = 0d0
    mV%stem_frac_intpol = 0d0
    mV%root_frac_intpol = 0d0

    ! SHOULD TURN THIS INTO A SUBROUTINE CALL AS COMMON TO BOTH DEFAULT AND CROPS
    if (.not.allocated(mV%deltat_1)) then 
      write(*,*) "Error - arrays not allocated - probably carbon_model() was called without initialize_mv()"
      STOP 1
    else ! deltat_1 allocated?

        ! Reset hydraulic properties
        mV%water_flux_mmolH2Om2s = 0d0
        mV%field_capacity = mV%field_capacity_initial
        mV%porosity = mV%porosity_initial

        ! input initial soil water fraction then
        ! update SWP and soil conductivity accordingly
        call update_soil_initial_conditions(pars(38), mV)

    endif

    ! load some needed module level values
    mV%lai = POOLS(1,2)/mV%LCA
    mV%mint = met(2,1)  ! minimum temperature (oC)
    mV%maxt = met(3,1)  ! maximum temperature (oC)
    mV%leafT = (mV%maxt*0.75d0) + (mV%mint*0.25d0) ! initial canopy temperature (oC)
    mV%swrad = met(4,1) ! incoming short wave radiation (MJ/m2/day)
    mV%co2 = met(5,1)   ! CO2 (ppm)
    mV%doy = ceiling(met(6,1)-(deltat(1)*0.5d0))   ! Day of year
    mV%rainfall = max(0d0,met(7,1)) ! rainfall (kgH2O/m2/s)
    mV%meant = (mV%maxt+mV%mint) * 0.5d0   ! mean air temperature (oC)
    mV%wind_spd = met(15,1) ! wind speed (m/s)
    mV%vpd_kPa = met(16,1)*1d-3  ! Vapour pressure deficit (Pa)

    ! Calculate solar declination for the current time step
    mV%declination = calculate_declination(mV%doy)
    ! calculate daylength in hours and seconds
    call calculate_daylength(mV)
    ! extract timing related values
    mV%dayl_hours_fraction = mV%dayl_hours * 0.04166667d0 ! 1/24 = 0.04166667
    mV%dayl_seconds_1 = mV%dayl_seconds**(-1d0)
    mV%seconds_per_step = seconds_per_day * deltat(1)
    mV%days_per_step = deltat(1) ; mV%days_per_step_1 = mV%deltat_1(1)

    ! calculate some temperature dependent meteorologial properties
    call meteorological_constants(mV%leafT,mV%leafT+freeze,mV%vpd_kPa, mV)

    ! initialise root reach based on initial conditions
    mV%fine_root_biomass = max(min_root,POOLS(1,3)*2d0)
    mV%root_biomass = mV%fine_root_biomass
    mV%root_reach = mV%max_depth * mV%root_biomass / (mV%root_k + mV%root_biomass)
    ! Determine initial soil layer thickness
    mV%layer_thickness(1) = top_soil_depth
    mV%layer_thickness(2) = max(min_layer,mV%root_reach-top_soil_depth)
    mV%layer_thickness(3) = mV%max_depth - sum(mV%layer_thickness(1:2))
    mV%layer_thickness(4) = top_soil_depth
    mV%previous_depth = max(top_soil_depth,mV%root_reach)
    ! needed to initialise soils
    call calculate_Rtot(mV)
    call calculate_update_soil_water(transpiration,soilevaporation,snowsublimation,&
                                     0d0,FLUXES(1,19), mV)  ! assume no evap or rainfall
    ! store soil water content of the rooting zone (mm)
    POOLS(1,8) = 1d3*mV%soil_waterfrac(1)*mV%layer_thickness(1)

    !
    ! Begin looping through each time step
    !

    do n = start, finish

      !!!!!!!!!!
      ! assign drivers and update some prognostic variables
      !!!!!!!!!!

      ! Incoming drivers
      mV%mint = met(2,n)  ! minimum temperature (oC)
      mV%maxt = met(3,n)  ! maximum temperature (oC)
      mV%leafT = (mV%maxt*0.75d0) + (mV%mint*0.25d0)     ! initial canopy temperature (oC)
      mV%swrad = met(4,n) ! incoming short wave radiation (MJ/m2/day)
      mV%co2 = met(5,n)   ! CO2 (ppm)
      mV%days_per_step = deltat(n) ; mV%days_per_step_1 = mV%deltat_1(n)
      mV%doy = ceiling(met(6,n)-(mV%days_per_step*0.5d0))   ! Day of year
      mV%rainfall = max(0d0,met(7,n)) ! rainfall (kgH2O/m2/s)
      mV%meant = met(14,n)   ! mean air temperature (oC)
      if (mV%mint > 0d0) then
          mV%airt_zero_fraction = 1d0
      else if (mV%maxt < 0d0) then
          mV%airt_zero_fraction = 0
      else
          mV%airt_zero_fraction = (mV%maxt-0d0) / (mV%maxt-mV%mint) ! fraction of temperture period above freezing
      end if
      mV%wind_spd = met(15,n) ! wind speed (m/s)
      mV%vpd_kPa = met(16,n)*1d-3  ! Vapour pressure deficit (Pa)

      ! states needed for module variables
      mV%lai = POOLS(n,2)/mV%LCA
      DIAGS(n,1) = mV%lai

      ! Calculate solar declination for the current time step
      mV%declination = calculate_declination(mV%doy)
      ! calculate daylength in hours and seconds
      call calculate_daylength(mV)
      ! extract timing related values
      mV%dayl_hours_fraction = mV%dayl_hours * 0.04166667d0 ! 1/24 = 0.04166667
      mV%dayl_seconds_1 = mV%dayl_seconds**(-1d0)
      mV%seconds_per_step = seconds_per_day * mV%days_per_step

      ! DS < 0.15 corresponds to the growth stage at beginning of the UK recommended period of
      ! N fertiliser application for winter wheat (Zodocks growth stage 20) - the early tillering stage (typically mid-march to April)
      if (mV%DS < 0.469d0) then
          mV%avN = pars(15)
      else! if (DS >= 0.469d0 .and. DS <= 1.293d0) then
          ! NOTE: The slope_n parameter can be included in the MDF optimisation.
          !       The value for this parameter has also been observed to be around -0.02.
          ! NOTE: Modified to only allow the dilution equation to dilute not enrich N content.
          !       This is to attempt to get around the N-dilultion model increasing N content
          !       during senescence which is unrealistic.
          ! NOTE: Dead foliage removed as only the remaining live foliage is photosynthetically active.
          !avN = max(0.1d0,min(avN,(pars(16)*(POOLS(n,2)+POOLS(n,10))) + pars(15)))
          mV%avN = max(0.1d0,min(mV%avN,(pars(16)*POOLS(n,2)) + pars(15)))
!      else
!          ! Set LNA to 0.1 after anthesis (Zodocks growth stage 75)
!          Non-applicable as we are explicitly tracking the continued live leaf area and the dead
!          avN = 0.1d0
      end if
      ! Update to canopy efficiency (gC/m2leaf/day)
      mV%ceff = mV%avN * mV%NUE
      ! Update output to DIAGS the average foliar N (gN/m2)
      DIAGS(n,16) = mV%avN


      !!!!!!!!!!
      ! Adjust snow balance balance based on temperture
      !!!!!!!!!!

      ! snowing or not...?
      if (((mV%mint + mV%maxt) * 0.5d0) > 0d0) then
          ! on average above freezing so no snow
          mV%snowfall = 0d0
      else
          ! on average below freezing, so some snow based on proportion of temperture
          ! below freezing
          mV%snowfall = mV%rainfall * (1d0 - mV%airt_zero_fraction) ; mV%rainfall = mV%rainfall - mV%snowfall
          ! Add rainfall to the snowpack and clear rainfall variable
          mV%snow_storage = mV%snow_storage + (mV%snowfall*mV%seconds_per_step)
      end if

      ! melting or not...?
      if (mV%mint < 0d0 .and. mV%maxt > 0d0) then
          ! Also melt some of the snow based on airt_zero_fraction
          ! default assumption is that snow is melting at 10 % per day hour above freezing
          mV%snow_melt = min(mV%snow_storage, mV%airt_zero_fraction * mV%snow_storage * 0.1d0 * mV%days_per_step)
          mV%snow_storage = mV%snow_storage - mV%snow_melt
          ! adjust to rate for later addition to rainfall
          mV%snow_melt = mV%snow_melt / mV%seconds_per_step
      elseif (mV%maxt < 0d0) then
          mV%snow_melt = 0d0
      else if (mV%mint > 0d0 .and. mV%snow_storage > 0d0) then
          ! otherwise we assume snow is melting at 10 % per day above hour
          mV%snow_melt = min(mV%snow_storage, mV%snow_storage * 0.1d0 * mV%days_per_step)
          mV%snow_storage = mV%snow_storage - mV%snow_melt
          ! adjust to rate for later addition to rainfall
          mV%snow_melt = mV%snow_melt / mV%seconds_per_step
      else
          mV%snow_melt = 0d0
      end if
      DIAGS(n,2) = mV%snow_storage

      !!!!!!!!!!
      ! Calculate surface exchange coefficients
      !!!!!!!!!!

      ! calculate some temperature dependent meteorologial properties
      call meteorological_constants(mV%leafT,mV%leafT+freeze,mV%vpd_kPa, mV)
      ! pass variables from memory objects
      mV%convert_ms1_mmol_1 = mV%convert_ms1_mol_1 * 1d3
      ! calculate aerodynamic using consistent approach with SPA
      call calculate_aerodynamic_conductance(mV)
      ! Canopy scale aerodynamic conductance (mmolH2O/m2ground/s)
      DIAGS(n,6) = mV%aerodynamic_conductance * mV%convert_ms1_mmol_1 * &
                   mV%leaf_canopy_wind_scaling
      DIAGS(n,14) = mV%leaf_canopy_wind_scaling ! canopy area scaling as a function of wind profiles

      !!!!!!!!!!
      ! Determine net shortwave and isothermal longwave energy balance
      !!!!!!!!!!

      call calculate_radiation_balance(mV)
      DIAGS(n,3) = mV%canopy_par_MJday ! Absorbed PAR by canopy (MJ/m2ground/day)
      DIAGS(n,15) = mV%leaf_canopy_light_scaling ! canopy area scaling as a function of light profiles

      !!!!!!!!!!
      ! Calculate physically constrained evaporation and
      ! soil water potential and total hydraulic resistance
      !!!!!!!!!!

      ! Canopy intercepted rainfall evaporation (kgH2O/m2/day)
      if (mV%lai > 0d0) then ! is this conditional needed?
          call calculate_wetcanopy_evaporation(wetcanopy_evap,mV%canopy_storage, mV)
      else
          ! reset pools
          mV%intercepted_rainfall = 0d0 ; mV%canopy_storage = 0d0 ; wetcanopy_evap = 0d0
      endif

      ! calculate the minimum soil & root hydraulic resistance based on total
      ! fine root mass ! *2*2 => *RS*C->Bio
      mV%fine_root_biomass = max(min_root,POOLS(n,3)*2d0)
      mV%root_biomass = mV%fine_root_biomass
      call calculate_Rtot(mV)
      ! Pass root~water~soil variables to output variable
      DIAGS(n,8) = mV%root_reach ! Rooting depth (m)
      DIAGS(n,10) = mV%wSWP      ! Soil water potential weighted by supply of water
      DIAGS(n,11) = mV%rSWP      ! Soil water potential weighted by access water
      DIAGS(n,12) = mV%Reff      ! Effective hydraulic resistance MPa.s.m2.mmol-1 H20

      ! calculate radiation absorption and estimate stomatal conductance
      call calculate_stomatal_conductance(mV)
      ! Estimate stomatal conductance relative to its minimum / maximum, i.e. how
      ! close are we to maxing out supply (note 0.01 taken from min_gs)
      DIAGS(n,7) = (mV%stomatal_conductance  - mV%minimum_conductance) &
                 / (mV%potential_conductance - mV%minimum_conductance)
      ! Store the canopy level stomatal conductance (mmolH2O/m2ground/s)
      DIAGS(n,5) = mV%stomatal_conductance

      ! adjustments

      ! Reset output variable
      if (mV%stomatal_conductance > vsmall) then
          ! Gross primary productivity (gC/m2/day)
          ! Assumes acm_gpp_stage_1 ran as part of stomatal conductance calculation
          FLUXES(n,1) = acm_gpp_stage_2(mV%stomatal_conductance, mV) * umol_to_gC * mV%dayl_seconds
          ! Estimate the ratio of leaf internal to ambient CO2 concentrations
          DIAGS(n,4) = mV%ci / mV%co2
          ! Canopy transpiration (kgH2O/m2/day)
          call calculate_transpiration(transpiration, mV)
          ! restrict transpiration to positive only
          transpiration = max(0d0,transpiration)
      else
          ! assume zero fluxes
          FLUXES(n,1) = 0d0 ; transpiration = 0d0 ; DIAGS(n,4) = 0d0
      endif
      ! Pass GPP estimate to module variable for use in crop development model
      mV%gpp_acm = FLUXES(n,1)

      ! Estimate average leaf water potential (MPa) based on effective hydraulic resistance, wSWP and transpiration.
      ! Positive LWPs can be estimated given very small gs and cold temperatures.
      ! Debugging print statements
      !print*,"Estimate LWP"
      !LWP = SWP(1:nos_root_layers) - head*canopy_height &
      !     - transpiration*uptake_fraction(1:nos_root_layers) &
      !     * (dayl_seconds_1/mmol_to_kg_water)/Rcond_layer(1:nos_root_layers)
      DIAGS(n,9) =  min(0d0, mV%wSWP - (head*canopy_height) - (((transpiration*mV%dayl_seconds_1)/mmol_to_kg_water) * mV%Reff))

      ! calculate weighted air temperature value based on daily minimum, maximum
      ! and means. This minimises the error introduced when scaling between
      ! daily and sub-daily timesteps
      !airt_weighting(1) = abs(met(3,n)-meant) / (met(3,n)-met(2,n))*0.5d0 ! maximum temperature weighting
      !airt_weighting(2) = 0.5d0                                            ! mean temperature
      !airt_weighting(3) = abs(met(2,n)-meant) / (met(3,n)-met(2,n))*0.5d0 ! minimum temperature weighting

      ! Heterotrophic respiration rate (Q10):  doubles with
      ! 10 degree temperature rise resprate from soil file = 0.0693
      !resp_rate = 0d0
      !resp_rate = resp_rate + ((0.5d0 * exp( resp_rate_temp_coeff * met(3,n) )) * airt_weighting(1))
      !resp_rate = resp_rate + ((0.5d0 * exp( resp_rate_temp_coeff * meant   )) * airt_weighting(2))
      !resp_rate = resp_rate + ((0.5d0 * exp( resp_rate_temp_coeff * met(2,n) )) * airt_weighting(3))
      mV%resp_rate = 0.5d0 * exp( resp_rate_temp_coeff * mV%meant )

      ! reallocate day of year to the end of the time step for use in
      ! crop development model
      mV%doy = met(6,n)
      ! determine development stage (DS)
      ! Note that DS must be updated here and not after management_dates. Otherwise,
      ! there will be problems using DS_time for calculating yield:GPP calculations in MODEL_LIKELIHOOD.f90
      call development_stage(mV%days_per_step, mV) ; DIAGS(n,13) = mV%DS
      ! determine the carbon partitioning based on development stage
      call carbon_alloc_fractions(CI%DS_shoot,CI%DS_root,CI%fol_frac,CI%stem_frac,CI%root_frac, mV)
      ! begin carbon allocation for crops
      call calc_pools_crops(CI%DS_LRRT,CI%LRRT, mV)
      ! conduct management updates at the end of the day
      call management_dates(CI%stock_seed_labile,mV%days_per_step, mV)

      ! temprate (i.e. temperature modified rate of metabolic activity)
      FLUXES(n,2) = mV%resp_rate
      ! autotrophic respiration (gC.m-2.d-1)
      FLUXES(n,3) = mV%resp_auto + mV%resp_cost_labile_to_npp + &
                    mV%resp_cost_npp_to_labile + mV%resp_cost_foliage_to_labile + &
                    mV%resp_cost_stem_to_labile
      ! leaf production rate (gC.m-2.d-1)
      FLUXES(n,4) = mV%alloc_to_foliage
      ! labile production (gC.m-2.d-1)
      FLUXES(n,5) = mV%alloc_to_labile + mV%foliage_to_labile + mV%stem_to_labile
      ! root production (gC.m-2.d-1)
      FLUXES(n,6) = mV%alloc_to_roots
      ! stem production (gC.m-2.d-1)
      FLUXES(n,7) = mV%alloc_to_stem
      ! labile from NPP (gC.m-2.d-1)
      FLUXES(n,8) = mV%alloc_from_labile
      ! alloc to storage organ (gC.m-2.d-1)
      FLUXES(n,9) = mV%alloc_to_storage_organ
      ! total leaf litter production (gC.m-2.d-1)
      FLUXES(n,10) = mV%litterfall_foliage
      ! total stem litter production (gC.m-2.d-1)
      FLUXES(n,11) = mV%litterfall_stem
      ! total root litter production (gC.m-2.d-1)
      FLUXES(n,12) = mV%litterfall_roots
      ! respiration heterotrophic litter (gC.m-2.d-1)
      FLUXES(n,13) = mV%resp_h_litter
      ! respiration heterotrophic som (gC.m-2.d-1)
      FLUXES(n,14) = mV%resp_h_soilOrgMatter
      ! litter to som (gC.m-2.d-1)
      FLUXES(n,15) = mV%decomposition
      ! alloc to autotrophic pool (gC.m-2.d-1)
      FLUXES(n,16) = mV%alloc_to_resp_auto
      ! harvest yield (gC.m-2.d-1)
      FLUXES(n,21) = mV%yield
      ! C extracted in addition to yield due to harvest activity (gC.m-2.d-1)
      FLUXES(n,22) = mV%BM_EX
      ! Respiration from autotrophic allocation (gC.m-2.d-1)
      FLUXES(n,23) = mV%resp_auto
      ! Respiration from labile to foliage translocation (gC.m-2.d-1)
      FLUXES(n,24) = mV%resp_cost_labile_to_npp
      ! Respiration from NPP to labile translocation (gC.m-2.d-1)
      FLUXES(n,25) = mV%resp_cost_npp_to_labile
      ! Respiration from foliage remobilisation (gC.m-2.d-1)
      FLUXES(n,26) = mV%resp_cost_foliage_to_labile
      ! Respiration from stem remobilisation (gC.m-2.d-1)
      FLUXES(n,27) = mV%resp_cost_stem_to_labile
      ! Foliage extracted from harvest (gC.m-2.d-1)
      FLUXES(n,28) = mV%HARVESTextracted_foliage
      ! Stem extracted from harvest (gC.m-2.d-1)
      FLUXES(n,29) = mV%HARVESTextracted_stem
      ! Dead still standing foliage extracted from harvest (gC.m-2.d-1)
      FLUXES(n,30) = mV%HARVESTextracted_dead_foliage
      ! Labile extracted from harvest (gC.m-2.d-1)
      FLUXES(n,31) = mV%HARVESTextracted_labile
      ! Foliage added to litter due to harvest (gC.m-2.d-1)
      FLUXES(n,32) = mV%HARVESTlitter_foliage
      ! Stem added to litter due to harvest (gC.m-2.d-1)
      FLUXES(n,33) = mV%HARVESTlitter_stem
      ! Dead still standing foliage added to litter due to harvest (gC.m-2.d-1)
      FLUXES(n,34) = mV%HARVESTlitter_dead_foliage
      ! Autotrophic pool added to litter due to harvest (gC.m-2.d-1)
      FLUXES(n,35) = mV%HARVESTlitter_resp_auto
      ! Labile added to litter due to harvest (gC.m-2.d-1)
      FLUXES(n,36) = mV%HARVESTlitter_labile
      ! Roots pool added to litter due to plough (gC.m-2.d-1)
      FLUXES(n,37) = mV%PLOUGHlitter_roots

      ! add any snow melt to the rainfall now that we have already dealt with the canopy interception
      mV%rainfall = mV%rainfall + mV%snow_melt
      ! do mass balance (i.e. is there enough water to support ET)
      call calculate_update_soil_water(transpiration,soilevaporation,snowsublimation, &
                                      ((mV%rainfall-mV%intercepted_rainfall)*seconds_per_day) &
                                      ,FLUXES(n,19), mV)
      ! now that soil mass balance has been updated we can add the wet canopy
      ! evaporation (kg.m-2.day-1)
      FLUXES(n,19) = FLUXES(n,19) + wetcanopy_evap
      ! store soil water content of surface (mm)
      POOLS(n+1,8) = 1d3*mV%soil_waterfrac(1)*mV%layer_thickness(1)
      ! Assign all water variables to output variables (kgH2O/m2/day)
      FLUXES(n,38) = transpiration   ! transpiration (kgH2O/m2/day)
      FLUXES(n,39) = soilevaporation ! soil evaporation (kgH2O/m2/day)
      FLUXES(n,40) = wetcanopy_evap  ! wet canopy evaporation (kgH2O/m2/day)
      FLUXES(n,41) = mV%runoff          ! soil surface runoff (kgH2O/m2/day)
      FLUXES(n,42) = mV%underflow       ! drainage from bottom of soil column (kgH2O/m2/day)
      FLUXES(n,43) = mV%water_grav_flow(1) ! drainage from the surface soil layer to 2nd (kgH2O/m2/day)
      FLUXES(n,44) = mV%infiltrated(1)  ! top soil surface infiltration by rain (kgH2O/m2/day)
      FLUXES(n,47) = mV%infiltrated(2)  ! middle soil surface infiltration by rain (kgH2O/m2/day)
      FLUXES(n,48) = mV%infiltrated(3)  ! bottom soil surface infiltration by rain (kgH2O/m2/day)
      FLUXES(n,45) = mV%uptake_fraction(1) ! transpiration fraction extracted from 1st rooting layer (the soil surface)
      FLUXES(n,46) = mV%uptake_fraction(2) ! transpiration fraction extracted from 2nd rooting layer (dynamic 2nd layer)

      ! labile pool
      POOLS(n+1,1) = mV%stock_labile
      ! foliar pool
      POOLS(n+1,2) = mV%stock_foliage
      ! root pool
      POOLS(n+1,3) = mV%stock_roots
      ! stem pool
      POOLS(n+1,4) = mV%stock_stem
      ! litter pool
      POOLS(n+1,5) = mV%stock_litter
      ! som pool
      POOLS(n+1,6) = mV%stock_soilOrgMatter
      ! autotrophic pool
      POOLS(n+1,7) = mV%stock_resp_auto
      ! POOLS(n+1,8) = soil surface water content
      ! storage organ pool
      POOLS(n+1,9) = mV%stock_storage_organ
      ! dead but still standing foliage
      POOLS(n+1,10) = mV%stock_dead_foliage

!      do nxp = 1, nopools
!         if (POOLS(n+1,nxp) /= POOLS(n+1,nxp) .or. POOLS(n+1,nxp) < 0d0) then
!             print*,"step",n,"POOL",nxp
!             print*,"met",met(:,n)
!             print*,"POOLS",POOLS(n,:)
!             print*,"FLUXES",FLUXES(n,:)
!             print*,"POOLS+1",POOLS(n+1,:)
!             print*,"wSWP",wSWP
!             print*,"waterfrac",soil_waterfrac
!             print*,"labile, foliage",stock_labile, stock_foliage
!             print*,"stem, roots",stock_stem,stock_roots
!             print*,"litter, som",stock_litter,stock_soilOrgMatter
!             print*,"storageOrgan, auto",stock_storage_organ,stock_resp_auto
!             print*,"gpp, nee",gpp_acm,nee_dalec
!             print*,"Ra, Rh_som, Rh_lit",resp_auto,resp_h_soilOrgMatter,resp_h_litter
!             print*,"pars",pars
!             print*,"DR",DR
!             print*,"DR stuff",fT,fV,fP
!             print*,"leaf","stem","rootlitter",litterfall_foliage,litterfall_stem,litterfall_roots
!             print*,"daylength",dayl_hours,"VD",VD,"VDh",VDh
!             print*,"meant",meant
!             print*,"sown",sown,"emerged",emerged
!             print*,"root_frac_intpol",root_frac_intpol
!             print*,"npp_shoot",npp_shoot,"npp",npp
!             print*,"RDR",RDR
!             !stop
!         endif
!     enddo
!
!      do nxp = 1, nofluxes
!         if (nxp /= 19 .and. nxp /= 38 .and. nxp /= 39 .and. nxp /= 40 .and. nxp /= 41) then
!            if (FLUXES(n,nxp) /= FLUXES(n,nxp) .or. FLUXES(n,nxp) < 0d0) then
!                 print*,"Special: step",n,"FLUXES",nxp
!                 print*,"met",met(:,n)
!                 print*,"POOLS",POOLS(n,:)
!                 print*,"FLUXES",FLUXES(n,:)
!                 print*,"POOLS+1",POOLS(n+1,:)
!                 print*,"wSWP",wSWP
!                 print*,"waterfrac",soil_waterfrac
!                 print*,"labile, foliage",stock_labile, stock_foliage
!                 print*,"stem, roots",stock_stem,stock_roots
!                 print*,"litter, som",stock_litter,stock_soilOrgMatter
!                 print*,"storageOrgan, auto",stock_storage_organ,stock_resp_auto
!                 print*,"gpp, nee",gpp_acm,nee_dalec
!                 print*,"Ra, Rh_som, Rh_lit",resp_auto,resp_h_soilOrgMatter,resp_h_litter
!                 print*,"pars",pars
!                 print*,"DR",DR
!                 print*,"DR stuff",fT,fV,fP
!                 print*,"leaf","stem","rootlitter",litterfall_foliage,litterfall_stem,litterfall_roots
!                 print*,"daylength",dayl_hours,"VD",VD,"VDh",VDh
!                 print*,"meant",meant
!                 print*,"sown",sown,"emerged",emerged
!                 print*,"root_frac_intpol",root_frac_intpol
!                 print*,"npp_shoot",npp_shoot,"npp",npp
!                 print*,"RDR",RDR
!                 !stop
!            end if
!         else
!             if (FLUXES(n,nxp) /= FLUXES(n,nxp)) then
!                 print*,"Default: step",n,"FLUXES",nxp
!                 print*,"met",met(:,n)
!                 print*,"POOLS",POOLS(n,:)
!                 print*,"FLUXES",FLUXES(n,:)
!                 print*,"POOLS+1",POOLS(n+1,:)
!                 print*,"wSWP",wSWP
!                 print*,"waterfrac",soil_waterfrac
!                 print*,"labile, foliage",stock_labile, stock_foliage
!                 print*,"stem, roots",stock_stem,stock_roots
!                 print*,"litter, som",stock_litter,stock_soilOrgMatter
!                 print*,"storageOrgan, auto",stock_storage_organ,stock_resp_auto
!                 print*,"gpp, nee",gpp_acm,nee_dalec
!                 print*,"Ra, Rh_som, Rh_lit",resp_auto,resp_h_soilOrgMatter,resp_h_litter
!                 print*,"pars",pars
!                 print*,"DR",DR
!                 print*,"DR stuff",fT,fV,fP
!                 print*,"leaf","stem","rootlitter",litterfall_foliage,litterfall_stem,litterfall_roots
!                 print*,"daylength",dayl_hours,"VD",VD,"VDh",VDh
!                 print*,"meant",meant
!                 print*,"sown",sown,"emerged",emerged
!                 print*,"root_frac_intpol",root_frac_intpol
!                 print*,"npp_shoot",npp_shoot,"npp",npp
!                 print*,"RDR",RDR
!                 !stop
!             endif
!         end if
!      enddo
!
!      if (stock_labile < 0d0 .or. stock_foliage < 0d0 .or. stock_stem < 0d0 .or. &
!          stock_roots < 0d0 .or. stock_litter < 0d0 .or. stock_soilOrgMatter < 0d0 .or. &
!          stock_storage_organ < 0d0 .or. stock_resp_auto < 0d0 .or. &
!          stock_labile /= stock_labile .or. stock_foliage /= stock_foliage .or. &
!         stock_stem /= stock_stem .or. &
!          stock_roots /= stock_roots .or. stock_litter /= stock_litter .or. &
!          stock_soilOrgMatter /= stock_soilOrgMatter .or. &
!          stock_storage_organ /= stock_storage_organ .or. &
!          stock_resp_auto /= stock_resp_auto .or.  &
!          gpp_acm < 0d0 .or. gpp_acm /= gpp_acm .or. resp_rate < 0d0 .or. &
!          resp_rate /= resp_rate .or. decomposition < 0d0 .or. alloc_from_labile < 0d0 .or. &
!          resp_cost_labile_to_npp < 0d0 .or. alloc_to_foliage < 0d0 .or. &
!          alloc_to_stem < 0d0 .or. alloc_to_roots < 0d0 .or. &
!          alloc_from_labile < 0d0 .or. resp_cost_labile_to_npp < 0d0 .or. wSWP /= wSWP) then
!          print*,"stocks less than zero or NaN", n
!          print*,stock_labile, stock_foliage
!          print*,stock_stem,stock_roots
!          print*,stock_litter,stock_soilOrgMatter
!          print*,stock_storage_organ,stock_resp_auto
!         print*,gpp_acm,nee_dalec
!         print*,resp_auto,resp_h_soilOrgMatter,resp_h_litter
!          print*,"pars",pars(1:33)
!          print*,"fluxes",fluxes(n,1:16)
!          print*,"DR",DR
!          print*,"DR stuff",fT,fV,fP
!          print*,"leaf","stem","rootlitter",litterfall_foliage,litterfall_stem,litterfall_roots
!          print*,"daylength",dayl_hours,"VD",VD,"VDh",VDh
!          print*,"meant",meant
!          print*,"sown",sown,"emerged",emerged
!          print*,"root_frac_intpol",root_frac_intpol
!          print*,"npp_shoot",npp_shoot,"npp",npp
!          print*,"RDR",RDR
!          !stop
!      endif

    end do ! no days loop

  end subroutine CARBON_MODEL
  !
  !------------------------------------------------------------------
  !
  subroutine acm_gpp_stage_1 (mV)

    ! Estimate the light and temperature limited photosynthesis components.
    ! See acm_gpp_stage_2() for estimation of CO2 supply limitation and
    ! combination of light, temperature and CO2 co-limitation

    implicit none

      type(model_working_variables) :: mV

    ! Declare local variables
    double precision :: a, b, c, Pl_max, PAR_m2, airt_adj

    !
    ! Metabolic limited photosynthesis
    !

    ! maximum rate of temperature and nitrogen (canopy efficiency) limited
    ! photosynthesis (gC.m-2.day-1 -> umolC/m2/s). Scaling from leaf to canopy
    ! scaled assumed to follow integral of light environment.
    mV%metabolic_limited_photosynthesis = gC_to_umol*mV%leaf_canopy_light_scaling*mV%ceff*seconds_per_day_1 &
                                     * ((mV%leafT - Vc_minT) / ((mV%leafT - Vc_minT) + Vc_coef))         &
                                     * opt_max_scaling(pn_max_temp,pn_min_temp,pn_opt_temp,pn_kurtosis,mV%leafT)

    !
    ! Light limited photosynthesis
    !

    ! Calculate light limted rate of photosynthesis (umolC.m-2.s-1, daylight) as a function
    ! light capture and leaf to canopy scaling on quantum yield (e0).
    mV%light_limited_photosynthesis = e0 * mV%canopy_par_MJday * mV%dayl_seconds_1 * gC_to_umol

    !
    ! Stomatal conductance independent variables for diffusion limited
    ! photosynthesis
    !

    ! Canopy level boundary layer conductance unit change
    ! (m.s-1 -> mol.m-2.s-1) assuming sea surface pressure only.
    ! Note the ratio of H20:CO2 diffusion through leaf level boundary layer is
    ! 1.37 (Jones appendix 2). Note conversion to resistance for easiler merging
    ! with stomatal conductance in acm_gpp_stage_2).
    mV%rb_mol_1 = 1d0 / (mV%aerodynamic_conductance * mV%convert_ms1_mol_1 * gb_H2O_CO2 * &
                mV%leaf_canopy_wind_scaling)

    ! Arrhenious Temperature adjustments for Michaelis-Menten coefficients
    ! for CO2 (kc) and O2 (ko) and CO2 compensation point
    ! See McMurtrie et al., (1992) Australian Journal of Botany, vol 40, 657-677
    mV%co2_half_sat   = arrhenious(kc_half_sat_25C,kc_half_sat_gradient,mV%leafT)
    mV%co2_comp_point = arrhenious(co2comp_sat_25C,co2comp_gradient,mV%leafT)

    ! don't forget to return
    return

  end subroutine acm_gpp_stage_1
  !
  !------------------------------------------------------------------
  !
  double precision function acm_gpp_stage_2(gs, mV)

    ! Combine the temperature (pn) and light (pl) limited gross primary productivity
    ! estimates with CO2 supply limited via stomatal conductance (gs).
    ! See acm_gpp_stage_1() for additional details on pn and pl calculation.

    implicit none

      type(model_working_variables) :: mV

    ! declare input variables
    double precision, intent(in) :: gs

    ! declare local variables
    double precision :: pp, qq, mult, rc, pd

    !
    ! Combined diffusion limitation and carboxylation limited photosynthesis
    !

    ! Estimation of ci is based on the assumption that metabilic limited
    ! photosynthesis is equal to diffusion limited. For details
    ! see Williams et al, (1997), Ecological Applications,7(3), 1997, pp. 882â€“894

    ! Daily canopy conductance dertmined through combination of aerodynamic and
    ! stomatal conductances. Both conductances are scaled to canopy aggregate.
    ! aerodynamic conductance already in units of molCO2.m-2.s-1 (see acm_gpp_stage_1).
    ! Stomatal conductance scaled from mmolH2O to molCO2.
    ! The ratio of H20:CO2 diffusion is 1.646259 (Jones appendix 2).
    !
    ! Combining in series the stomatal and boundary layer conductances
    ! to make canopy resistence (s/m2/molCO2)
    rc = 1d0 / (gs*gs_H2Ommol_CO2mol) + mV%rb_mol_1

    ! pp and qq represent limitation by metabolic (temperature & N) and
    ! diffusion (co2 supply) respectively
    pp = mV%metabolic_limited_photosynthesis*rc ; qq = mV%co2_comp_point-mV%co2_half_sat
    mult = mV%co2+qq-pp
    ! calculate internal CO2 concentration (ppm or umol/mol)
    mV%ci = 0.5d0*(mult+sqrt((mult*mult)-4d0*(mV%co2*qq-pp*mV%co2_comp_point)))

    ! calculate CO2 limited rate of photosynthesis (umolC.m-2.s-1)
    pd = ((mV%co2-mV%ci)/rc)

    !
    ! Estimate CO2 and light co-limitation
    !

    ! calculate combined light and CO2 limited photosynthesis (umolC/m2/s)
    acm_gpp_stage_2 = mV%light_limited_photosynthesis*pd/(mV%light_limited_photosynthesis+pd)

    ! Estimate ci as a function of the final combined GPP estimate
    pp = acm_gpp_stage_2*rc ; mult = mV%co2+qq-pp
    ! calculate internal CO2 concentration (ppm or umol/mol)
    mV%ci = 0.5d0*(mult+sqrt((mult*mult)-4d0*(mV%co2*qq-pp*mV%co2_comp_point)))

    ! sanity check
    if (acm_gpp_stage_2 /= acm_gpp_stage_2) then
        acm_gpp_stage_2 = 0d0 ; mV%ci = 0d0
    end if

    ! don't forget to return
    return

  end function acm_gpp_stage_2
  !
  !------------------------------------------------------------------
  !
  double precision function find_gs_iWUE(gs_in, mV)

    ! Calculate CO2 limited photosynthesis as a function of metabolic limited
    ! photosynthesis (pn), atmospheric CO2 concentration and stomatal
    ! conductance (gs_in). Photosynthesis is calculated twice to allow for
    ! testing of senstivity to iWUE.

    ! arguments
    double precision, intent(in) :: gs_in

      type(model_working_variables) :: mV

    !!!!!!!!!!
    ! Optimise intrinsic water use efficiency
    !!!!!!!!!!

    ! Determine impact of gs increment on pd and how far we are from iWUE
    find_gs_iWUE = mV%iWUE_step - (acm_gpp_stage_2(gs_in + mV%delta_gs, mV) - acm_gpp_stage_2(gs_in, mV))

    ! Remember to return back to the user
    return

  end function find_gs_iWUE
  !
  !----------------------------------------------------------------------
  !
  subroutine calculate_stomatal_conductance (mV)

    use brent_zero, only: zbrent

    ! Determines an approximation of canopy scale stomatal conductance (gc)
    ! mmolH2O.m-2.s-1 based on potential hydraulic flow, air temperature and absorbed radiation.

    implicit none

      type(model_working_variables) :: mV

    ! local variables
    double precision :: denom, iWUE_upper!, iWUE_lower
    double precision, parameter :: max_gs = 1000d0, &  ! mmolH2O.m-2.s-1 (leaf area)
                                   min_gs = 0.01d0, &  ! mmolH2O.m-2.s-1 (leaf area)
                                   tol_gs = 0.01d0     ! mmolH2O.m-2.s-1 (leaf area)

    !!!!!!!!!!
    ! Calculate stomatal conductance under H2O and CO2 limitations
    !!!!!!!!!!

    if (mV%aerodynamic_conductance > vsmall .and. mV%total_water_flux > vsmall .and. &
        mV%leafT > Vc_minT .and. mV%leaf_canopy_light_scaling > vsmall) then

        ! Pass minimum conductance from local parameter to global value
        mV%minimum_conductance = min_gs * mV%leaf_canopy_light_scaling

        ! Invert Penman-Monteith equation to give gs (m.s-1) needed to meet
        ! maximum possible evaporation for the day.
        ! This will then be reduced based on CO2 limits for diffusion based
        ! photosynthesis
        denom = mV%slope * (((mV%canopy_swrad_MJday * 1d6 * mV%dayl_seconds_1) + mV%canopy_lwrad_Wm2)) &
              + (mV%ET_demand_coef * mV%aerodynamic_conductance * mV%leaf_canopy_wind_scaling)
        denom = (denom / (mV%lambda * mV%total_water_flux * mmol_to_kg_water)) - mV%slope
        mV%potential_conductance = (mV%aerodynamic_conductance * mV%leaf_canopy_wind_scaling) / (denom / mV%psych)

        ! convert m.s-1 to mmolH2O.m-2.d-1, per unit ground area, note that this
        ! is implicitly the canopy scaled value
        mV%potential_conductance = mV%potential_conductance * mV%convert_ms1_mmol_1
        ! if conditions are dew forming then set conductance to maximum as we
        ! are not going to be limited by water demand
        if (mV%potential_conductance <= 0d0 .or. mV%potential_conductance > max_gs*mV%leaf_canopy_light_scaling) then
            mV%potential_conductance = max_gs*mV%leaf_canopy_light_scaling
        end if

        ! If there is a positive demand for water then we will solve for
        ! photosynthesis limits on gs through iterative solution

        ! Determine the appropriate canopy scaled gs increment and return threshold
        mV%delta_gs = 1d0 * mV%leaf_canopy_light_scaling ! mmolH2O/m2leaf/s
        mV%iWUE_step = iWUE * mV%leaf_canopy_light_scaling ! umolC/mmolH2Ogs/s

        ! Calculate stage one acm, temperature and light limitation which
        ! are independent of stomatal conductance effects
        call acm_gpp_stage_1(mV)

        ! Intrinsic WUE optimisation
        ! Check that the water restricted water range brackets the root solution for the bisection
        iWUE_upper = find_gs_iWUE(mV%potential_conductance,mV) !; iWUE_lower = find_gs_iWUE(min_gs,mV)
        if ( iWUE_upper * find_gs_iWUE(min_gs,mV) > 0d0 ) then
            ! Then both proposals indicate that photosynthesis
            ! would be increased by greater opening of the stomata
            ! and is therefore water is limiting!
            mV%stomatal_conductance = mV%potential_conductance
            ! Exception being if both are positive - therefore assume
            ! lowest
            if (iWUE_upper > 0d0) mV%stomatal_conductance = mV%minimum_conductance
        else if (mV%potential_conductance < mV%minimum_conductance) then
            ! If the potential conductance is less than the hardcoded minimum
            ! assume stomatal conductance is the minimum and move on.
            mV%stomatal_conductance = mV%minimum_conductance
        else
            ! In all other cases iterate
            mV%stomatal_conductance = zbrent('calculate_gs:find_gs_iWUE', &
                                          find_gs_iWUE_,mV%minimum_conductance,mV%potential_conductance,tol_gs*mV%lai,mV%iWUE_step*0.10d0)
        end if ! iWUE_upper * find_gs_iWUE(min_gs) > 0d0

    else ! if aerodynamic conductance > vsmall

        ! if no LAI then there can be no stomatal conductance
        mV%potential_conductance = max_gs ; mV%minimum_conductance = vsmall
        mV%stomatal_conductance = vsmall

    endif ! if aerodynamic conductance > vsmall

    contains 
    double precision function find_gs_iWUE_(x) 
      double precision, intent(in):: x 
      find_gs_iWUE_ = find_gs_iWUE(x, mV)
    end function

  end subroutine calculate_stomatal_conductance
  !
  !------------------------------------------------------------------
  !
  subroutine meteorological_constants(input_temperature,input_temperature_K,input_vpd_kPa, mV)

    ! Determine some multiple use constants used by a wide range of functions
    ! All variables here are linked to air temperature and thus invarient between
    ! iterations and can be stored in memory...

    implicit none

      type(model_working_variables) :: mV

    ! arguments
    double precision, intent(in) :: input_temperature, input_temperature_K, &
                                    input_vpd_kPa

    ! local variables
    double precision :: mult, &
              dynamic_viscosity    ! dynamic viscosity (kg.m-2.s-1)

    !
    ! Used for soil, canopy evaporation and transpiration
    !

    ! Density of air (kg/m3)
    mV%air_density_kg = 353d0/input_temperature_K
    ! Conversion ratio for m.s-1 -> mol.m-2.s-1
    mV%convert_ms1_mol_1 = const_sfc_pressure / (input_temperature_K*Rcon)
    ! latent heat of vapourisation,
    ! function of air temperature (J.kg-1)
    mV%lambda = 2501000d0-2364d0*input_temperature

    ! psychrometric constant (kPa K-1)
    mV%psych = (0.0646d0*exp(0.00097d0*input_temperature))
    ! Straight line approximation of the true slope; used in determining
    ! relationship slope
    mult = input_temperature+237.3d0
    ! 2502.935945 = 0.61078*17.269*237.3
    ! Rate of change of saturation vapour pressure with temperature (kPa.K-1)
    mV%slope = (2502.935945d0*exp(17.269d0*input_temperature/mult)) / (mult*mult)

    ! estimate frequently used atmmospheric demand component
    mV%ET_demand_coef = mV%air_density_kg*cpair*input_vpd_kPa

    !
    ! Used for soil evaporation and leaf level conductance
    !

    ! Determine diffusion coefficient (m2.s-1), temperature dependant (pressure dependence neglected). Jones p51; appendix 2
    ! Temperature adjusted from standard 20oC (293.15 K), NOTE that 1/293.15 = 0.003411223
    ! 0.0000242 = conversion to make diffusion specific for water vapor (um2.s-1)
    mV%water_vapour_diffusion = 0.0000242d0*((input_temperature_K/293.15d0)**1.75d0)

    !
    ! Used for calculation of leaf level conductance
    !

    ! Calculate the dynamic viscosity of air (kg.m-2.s-1)
    dynamic_viscosity = ((input_temperature_K**1.5d0)/(input_temperature_K+120d0))*1.4963d-6
    ! and kinematic viscosity (m2.s-1)
    mV%kinematic_viscosity = dynamic_viscosity/mV%air_density_kg

  end subroutine meteorological_constants
  !
  !------------------------------------------------------------------
  !
  subroutine calculate_transpiration(transpiration, mV)

    ! Models leaf cnaopy transpiration based on the Penman-Monteith model of
    ! evapotranspiration used to estimate SPA's daily evapotranspiration flux
    ! (kgH20.m-2.day-1).

    implicit none

      type(model_working_variables) :: mV

    ! arguments
    double precision, intent(out) :: transpiration ! kgH2O.m-2.day-1

    ! local variables
    double precision :: canopy_radiation & ! isothermal net radiation (W/m2)
                                  ,gs,gb   ! stomatal and boundary layer conductance (m.s-1)

    !!!!!!!!!!
    ! Estimate energy radiation balance (W.m-2)
    !!!!!!!!!!

    ! Absorbed shortwave radiation MJ.m-2.day-1 -> J.m-2.s-1
    canopy_radiation = mV%canopy_lwrad_Wm2 + (mV%canopy_swrad_MJday * 1d6 * mV%dayl_seconds_1)

    !!!!!!!!!!
    ! Calculate canopy conductance (to water vapour)
    !!!!!!!!!!

    ! Change units of potential stomatal conductance
    ! (mmolH2O.m-2.d-1 -> m.s-1).
    ! Note assumption of sea surface pressure only
    gs = mV%stomatal_conductance / mV%convert_ms1_mmol_1
    ! Scale aerodynamic conductance to canopy scale
    gb = mV%aerodynamic_conductance * mV%leaf_canopy_wind_scaling

    !!!!!!!!!!
    ! Calculate canopy evaporative fluxes (kgH2O/m2/day)
    !!!!!!!!!!

    ! Calculate numerator of Penman Montheith (kgH2O.m-2.day-1)
    ! NOTE: that restriction within water supply restriction is determined
    ! during stomatal conductance level.
    transpiration = ( ( (mV%slope*canopy_radiation) + (mV%ET_demand_coef*gb) ) &
                      / (mV%lambda*(mV%slope+(mV%psych*(1d0+gb/gs)))) )*mV%dayl_seconds

  end subroutine calculate_transpiration
  !
  !------------------------------------------------------------------
  !
  subroutine calculate_wetcanopy_evaporation(wetcanopy_evap,storage, mV)

    ! Estimates evaporation of canopy intercepted rainfall based on the Penman-Monteith model of
    ! evapotranspiration used to estimate SPA's daily evapotranspiration flux
    ! (kgH20.m-2.day-1).

    implicit none

      type(model_working_variables) :: mV

    ! arguments
    double precision, intent(inout) :: storage      ! canopy water storage kgH2O/m2
    double precision, intent(out) :: wetcanopy_evap ! kgH2O.m-2.day-1

    ! local variables
    double precision :: canopy_radiation, & ! isothermal net radiation (W/m2)
                                      gb    ! stomatal and boundary layer conductance (m.s-1)

    ! Assuming there is any rainfall, currently water on the canopy or dew formation
    if (mV%rainfall > 0d0 .or. storage > 0d0) then

        !!!!!!!!!!
        ! Calculate canopy conductance (to water vapour)
        !!!!!!!!!!

        ! Combine in series stomatal conductance with boundary layer
        gb = mV%aerodynamic_conductance * mV%leaf_canopy_wind_scaling

        !!!!!!!!!!
        ! Estimate energy radiation balance (W.m-2)
        !!!!!!!!!!

        ! Absorbed shortwave radiation MJ.m-2.day-1 -> J.m-2.s-1
        canopy_radiation = mV%canopy_lwrad_Wm2 + (mV%canopy_swrad_MJday * 1d6 * mV%dayl_seconds_1)

        !!!!!!!!!!
        ! Calculate canopy evaporative fluxes (kgH2O/m2/day)
        !!!!!!!!!!

        ! Calculate potential Penman Montheith (kgH2O.m-2.day-1)
        wetcanopy_evap = max(0d0,(((mV%slope*canopy_radiation) + (mV%ET_demand_coef*gb)) &
                                 / (mV%lambda*(mV%slope+mV%psych))) * mV%dayl_seconds)

        ! Update based on canopy water storage
        call canopy_interception_and_storage(wetcanopy_evap,storage, mV)

    else

        ! there is no water movement possible
        mV%intercepted_rainfall = 0d0 ; wetcanopy_evap = 0d0

    endif

  end subroutine calculate_wetcanopy_evaporation
  !
  !------------------------------------------------------------------
  !
  subroutine calculate_soil_evaporation(soilevap, mV)

    ! Estimate soil surface evaporation based on the Penman-Monteith model of
    ! evapotranspiration used to estimate SPA's daily evapotranspiration flux
    ! (kgH20.m-2.day-1).

    implicit none

      type(model_working_variables) :: mV

    ! arguments
    double precision, intent(out) :: soilevap ! kgH2O.m-2.day-1

    ! local variables
    double precision :: local_temp &
                   ,soil_radiation & ! isothermal net radiation (W/m2)
                            ,esurf & ! see code below
                             ,esat & ! soil air space saturation vapour pressure
                              ,gws   ! water vapour conductance through soil air space (m.s-1)

    ! oC -> K for local temperature value
    local_temp = mV%maxt + freeze

    !!!!!!!!!!
    ! Estimate energy radiation balance (W.m-2)
    !!!!!!!!!!

    ! Absorbed shortwave radiation MJ.m-2.day-1 -> J.m-2.s-1
    soil_radiation = mV%soil_lwrad_Wm2 + (mV%soil_swrad_MJday * 1d6 * mV%dayl_seconds_1)

    !!!!!!!!!!
    ! Calculate soil evaporative fluxes (kgH2O/m2/day)
    !!!!!!!!!!

    ! calculate saturated vapour pressure (kPa), function of temperature.
    esat = 0.1d0 * exp( 1.80956664d0 + ( 17.2693882d0 * local_temp - 4717.306081d0 ) / ( local_temp - 35.86d0 ) )
    mV%air_vapour_pressure = esat - mV%vpd_kPa

    ! Soil conductance to water vapour diffusion (m s-1)...
    gws = mV%porosity(1) * mV%water_vapour_diffusion / (tortuosity*mV%drythick)

    ! vapour pressure in soil airspace (kPa), dependent on soil water potential
    ! - Jones p.110. partial_molar_vol_water. Less vapour pressure of the air to
    ! estimate the deficit between soil and canopy air spaces
    esurf = (esat * exp( 1d6 * mV%SWP(1) * partial_molar_vol_water / (Rcon * local_temp) )) - mV%air_vapour_pressure

    ! Estimate potential soil evaporation flux (kgH2O.m-2.day-1)
    soilevap = ( ((mV%slope*soil_radiation) + (mV%air_density_kg*cpair*esurf*mV%soil_conductance)) &
               / (mV%lambda*(mV%slope+(mV%psych*(1d0+mV%soil_conductance/gws)))) ) * mV%dayl_seconds

    return

  end subroutine calculate_soil_evaporation
  !
  !------------------------------------------------------------------
  !
  subroutine calculate_aerodynamic_conductance (mV)

    !
    ! Calculates the aerodynamic or bulk canopy conductance (m.s-1). Here we
    ! assume neutral conditions due to the lack of an energy balance calculation
    ! in either ACM or DALEC. The equations used here are extracted from SPA.
    !

    implicit none

      type(model_working_variables) :: mV

    ! local variables
    double precision :: local_lai, &
           mixing_length_momentum, & ! mixing length parameter for momentum (m)
            length_scale_momentum, & ! length scale parameter for momentum (m)
                     canopy_decay    ! within canopy decay coefficient for wind

    ! parameters
    double precision, parameter :: foliage_drag = 0.2d0 ! foliage drag coefficient

    ! Restrict LAI used here to greater than a minium value which prevents un-realistic outputs
    local_lai = max(min_lai,mV%lai)

    ! Calculate the zero plane displacement and roughness length
    call z0_displacement(mV%ustar_Uh,local_lai, mV)
    ! Calculate friction velocity at tower height (reference height) (m.s-1)
    ! WARNING neutral conditions only; see WRF module_sf_sfclay.F for 'with
    ! stability versions'
    !    ustar = (wind_spd / log((tower_height-displacement)/roughl)) * vonkarman
    mV%ustar = mV%wind_spd * mV%ustar_Uh

    ! Both the length scale and mixing length are assumed to be constant within
    ! the canopy (under dense canopy conditions).
    ! Calculate length scale (lc) for momentum absorption within the canopy; Harman & Finnigan (2007)
    ! Calculate mixing length (lm) for vertical momentum within the canopy Harman & Finnigan (2008)
    length_scale_momentum = (4d0*canopy_height) / local_lai
    mixing_length_momentum = 2d0*(mV%ustar_Uh**3)*length_scale_momentum

    ! Estimate above canopy wind speed decay followin Harman & Finnigan (2008)
    ! and assuming neutral conditions only.
    call log_law_decay(mV)

    ! Calculate canopy decay coefficient with stability correction
    ! NOTE this is not consistent with canopy momentum decay done by Harman &
    ! Finnigan (2008) instead follows Nui & Yang 2004; Qin et al 2002.
    canopy_decay = sqrt((foliage_drag*canopy_height*local_lai)/mixing_length_momentum)

    ! Estimating the within canopy wind speed, we assume that the wind speed
    ! just inside of the canopy is most important.
    !canopy_wind = canopy_wind*exp((ustar_Uh*((canopy_height*1d0)-canopy_height))/mixing_length_momentum)

    ! Calculate_soil_conductance
    call calculate_soil_conductance(mixing_length_momentum,local_lai,canopy_decay, mV)
    ! Calculate top of canopy leaf conductance (m/s) for water vapour under forced convective conditions
    ! The leaf-to-canopy scaling is achieved with the leaf_canopy_wind_scaling scalar.
    call average_leaf_conductance(mV%aerodynamic_conductance, mV)

    ! Estimate leaf to canopy scaling factor for use with aerodynamic conductance.
    ! Based on the canopy scaling of photosynthetic capacity due to light from
    ! Sellers et al., (1992), Remote Sensing Environment, 42(3), 187-216.
    ! But now applied on the within canopy decay gradient.
    mV%leaf_canopy_wind_scaling = exp(canopy_decay*(1d0-(soil_roughl/canopy_height))) &
                             - exp(canopy_decay*(1d0-((mV%roughl+mV%displacement)/canopy_height)))
    mV%leaf_canopy_wind_scaling = mV%leaf_canopy_wind_scaling / exp(canopy_decay)

  end subroutine calculate_aerodynamic_conductance
  !
  !------------------------------------------------------------------
  !
  subroutine average_leaf_conductance(gv_forced, mV)

    !
    ! Subroutine calculates the forced conductance of water vapour for non-cylinder within canopy leaves (i.e. broadleaf)
    ! Free convection (i.e. that driven by energy balance) is negelected here due to the lack of an energy balance
    ! calculation in DALEC. Should a energy balance be added then this code could be expanded include free conductance
    ! Follows a simplified approach to that used in SPA (Smallman et al 2013).
    !

    implicit none

      type(model_working_variables) :: mV

    ! arguments
    double precision, intent(out) :: gv_forced ! canopy conductance (m/s) for water vapour under forced convection

    ! local parameters
    double precision, parameter :: leaf_width_coef = 25d0, & ! (1/leaf_width) * 0.5,
                                                               ! where 0.5 accounts for one half
                                                               ! of the leaf used in water exchange
                                        leaf_width = 0.02d0    ! leaf width (m) (alternates 0.04, 0.08)
!                                                Pr = 0.72d0, & ! Prandtl number
!                                           Pr_coef = 1.05877d0 !1.18d0*(Pr**(0.33d0))
    ! local variables
    double precision :: &
              Sh_forced & ! Sherwood number under forced convection
                    ,Re   ! Reynolds number

    ! Sherwood number under forced convection. NOTE: 0.962 * Pr_coef = 1.018537
    Sh_forced = 1.018537d0*(sqrt((leaf_width*mV%canopy_wind)/mV%kinematic_viscosity))
    ! Estimate the the forced conductance of water vapour
    gv_forced = mV%water_vapour_diffusion*Sh_forced*leaf_width_coef

  end subroutine average_leaf_conductance
  !
  !------------------------------------------------------------------
  !
  subroutine log_law_decay (mV)

    ! Standard log-law above canopy wind speed (m.s-1) decay under neutral
    ! conditions.
    ! See Harman & Finnigan 2008; Jones 1992 etc for details.

    implicit none

      type(model_working_variables) :: mV

    ! log law decay, NOTE: given canopy height (9 m) the log function reduces
    ! to a constant value down to ~ 7 decimal place (0.3161471806). Therefore
    ! 1/vonkarman * 0.31 = 0.7710906
    mV%canopy_wind = mV%ustar * vonkarman_1 * log((canopy_height-mV%displacement) / mV%roughl)

  end subroutine log_law_decay
  !
  !-----------------------------------------------------------------
  !
  subroutine calculate_field_capacity (mV)

    use brent_zero, only: zbrent

    ! field capacity calculations for saxton eqns !

    implicit none

      type(model_working_variables) :: mV

    ! local variables..
    integer :: i
    double precision :: x1, x2

    x1 = 0.1d0 ; x2 = 0.7d0 ! low/high guess
    do i = 1 , nos_soil_layers+1
       mV%water_retention_pass = i
       ! field capacity is water content at which SWP = -10 kPa
       mV%field_capacity(i) = zbrent('water_retention:water_retention_saxton_eqns', &
                                   water_retention_saxton_eqns_ , x1 , x2 , 0.001d0, 0d0 )
    enddo

    contains
      double precision function water_retention_saxton_eqns_(x)
      double precision, intent(in):: x
      water_retention_saxton_eqns_ = water_retention_saxton_eqns(x, mV)
    end function

  end subroutine calculate_field_capacity
  !
  !------------------------------------------------------------------
  !
  subroutine calculate_daylength (mV)

    ! Subroutine uses day of year and latitude (-90 / 90 degrees) as inputs,
    ! combined with trigonomic functions to calculate day length in hours and seconds

    implicit none

      type(model_working_variables) :: mV

    ! local variables
    double precision :: dec, sinld, cosld, aob

    !
    ! Estimate solar geometry variables needed
    !

    ! day length is estimated as the ratio of sin and cos of the product of declination an latitude in radiation
    sinld = mV%sin_latitude_radians * sin( mV%declination )
    cosld = mV%cos_latitude_radians * cos( mV%declination )
    aob = max(-1d0,min(1d0,sinld / cosld))

    ! estimate day length in hours and seconds and upload to module variables
    mV%dayl_hours = 12d0 * ( 1d0 + 2d0 * asin( aob ) * pi_1 )
    mV%dayl_seconds = mV%dayl_hours * seconds_per_hour

    ! return to user
    return

  end subroutine calculate_daylength
  !
  !------------------------------------------------------------------
  !
  subroutine calculate_longwave_isothermal(canopy_temperature,soil_temperature, mV)

    ! Subroutine estimates the isothermal net longwave radiation (W.m-2) for
    ! the canopy and soil surface. SPA uses a complex multi-layer radiative
    ! transfer scheme including reflectance, transmittance any absorption.
    ! However, for a given canopy vertical profiles, the LAI absorption
    ! relationship is readily predicted via Michaelis-Menten or
    ! non-rectangular hyperbola as done here.

    implicit none

      type(model_working_variables) :: mV

    ! arguments
    double precision, intent(in) :: canopy_temperature, soil_temperature ! oC

    ! local variables
    double precision ::    dT, & ! Canopy transmittance for long wave radiation
                        lwrad, & ! downward long wave radiation from sky (W.m-2)
        longwave_release_soil, & ! emission of long wave radiation from surfaces per m2
      longwave_release_canopy, & ! assuming isothermal condition (W.m-2)
                        Vc_dT    ! Multi-use variable

    ! estimate long wave radiation from atmosphere (W.m-2)
    lwrad = emiss_boltz * (mV%maxt+freeze-20d0)**4d0
    ! estimate isothermal long wave emission per unit area
    longwave_release_soil = emiss_boltz * (soil_temperature+freeze)**4d0
    ! estimate isothermal long wave emission per unit area
    longwave_release_canopy = emiss_boltz * (canopy_temperature+freeze)**4d0
    ! Canopy transmittance for thermal radiation
    dT = 1d0-exp(-mV%lai/mV%Vc*mV%mu_obar) ; Vc_dT = mV%Vc*dT

    !!!!!!!!!!
    ! Isothermal net long wave canopy and soil balance (W.m-2)
    !!!!!!!!!!

    ! Diffuse longwave absorbed by the canopy
    mV%canopy_lwrad_Wm2 = (lwrad*Vc_dT) - (Vc_dT*2d0*longwave_release_canopy) + (Vc_dT*longwave_release_soil)
    ! Diffuse longwave absorbed by the soil
    mV%soil_lwrad_Wm2 = (lwrad*(1d0-Vc_dT)) + (Vc_dT*longwave_release_canopy) - longwave_release_soil

  end subroutine calculate_longwave_isothermal
  !
  !------------------------------------------------------------------
  !
  subroutine calculate_radiation_balance (mV)

    implicit none

      type(model_working_variables) :: mV

    !
    ! Parametric approximation for 2-stream, multi-layer radiative transfer
    !

    !
    ! References
    !
    ! Sellers (1985) Canopy reflectance, photosynthesis and transpiration.
    !                International Journal of Remote Sensing, 6(8), 1335-1772, doi: 10.1080/01431168508948283
    ! Sellers et al., (1986) A simple biosphere model (SiB) for use in general circulation models.
    !                        Journal of Atmospheric Sciences, 43(6), 505-531
    ! Sellers et al., (1996) A revised land surface parameterisation (SiB2) for atmospheric GCMs. Part 1: Model formualtion.
    !                        Journal of Climate, 9(4), 676-705, doi: 10.1175/1520_0442(1996)009<0676:ARLSPF>2.0.CO;2

    !
    ! Description
    !
    ! The purpose of these equations is the approximation of the behaviuour of a 2-stream multiple canopy layer model
    ! accounting for solar angle, within canopy interception, reflectance and transmittance.
    !
    ! As a result the actual parametric inputs are few, but requires a large number of empiracal relationships to describe
    ! the non-linear dynamics of radiative transfer

    ! NOTE: that this code provides a daily timescale linear correction on
    ! isothermal longwave balance to net based on soil surface incident shortwave
    ! radiation. This correction is drawn from the standard ACM-GPP-ET-v1 approach

    ! Declare local variables
    double precision :: delta_iso

    ! Calculate the declination
!    declination = calculate_declination(doy)
    ! Calculate cosine zenith angle
    call calculate_cosine_solar_zenith_angle(mV)
    ! Estimate shortwave radiation balance
    call calculate_shortwave_balance(mV)
    ! Estimate isothermal long wave radiation balance
    call calculate_longwave_isothermal(mV%meant,mV%meant, mV)
    ! Apply linear correction to soil surface isothermal->net longwave radiation
    ! balance based on absorbed shortwave radiation
    delta_iso = (soil_iso_to_net_coef_LAI * mV%lai) + &
                (soil_iso_to_net_coef_SW * (mV%soil_swrad_MJday * 1d6 * seconds_per_day_1)) + &
                 soil_iso_to_net_const
    ! In addition to the iso to net adjustment, SPA analysis shows that soil net never gets much below zero
    !soil_lwrad_Wm2 = max(-0.1d0,soil_lwrad_Wm2 + delta_iso)
    mV%soil_lwrad_Wm2 = mV%soil_lwrad_Wm2 + delta_iso
    ! Apply linear correction to canopy isothermal->net longwave radiation
    ! balance based on absorbed shortwave radiation
    delta_iso = (canopy_iso_to_net_coef_LAI * mV%lai) + &
                (canopy_iso_to_net_coef_SW * (mV%canopy_swrad_MJday * 1d6 * seconds_per_day_1)) + &
                canopy_iso_to_net_const
    mV%canopy_lwrad_Wm2 = mV%canopy_lwrad_Wm2 + delta_iso

  end subroutine calculate_radiation_balance
  !
  !-----------------------------------------------------------------
  !
  subroutine calculate_shortwave_balance (mV)

    ! Subroutine estimates the canopy and soil absorbed shortwave radiation
    ! (MJ/m2/day). Radiation absorption is paritioned into NIR and PAR for
    ! canopy, and NIR + PAR for soil.
    ! Follows an implementation of the Sellers (1985) approximation

    implicit none

      type(model_working_variables) :: mV

    ! Declare local parameters
    double precision, dimension(no_wavelength), parameter :: &
                                   newsnow_reflectance = (/0.73d0,0.95d0/) ! NIR/PAR new snow reflectance fraction
    ! local variables
    double precision :: Gu, K, mu, S2, S3, fsnow, diffuse_fraction
    ! Local variables with different values per wavelength
    double precision, dimension(no_wavelength) :: &
                      as_mu, dd, ff, sigma, u1, u2, u3, &
                      S1, S1_1, p1, p2, p3, p4, D1, D1_1, D2, D2_1, &
                      h1, h1_sigma, h2, h3, h4, h4_sigma, h5, h6, h7, h8, h9, h10, &
                      mu_obar_hh, mu_obar_K, &
                      Iup, Idown, soil_albedo, soil_absorption, &
                      canopy_absorption_fraction_diffuse, &
                      canopy_absorption_fraction_direct, &
                      soil_absorption_fraction_diffuse, &
                      soil_absorption_fraction_direct, &
                      soil_nir_par_MJday, canopy_nir_par_MJday, &
                      swrad_direct, swrad_diffuse

    ! Estimate the diffuse fraction of shortwave radiation
    call calculate_diffuse_fraction(diffuse_fraction, mV)
    ! Estimate multiple use par and nir components (Note: units are MJ/m2/d)
    swrad_diffuse(1) = (1d0 - sw_par_fraction) * mV%swrad * diffuse_fraction      ! NIR
    swrad_diffuse(2) = sw_par_fraction * mV%swrad * diffuse_fraction              ! PAR
    swrad_direct(1) = (1d0 - sw_par_fraction) * mV%swrad * (1d0-diffuse_fraction) ! NIR
    swrad_direct(2) = sw_par_fraction * mV%swrad * (1d0-diffuse_fraction)         ! PAR

    ! Assign cosine_solar_zenith_angle to a local variable for easier readability
    mu = mV%cosine_solar_zenith_angle

    ! Relative projected area of leaf elements in direction of the cosine_solar_zenith_angle (mu).
    ! This variable is determined as the result of two empirical functions related to the leaf_distribution_deviance
    ! Note the notation used here Gu is varied, for clarity, from the actual used in Sellers (1985) which is G(mu).
    ! Similarly, the coefficients used in calculating Gu indicate "empty set".
    ! For clarity and visual similarity, I've used the capital letter O
    Gu = mV%O1 + (mV%O2 * mu)

    ! Extinction coefficient for direct radiation, related to Gu and the cosine zenith angle
    K = Gu / mu
    ! Combination of the inverse spectral characteristics and light extinction coefficient
    mu_obar_K = mV%mu_obar * K
    ! Single leaf scattering albedo within the canopy, varied by mu, leaf distribution and wavelength
    as_mu = ((mV%canopy_scattering * 0.5d0) * (Gu / (Gu+(mu*mV%O2)))) &
          * (1d0 - (mu*(mV%O1/(Gu+(mu*mV%O2)))*log((Gu+(mu*mV%O2)+(mu*mV%O1))/(mu*mV%O1)) ) )

    ! Upscatting coefficient for direct radiation, varied by mu and wavelength
    mV%beta0 = ((1d0+mu_obar_K) / (mV%canopy_scattering*mu_obar_K)) * as_mu

    ! Various terms, yet to have their specific functions determined
    ! Note that notations from Sellers (1985) have been given double letters if only single character was used,
    ! or written word for greek notation
    dd = mV%canopy_scattering * mu_obar_K * mV%beta0
    ff = mV%canopy_scattering * mu_obar_K * (1-mV%beta0)
    sigma = (mV%cc*mV%cc) + (mV%bb*mV%bb) + (mu_obar_K*mu_obar_K)
    u1 = mV%bb - (mV%cc/mV%soil_reflectance) ; u2 = mV%bb - (mV%cc*mV%soil_reflectance) ; u3 = ff + (mV%cc*mV%soil_reflectance)
    S1 = exp(-mV%hh*mV%lai) ; S1_1 = 1d0/S1 ; S2 = exp(-K*mV%lai)
    mu_obar_hh = mV%mu_obar*mV%hh

    ! Related to diffuse radiation
    p1 = mV%bb + mu_obar_hh ; p2 = mV%bb - mu_obar_hh
    ! Related to direct radiation
    p3 = mV%bb + mu_obar_K ; p4 = mV%bb - mu_obar_K
    !
    D1 = (p1 * (u1 - mu_obar_hh) * S1_1) - (p2*(u2+mu_obar_hh)*S1) ; D1_1 = 1d0/D1
    D2 = ((u2+mu_obar_hh)*S1_1) - ((u2-mu_obar_hh)*S1) ; D2_1 = 1d0/D2

    !
    ! Direct radiation specific components
    !

    h1 = (-dd*p4) - (mV%cc*ff) ; h1_sigma = h1 / sigma
    h2 =  D1_1 * ( ((dd-(h1_sigma*p3))*((u1-mu_obar_hh)*S1_1)) &
                     - (p2*(dd-mV%cc-(h1_sigma*(u1+mu_obar_K)))*S2) )
    h3 = -D1_1 * ( ((dd-(h1_sigma*p3))*(u1+mu_obar_hh)*S1) &
                     - (p1*(dd-mV%cc-(h1_sigma*(u1+mu_obar_K)))*S2) )
    h4 = (-dd*p3) - (mV%cc*ff) ! NOTE: "-" at the beginning is a correction identified in Sellers et al., (1996)
    h4_sigma = h4 / sigma
    h5 = -D2_1 * ( (h4_sigma*(u2+mu_obar_hh)*S1_1) &
                     + (u3-(h4_sigma*(u2-mu_obar_K)*S2)) )
    h6 =  D2_1 * ( (h4_sigma*(u2-mu_obar_hh)*S1) &
                     + (u3-(h4_sigma*(u2-mu_obar_K)*S2)) )

    ! Fraction of direct radiation which leaves the canopy top as diffuse
    Iup = ((h1*S2)/sigma) + (h2*S1) + (h3*S1_1)
    ! Fraction of direct radiation which leaves the canopy base as diffuse
    Idown = ((h4*S2)/sigma) + (h5*S1) + (h6*S1_1)

    ! Update soil reflectance based on snow cover
    if (mV%snow_storage > 0d0) then
        fsnow = 1d0 - exp( - mV%snow_storage * 0.1d0 )  ! fraction of snow cover on the ground
        soil_albedo = ((1d0 - fsnow) * mV%soil_reflectance) + (fsnow * newsnow_reflectance) ! NIR & PAR
    else
        ! Estimate the combined soil and surface layer reflectances, assume direct and diffuse reflectances are isotropic
        ! NOTE; this variable is also used in the diffuse calculation too
        !soil_albedo = (soil_surface_reflectance * Vg) + ((1d0-Vg) * soil_reflectance)
        soil_albedo = mV%soil_reflectance
    endif
    ! Estimate soil absorption fraction
    soil_absorption = 1d0 - soil_albedo

    S3 = exp(-K*mV%lai/mV%Vc)
    ! Fraction of direct radiation absorbed by the canopy
    canopy_absorption_fraction_direct = mV%Vc * (1d0 - Iup - (Idown*soil_absorption) &
                                             - (S3*soil_absorption))
    ! Fraction of direct radiation absorbed by the soil
    soil_absorption_fraction_direct = ((1d0-mV%Vc)*soil_absorption) &
                                    + (mV%Vc*((Idown*soil_absorption) + (S3*soil_absorption)))

    !
    ! Diffuse radiation specific components
    !

    h7  = (mV%cc/D1)  * (u1-mu_obar_hh) * S1_1
    h8  = (-mV%cc/D1) * (u1+mu_obar_hh) * S1
    h9  =  D2_1 * (u2+mu_obar_hh) * S1_1
    h10 = -D2_1 * (u2-mu_obar_hh) * S1

    ! Fraction of diffuse radiation which leaves the canopy top as diffuse
    Iup = (h7*S1) + (h8*S1_1)
    ! Fraction of diffuse radiation which leaves the canopy base as diffuse
    Idown = (h9*S1) + (h10*S1_1)

    ! Fraction of diffuse radiation absorbed by the canopy
    canopy_absorption_fraction_diffuse = mV%Vc * (1d0 - Iup - (Idown*soil_absorption))
    ! Fraction of diffuse radiation absorbed by the soil
    soil_absorption_fraction_diffuse = ((1d0-mV%Vc)*soil_absorption) + (mV%Vc*((Idown*soil_absorption)))

    !
    ! Combine direct and diffuse, convert into actual units of energy (MJ/m2/d)
    !

    ! Determine the combined direct and diffuse absorptions across wavelength
    soil_nir_par_MJday = (soil_absorption_fraction_diffuse * swrad_diffuse) &
                       + (soil_absorption_fraction_direct * swrad_direct)
    canopy_nir_par_MJday = (canopy_absorption_fraction_diffuse * swrad_diffuse) &
                       + (canopy_absorption_fraction_direct * swrad_direct)
    ! Assign to specific output variables for use elsewhere in the model
    mV%canopy_par_MJday = canopy_nir_par_MJday(2)
    ! combine totals for use is soil evaporation
    mV%soil_swrad_MJday = sum(soil_nir_par_MJday)
    ! Combine to estimate total shortwave canopy absorbed radiation
    mV%canopy_swrad_MJday = sum(canopy_nir_par_MJday)

    ! Estimate the integral of light interception for use as a leaf to canopy
    ! scaler for photosynthesis, transpiration, and gs
    ! Based on the canopy scaling of photosynthetic capacity due to light from
    ! Sellers et al., (1992), Remote Sensing Environment, 42(3), 187-216.
    mV%leaf_canopy_light_scaling = (1d0-S2) / K

    ! check energy balance
!    balance = swrad - canopy_par_MJday - canopy_nir_MJday - refl_par_MJday - refl_nir_MJday - soil_swrad_MJday
!    if ((balance - swrad) / swrad > 0.01) then
!        print*,"SW residual frac = ",(balance - swrad) / swrad,"SW residual = ",balance,"SW in = ",swrad
!   endif

  end subroutine calculate_shortwave_balance
  !
  !-----------------------------------------------------------------
  !
  !subroutine calculate_radiation_commons(lat,rad_pars)
  subroutine calculate_radiation_commons(lat, mV)

    implicit none

      type(model_working_variables) :: mV

    ! Description

    ! Declare arguments
    double precision, intent(in) :: lat!, & ! site latitude in degrees
                               !rad_pars(6) !

    ! Calculate some common variables and place into memory
    mV%latitude = lat
    mV%latitude_radians = lat * deg_to_rad
    mV%sin_latitude_radians = sin(mV%latitude_radians)
    mV%cos_latitude_radians = cos(mV%latitude_radians)

    ! Load canopy optical properties to their module variables
!    canopy_reflectance(1)   = rad_pars(1) ! canopy_nir_reflectance
!    canopy_reflectance(2)   = rad_pars(2) ! canopy_par_reflectance
!    canopy_transmittance(1) = rad_pars(3) ! canopy_nir_transmittance
!    canopy_transmittance(2) = rad_pars(4) ! canopy_par_transmittance
!    soil_reflectance(1)     = rad_pars(5) ! soil_nir_reflectance
!    soil_reflectance(2)     = rad_pars(6) ! soil_par_reflectance

    ! Canopy scattering of incident light, varied by wavelength
    ! NOTE: if we want to put snow fall on the canopy in the model, then this
    ! line will need to move into shortwave_balance() or calculate_radiation_balance()
    ! to be recalculated with each update of the canopy reflectance and transmittances.
    mV%canopy_scattering = mV%canopy_reflectance + mV%canopy_transmittance

    ! Two empirical functions related to the leaf_distribution_deviance
    ! used in the calcuation of several variable varying by day of year
    ! The coefficient notation used in Sellers (1985) indicate "empty set".
    ! For clarity and visual similarity, I've used the capital letter O
    mV%O1 = 0.5d0-(0.633d0*leaf_distribution_deviance)-(0.33d0*leaf_distribution_deviance**2d0)
    mV%O2 = 0.877d0*(1d0-(2d0*mV%O1))

    ! The average inverse diffuse optical depth per unit leaf area.
    ! mu_obar indicates mu with over bar
    ! See notes or references for the original integral equation
    mV%mu_obar = (1d0/mV%O2) * ( 1d0-(mV%O1/mV%O2)*log((mV%O1+mV%O2)/mV%O1) )
    ! Mean leaf angle deviation from the horizontal (radians)
    ! SPA default assumption is 30 degrees, where (pi/180) is the conversion to radians
    mV%leaf_angle = 0.5235988d0 !30d0 * (pi/180d0)
    ! Analytical correction for leaf angle (radians) on light scattering within the canopy
    ! Notation is the verbal description of that used in Sellers (1985)
    mV%cos2theta = (1d0 + cos(2d0*mV%leaf_angle)) * 0.5d0

    ! Upward scattering as diffuse radiation, a function of canopy_transmittance, canopy_reflectance and leaf angle.
    ! Notation in Sellers (1985) is "c"
    mV%cc = 0.5 * (mV%canopy_reflectance + mV%canopy_transmittance + (mV%canopy_reflectance - mV%canopy_transmittance) * mV%cos2theta)
    ! The upward scattering fraction / coefficient for diffuse radiation.
    ! NOTE there is a direct radiation equivalent found below, noted as beta0
    mV%beta = mV%cc / mV%canopy_scattering
    ! Downward scatting of diffuse radiation
    ! Note that notations from Sellers (1985) have been given double letters if only single character was used,
    ! or written word for greek notation
    mV%bb = (1d0-(1d0-mV%beta)*mV%canopy_scattering)
    ! Extinction coefficient for diffuse radiation, related to absorption normalised by mu_obar
    ! Note that notations from Sellers (1985) have been given double letters if only single character was used,
    ! or written word for greek notation
    mV%hh = (((mV%bb**2d0 - mV%cc**2d0))**(0.5d0)) / mV%mu_obar

    ! Define the vegetated and covered soil (i.e. by litter) fractions
    ! Vc, could also be considered a canopy clumping factor
    mV%Vc = 0.75d0 ; mV%Vg = 0d0

    ! Return back to user
    return

  end subroutine calculate_radiation_commons
  !
  !-----------------------------------------------------------------
  !
  subroutine calculate_cosine_solar_zenith_angle (mV)

    implicit none

      type(model_working_variables) :: mV

    ! Calculate some common variables needed for the Sellers (1985)
    ! parameteric appoximation

    ! Declare local parameters

    ! Solar angle for the hour of the day, assumed in daily model to be 10 am (or 2 pm)
    ! therefore, 2 = 14 - 12, where 14 = timestep of day and 12 = half the number
    ! hour-angle = ( hr - 12 ) * 15. * ( pi / 180.d0 )
    ! of time steps per day.
    double precision, parameter :: cos_hour_angle = cos(0.5235988d0)

    ! Declare local variables

    ! Now calculate the solar-zenith-angle, as per..
    !  sin(latitude)sin(declination) + cos(latitude)cos(declination)cos(hour_angle)
    ! where hour-angle = ( hr - 12 ) * 15. * ( pi / 180.d0 )
    ! and from that the solar flux.
    ! eqn 2.15 in Hartmann's Global Physical Climatology...
    ! Minimum allowed value constraint from SIB3 implementation of Sellers (1985)
    mV%cosine_solar_zenith_angle = max(0.01747d0, mV%sin_latitude_radians * sin(mV%declination) + &
                                               mV%cos_latitude_radians * cos(mV%declination) * &
                                               cos_hour_angle)

  end subroutine calculate_cosine_solar_zenith_angle
  !
  !-----------------------------------------------------------------
  !
  subroutine calculate_diffuse_fraction(diffuse_fraction, mV)

    implicit none

      type(model_working_variables) :: mV

    ! Description estimate ratio of actual to extra-solar radiation
    ! for day length period following Erbs et al., (1982)

    ! Declare arguments
    double precision, intent(out) :: diffuse_fraction

    ! Declare local parameters
    double precision, parameter :: So = 117.504d0 ! solar constant (1360 Wm-2)
                                                  ! unit scaled to MJ/m2/day
    ! Declare local variables
    double precision :: clear_day_swrad, Kt

    ! Estimate sunset solar angle
    mV%sunset_solar_angle = acos(-tan(mV%latitude_radians)*tan(mV%declination))
    ! Estimate clear day light solar radiation (MJ/m2/d)
    clear_day_swrad = So * pi_1 * (1d0+0.033d0*cos(two_pi*mV%doy/365d0)) &
                    * (mV%cos_latitude_radians*cos(mV%declination)*sin(mV%sunset_solar_angle) &
                      + mV%sin_latitude_radians*sin(mV%declination))
    ! Estimate the ratio of actual to clear day radiation (MJ/m2/d over MJ/m2/d)
    Kt = min(1d0,mV%swrad / clear_day_swrad)

    ! Calculate diffuse ratio
    if (mV%sunset_solar_angle < 1.4208d0) then
        if (Kt < 0.715d0) then
            diffuse_fraction = 1d0 - 0.2727d0*Kt + 2.4495d0*Kt**2 - 11.9514d0*Kt**3 + 9.3879d0*Kt**4
        else
            diffuse_fraction = 0.143d0
        end if
    else
        if (Kt < 0.722d0) then
            diffuse_fraction = 1d0 + 0.28832d0*Kt - 2.5557d0*Kt**2 + 0.8448d0*Kt**3
        else
            diffuse_fraction = 0.175d0
        end if
    end if

    ! Sanity check
    diffuse_fraction = min(1d0, diffuse_fraction)

    ! return
    return

  end subroutine calculate_diffuse_fraction
  !
  !-----------------------------------------------------------------
  !
  subroutine calculate_Rtot (mV)

    ! Purpose of this subroutine is to calculate the minimum soil-root hydraulic
    ! resistance input into ACM. The approach used here is identical to that
    ! found in SPA.

    ! local variables
    integer :: i, rooted_layer

      type(model_working_variables) :: mV
    double precision :: transpiration_resistance,root_reach_local, &
                        slpa, mult, prev, exp_func!, root_depth_50, bonus
    double precision, dimension(nos_root_layers) :: Rcond_layer, &
                                                    root_mass,  &
                                                    root_length
    double precision, parameter :: rootdist_tol = 13.81551d0 ! log(1d0/rootdist_tol - 1d0) were rootdist_tol = 1d-6
                                  !rootdist_tol = 1d-6!, & ! Root density assessed for the max rooting depth
                                   !root_depth_frac_50 = 0.25d0 ! fractional soil depth above which 50 %
                                                               ! of the root mass is assumed to be located

    ! reset water flux
    mV%total_water_flux = 0d0 ; mV%water_flux_mmolH2Om2s = 0d0 ; mV%wSWP = 0d0 ; mV%rSWP = 0d0 ; mV%Reff = 0d0
    slpa = 0d0 ; root_length = 0d0 ; root_mass = 0d0 ; Rcond_layer = 0d0 ; mV%conductance_mmolH2OMPam2s = 0d0
    ! calculate soil depth to which roots reach
    mV%root_reach = mV%max_depth * mV%root_biomass / (mV%root_k + mV%root_biomass)
    ! calculate the plant hydraulic resistance component.
    transpiration_resistance = canopy_height / (gplant * max(min_lai,mV%lai))

    !!!!!!!!!!!
    ! calculate current steps soil hydraulic conductivity
    !!!!!!!!!!!

    ! seperately calculate the soil conductivity as this applies to each layer
    do i = 1, nos_soil_layers
       call calculate_soil_conductivity(i,mV%soil_waterfrac(i),mV%soil_conductivity(i), mV)
    end do ! soil layers

    !!!!!!!!!!!
    ! Calculate hydraulic properties and each rooted layer
    !!!!!!!!!!!

    ! calculate and accumulate steady state water flux in mmol.m-2.s-1
    ! NOTE: Depth correction already accounted for in soil resistance
    ! calculations and this is the maximum potential rate of transpiration
    ! assuming saturated soil and leaves at their minimum water potential.
    mV%demand = max(0d0, (mV%SWP(1:nos_root_layers) - (head*canopy_height)) - mV%minlwp )
    ! now loop through soil layers, where root is present
    rooted_layer = 1
    ! Determine the exponential coefficient needed for an exponential decay to the current root reach
    ! Exponential decay profile following:
    ! Y = 1 / (1 + exp(-B * Z)), where Y = density at Z, B = gradient, Z = depth
    ! To determine gradient for current maximum root depth assuming density reaches rootdist_tol value, rearranges to:
    ! B = ln(1/Y - 1) / Z
!d = seq(0,2, 0.01) ; c = -2.6 ; rmax = 1 ; d50 = 0.25 ; rd = rmax / (1+ (d/d50)**c)
!rmax = rd * (1 + (d/d50)**c)
!(((rmax / rd) - 1)**(1/c)) * d50 = d ! Depth at which rd = 99 %
    !slpa = log(1d0/rootdist_tol - 1d0) / root_reach
    slpa = rootdist_tol / mV%root_reach
    prev = 1d0
    do i = 1, nos_root_layers
       ! Determine the exponential function for the current cumulative depth
       exp_func = exp(-slpa * sum(mV%layer_thickness(1:i)))
       ! Calculate the difference in the integral between depths, i.e. the proportion of root in the current volume
       mult = prev - (1d0 - (1d0/(1d0+exp_func)) + (0.5d0 * exp_func))
       ! Assign fine roo the the current layer...
       root_mass(i) = mV%fine_root_biomass * mult
       ! and determine the associated amount of root
       root_length(i) = root_mass(i) * root_mass_length_coef_1
       prev = prev - mult
       ! If there is root in the current layer then we should calculate the resistances
       if (root_mass(i) > 0d0) then
           ! Track the deepest root layer assessed
           rooted_layer = i
           ! if there is root then there is a water flux potential...
           root_reach_local = min(mV%root_reach,mV%layer_thickness(i))
           ! calculate and accumulate steady state water flux in mmol.m-2.s-1
           call plant_soil_flow(i,root_length(i),root_mass(i) &
                               ,mV%demand(i),root_reach_local &
                               ,transpiration_resistance,Rcond_layer(i), mV)
       else
           ! ...if there is not then we wont have any below...
           exit
       end if ! root present in current layer?
    end do ! nos_root_layers
    ! Turn the output resistance into conductance
    Rcond_layer = Rcond_layer**(-1d0)

    ! if freezing then assume soil surface is frozen, therefore no water flux
    if (mV%meant < 1d0) then
        mV%water_flux_mmolH2Om2s(1) = 0d0
        Rcond_layer(1) = 0d0
    end if

    ! Calculate sum value (mmolH2O.m-2.s-1)
    mV%total_water_flux = sum(mV%water_flux_mmolH2Om2s)
    ! Calculate effective resistance
    ! NOTE: minimum condition used to guard against zero conductance and propagation of Inf / NaN
    ! through the model structure/
    mV%Reff = min(1d6,sum(mV%conductance_mmolH2OMPam2s)**(-1d0))
    if (mV%total_water_flux <= vsmall) then
        ! Set values for no water flow situation
        mV%uptake_fraction = (mV%layer_thickness(1:nos_root_layers) / sum(mV%layer_thickness(1:nos_root_layers)))
        ! Estimate weighted soil water potential based on fractional extraction from soil layers
        mV%wSWP = sum(mV%SWP(1:nos_root_layers) * mV%uptake_fraction(1:nos_root_layers))
        ! rSWP based on the conductance due to the roots themselves.
        ! However, similar to the wSWP we need a special case calculation
        ! when there is no extraction from the soil. Here we use the ratio of root mass itself.
        mV%rSWP = sum(mV%SWP(1:rooted_layer) * (root_mass(1:rooted_layer) / sum(root_mass(1:rooted_layer))))
        mV%total_water_flux = 0d0
      else
        ! calculate weighted SWP and uptake fraction
        mV%uptake_fraction(1:nos_root_layers) = mV%water_flux_mmolH2Om2s(1:nos_root_layers) / mV%total_water_flux
        ! Estimate weighted soil water potential based on fractional extraction from soil layers
        mV%wSWP = sum(mV%SWP(1:nos_root_layers) * mV%uptake_fraction(1:nos_root_layers))
        ! rSWP based on the conductance due to the roots themselves.
        ! The idea being that the plant may hedge against growth based on the majority of the
        ! profile being dry while not losing leaves within some toleration.
        mV%rSWP = sum(mV%SWP(1:rooted_layer) * (Rcond_layer(1:rooted_layer) / sum(Rcond_layer(1:rooted_layer))))
    endif

    ! and return
    return

  end subroutine calculate_Rtot
  !
  !-----------------------------------------------------------------
  !
  subroutine canopy_interception_and_storage(potential_evaporation,storage, mV)

    ! Simple daily time step integration of canopy rainfall interception, runoff
    ! and rainfall (kgH2O.m-2.s-1). NOTE: it is possible for intercepted rainfall to be
    ! negative if stored water running off into the soil is greater than
    ! rainfall (i.e. when leaves have died between steps)

    implicit none

      type(model_working_variables) :: mV

    ! arguments
    double precision, intent(inout) :: storage, & ! canopy water storage (kgH2O/m2)
                         potential_evaporation    ! wet canopy evaporation (kgH2O.m-2.day-1),
                                                  ! enters as potential but leaves as water balance adjusted.
                                                  ! Note that this assumes a completely wet leaf surface
    ! local variables
    double precision :: a, through_fall, max_storage, max_storage_1, daily_addition, wetcanopy_evaporation &
                       ,potential_drainage_rate ,drain_rate, evap_rate, initial_canopy, co_mass_balance, dx, dz, tmp(3)
    ! local parameters
    double precision, parameter :: CanIntFrac = -0.5d0,     & ! Coefficient scaling rainfall interception fraction with LAI
                                        clump = 0.75d0,     & ! Clumping factor (1 = uniform, 0 totally clumped)
                                                              ! He et al., (2012) http://dx.doi.org/10.1016/j.rse.2011.12.008
                                  CanStorFrac = 0.2d0,      & ! Coefficient scaling canopy water storage with LAI
                                 RefDrainRate = 0.002d0,    & ! Reference drainage rate (mm/min; Rutter et al 1975)
                                  RefDrainLAI = 0.952381d0, & ! Reference drainage 1/LAI (m2/m2; Rutter et al 1975, 1/1.05)
                                 RefDrainCoef = 3.7d0,      & ! Reference drainage Coefficient (Rutter et al 1975)
                               RefDrainCoef_1 = RefDrainCoef ** (-1d0)

    ! hold initial canopy storage in memory
    initial_canopy = storage
    ! determine maximum canopy storage & through fall fraction
    through_fall = exp(CanIntFrac*mV%lai*clump)
    ! maximum canopy storage (mm); minimum is applied to prevent errors in
    ! drainage calculation. Assume minimum capacity due to wood.
    max_storage = max(min_storage,CanStorFrac*mV%lai)
    ! caclulate inverse for efficient calculations below
    max_storage_1 = max_storage**(-1d0)
    ! potential intercepted rainfall (kgH2O.m-2.s-1)
    mV%intercepted_rainfall = mV%rainfall * (1d0 - through_fall)

    ! calculate drainage coefficients (Rutter et al 1975); Corsican Pine
    ! 0.002 is canopy specific coefficient modified by 0.002*(max_storage/1.05)
    ! where max_storage is the canopy maximum capacity (mm) (LAI based) and
    ! 1.05 is the original canopy capacitance
    a = log( RefDrainRate * ( max_storage * RefDrainLAI ) ) - RefDrainCoef * max_storage

    ! average rainfall intercepted by canopy (kgH2O.m-2.day-1)
    daily_addition = mV%intercepted_rainfall * seconds_per_day

    ! reset cumulative variables
    through_fall = 0d0 ; wetcanopy_evaporation = 0d0
    drain_rate = 0d0 ; evap_rate = 0d0

    ! add rain to the canopy and overflow as needed
    storage = storage + daily_addition

    if (storage > max_storage) then

        if (potential_evaporation > 0d0) then

            ! Assume water drainage will always occur an order magnitude above evaporation
            ! so water above canopy capacity is drained. Water below max_storage is accessable by evaporation only.

            ! Assume drainage is all water above the maximum canopy storage (kg/m2/day)
            drain_rate = storage - max_storage

            ! Estimate evaporation from remaining water (i.e. that left after
            ! initial co-access of evaporation and drainage).
            ! Assume evaporation is now restricted by:
            ! 1) energy already spent on evaporation (the -evap_rate) and
            ! 2) linear increase in surface resistance as the leaf surface
            ! dries (i.e. the 0.5).
            evap_rate = min(potential_evaporation * 0.5d0 * storage * max_storage_1, storage - drain_rate)

        else

            ! Load dew formation to the current local evap_rate variable
            evap_rate = potential_evaporation
            ! Restrict drainage the quantity above max_storage, adding dew formation too
            drain_rate = (storage - evap_rate) - max_storage

        endif

    else

        ! no drainage just apply evaporation / dew formation fluxes directly
        drain_rate = 0d0 ; evap_rate = potential_evaporation
        if (evap_rate > 0d0) then
            ! evaporation restricted by fraction of surface actually covered
            ! in water and integrated over period to bare leaf (i.e. the *0.5)
            evap_rate = evap_rate * storage * max_storage_1 * 0.5d0
            ! and the total amount of water
            evap_rate = min(evap_rate,storage)
        else
            ! then dew formation has occurred, if this pushes storage > max_storage add it to drainage
            drain_rate = max(0d0,(storage - evap_rate) - max_storage)
        endif ! evap_rate > 0

    endif ! storage > max_storage

    ! update canopy storage with water flux
    storage = storage - evap_rate - drain_rate
    wetcanopy_evaporation = wetcanopy_evaporation + evap_rate
    through_fall = through_fall + drain_rate

    ! correct intercepted rainfall rate to kgH2O.m-2.s-1
    mV%intercepted_rainfall = mV%intercepted_rainfall - (through_fall * seconds_per_day_1)

!    ! sanity checks; note 1e-8 prevents precision errors causing flags
!    if (intercepted_rainfall > rainfall .or. storage < -1d-8 .or. &
!       (wetcanopy_evaporation * days_per_step_1) > (1d-8 + initial_canopy + (rainfall*seconds_per_day)) ) then
!        print*,"Condition 1",intercepted_rainfall > rainfall
!        print*,"Condition 2",storage < -1d-8
!        print*,"Condition 3",(wetcanopy_evaporation * days_per_step_1) > (1d-8 + initial_canopy + (rainfall*seconds_per_day))
!        print*,"storage (kgH2O/m2)",storage,"max_storage (kgH2O/m2)",max_storage,"initial storage (kgH2O/m2)", initial_canopy
!        print*,"rainfall (kgH2O/m2/day)", rainfall*seconds_per_day, "through_fall (kgH2O/m2/day)", (through_fall * days_per_step_1)
!        print*,"through_fall_total (kgH2O/m2/step)",through_fall
!        print*,"potential_evaporation (kgH2O/m2/day)",potential_evaporation
!        print*,"actual evaporation    (kgH2O/m2/day)",wetcanopy_evaporation * days_per_step_1
!        stop
!    endif

    ! average evaporative flux to daily rate (kgH2O/m2/day)
    potential_evaporation = wetcanopy_evaporation

    ! final clearance of canopy storage of version small values at the level of system precision
    if (storage < 10d0*vsmall) storage = 0d0

  end subroutine canopy_interception_and_storage
  !
  !-----------------------------------------------------------------
  !
  subroutine calculate_update_soil_water(Eleaf,Esoil,Esnow,rainfall_in,corrected_ET, mV)

    !
    ! Function updates the soil water status and layer thickness
    ! Soil water profile is updated in turn with evaporative losses,
    ! rainfall infiltration and gravitational drainage
    ! Root layer thickness is updated based on changes in the rooting depth from
    ! the previous step
    !

    implicit none

      type(model_working_variables) :: mV

    ! arguments
    double precision, intent(in) :: rainfall_in   ! rainfall (kgH2O.m-2.day-1)
    double precision, intent(inout) :: Eleaf, Esoil, Esnow ! evapotranspiration estimate (kgH2O.m-2.day-1)
    double precision, intent(out) :: corrected_ET      ! water balance corrected evapotranspiration (kgH2O/m2/day)

    ! local variables
    integer :: day, a
    double precision :: depth_change, water_change, initial_soilwater, balance, mass_check, &
                        Esoil_local, Esnow_local
    double precision, dimension(nos_root_layers) :: avail_flux, evaporation_losses, pot_evap_losses
    !logical :: iter_soil = .true.

    ! set soil water exchanges
    Esoil = 0d0 ; Esnow = 0d0 ; corrected_ET = 0d0 ; evaporation_losses = 0d0
    mV%underflow = 0d0 ; mV%runoff = 0d0 ; mV%infiltrated = 0d0 ; mV%water_grav_flow = 0d0 ; pot_evap_losses = 0d0
    initial_soilwater = 1d3 * sum(mV%soil_waterfrac(1:nos_soil_layers) * mV%layer_thickness(1:nos_soil_layers))

    !! Assume leaf transpiration is drawn from the soil based on the
    !! update_fraction estimated in calculate_Rtot
    pot_evap_losses = Eleaf * mV%uptake_fraction
    !! Assume all soil evaporation comes from the soil surface only
    !pot_evap_losses(1) = pot_evap_losses(1) + Esoil

! Conditions under which iterative solution should not be needed...
! Scenario 1
!          (i) Soil layers at or below field capacity, therefore no drainage
!         (ii) The existing water supply and rainfall can support evaporative demanded by the canopy and soil
!     Outcome: Extract all needed water, potentially leaving soil in negetative status, followed by infilatration.
!              Allow for drainage if soil is above field capacity as a result of this proecss
! Scenario 2
!          (i) Soil layers ABOVE field capacity, therefore THERE is drainage
!         (ii) The existing water supply and rainfall can support evaporative demanded by the canopy and soil
!     Outcome: Extract allow water and add all infiltration into the soil.
!              Allow for drainage in the final instance as strongly exponential drainage flow should negate time difference.
!              NOTE: that this may bias between runoff and underflow estimation
! UPDATE (19/04/2024): Soil layer above or near field capacity do require daily iteration or other solution at a later time
!                      due to the non-linear effects on soil surface evaporation as a result of drythick development

    ! to allow for smooth water balance integration carry this out at daily time step
    ! NOTE: Should inestigate conditions where we don't need to do all the days.
    !       For example, can we check how the various terms are changing and whether
    !       we can assume an average for the rest of the time step?
    do day = 1, nint(mV%days_per_step)
       ! Possible conditions for avoiding looping all days
       ! 1) When drythick == min_drythick and rainfall_in > Esoil in first day
       ! 2) If initially, drythick > min_drythick, but rainfall_in > Esoil, iteration can stop once drythick == min_drythick
       ! 3) If drythick == min_drythick and rainfall_in < Esoil in first day, iteration can still be avoided if the time step multiple does not result in drythick >  min_drythick
       ! 4)

       !!!!!!!!!!
       ! Evaporative losses
       !!!!!!!!!!

       ! Estimate drythick for the current step
       !drythick = max(min_drythick, top_soil_depth * max(0d0,(1d0 - (soil_waterfrac(1) / field_capacity(1)))))
       mV%drythick = max(min_drythick, top_soil_depth * max(0d0,(1d0 - (mV%soil_waterfrac(1) / mV%porosity(1)))))
       ! Soil surface (kgH2O.m-2.day-1)
       call calculate_soil_evaporation(Esoil_local, mV)

       ! If snow present assume that soil evaporation is sublimation of soil first
       if (Esoil_local > 0d0 .and. mV%snow_storage > 0d0) then
           if (mV%snow_storage > Esoil_local) then
               Esnow_local = Esoil_local
               mV%snow_storage = mV%snow_storage - Esnow_local
               Esoil_local = 0d0
           else
               Esnow_local = mV%snow_storage
               Esoil_local = Esoil_local - Esnow_local
               mV%snow_storage = 0d0
           end if
       else
           Esnow_local = 0d0
       end if
       ! Accumulate overall time step soil and snow evaporation
       Esoil = Esoil + Esoil_local
       Esnow = Esnow + Esnow_local

       ! load transpiration losses from soil profile
       evaporation_losses = pot_evap_losses
       ! Update with current daily estimate of soil evaporation losses, surface only
       evaporation_losses(1) = evaporation_losses(1) + Esoil_local

       ! can not evaporate from soil more than is available (m -> mm)
       ! NOTE: This is due to the fact that both soil evaporation and transpiration
       !       are drawing from the same water supply.
       avail_flux = mV%soil_waterfrac(1:nos_root_layers) * mV%layer_thickness(1:nos_root_layers) * 1d3
       do a = 1, nos_root_layers ! note: timed comparison between "where" and do loop supports do loop for smaller vectors
          if (evaporation_losses(a) > avail_flux(a)) evaporation_losses(a) = avail_flux(a) * 0.9999d0
       end do
       ! this will update the ET estimate outside of the function
       ! days_per_step corrections happens outside of the loop below
       corrected_ET = corrected_ET + sum(evaporation_losses)

       ! adjust water already committed to evaporation
       ! convert kg.m-2 (or mm) -> Mg.m-2 (or m)
       mV%soil_waterfrac(1:nos_root_layers) = mV%soil_waterfrac(1:nos_root_layers) &
                                         + ((-evaporation_losses(1:nos_root_layers)*1d-3) / mV%layer_thickness(1:nos_root_layers))

       ! Correct for dew formation; any water above porosity in the top layer is assumed runoff
       ! NOTE: layer_thickness * 1d3 scales between m3/m3 to kg/m2
       ! Worth investigating whether this term is actually important, dew formation in unlikely
       ! to be large. Therefore, maybe a conditional statment and calculation unrequired
       if (mV%soil_waterfrac(1) > mV%porosity(1)) then
           mV%runoff = mV%runoff + ((mV%soil_waterfrac(1)-mV%porosity(1)) * mV%layer_thickness(1) * 1d3)
           mV%soil_waterfrac(1) = mV%porosity(1)
       endif

       !!!!!!!!!!
       ! Rainfall infiltration drainage
       !!!!!!!!!!

       ! Determine infiltration from rainfall (kgH2O/m2/day),
       ! if rainfall is probably liquid / soil surface is probably not frozen
       ! reset soil water change variable
       mV%waterchange = 0d0
       call infiltrate(rainfall_in, mV)
       ! update soil profiles. Convert fraction into depth specific values
       ! (rather than m3/m3) then update fluxes
       mV%soil_waterfrac(1:nos_soil_layers) = mV%soil_waterfrac(1:nos_soil_layers) &
                                         + (mV%waterchange(1:nos_soil_layers) / mV%layer_thickness(1:nos_soil_layers))
       ! soil waterchange variable reset in gravitational_drainage()

       !!!!!!!!!!
       ! Gravitational drainage
       !!!!!!!!!!

       ! Determine drainage flux between surface -> sub surface
       call gravitational_drainage(1, mV)

    end do ! days_per_step

    ! apply time step correction kgH2O/m2/step -> kgH2O/m2/day
    mV%water_grav_flow = mV%water_grav_flow * mV%days_per_step_1
    mV%infiltrated = mV%infiltrated * mV%days_per_step_1
    corrected_ET = corrected_ET * mV%days_per_step_1
    mV%underflow = mV%underflow * mV%days_per_step_1
    mV%runoff = mV%runoff * mV%days_per_step_1
    Esoil = Esoil * mV%days_per_step_1
    Esnow = Esnow * mV%days_per_step_1

    ! Based on the soil mass balance corrected_ET, make assumptions to correct Eleaf and Esnow
    balance = (corrected_ET-Eleaf) / Esoil
    if (balance < 1d0 .and. balance > 0d0) Esoil = Esoil * balance

    ! Update corrected_ET with snow sublimation
    corrected_ET = corrected_ET + Esnow

    !!!!!!!!!!
    ! Update soil layer thickness
    !!!!!!!!!!

    depth_change = (top_soil_depth+min_layer) ; water_change = 0
    ! if roots extent down into the bucket
    if (mV%root_reach > depth_change) then

        !!!!!!!!!!
        ! Soil profile is within the bucket layer (layer 3)
        !!!!!!!!!!

        if (mV%previous_depth > depth_change) then
            ! how much has root depth extended since last step?
            depth_change = mV%root_reach - mV%previous_depth
        else
            ! how much has root depth extended since last step?
            depth_change = mV%root_reach - depth_change
        endif

        ! if there has been an increase
        if (depth_change > 0.05d0) then

            ! determine how much water (mm) is within the new volume of soil
            water_change = mV%soil_waterfrac(nos_soil_layers) * depth_change
            ! now assign that new volume of water to the deep rooting layer
            mV%soil_waterfrac(nos_root_layers) = ((mV%soil_waterfrac(nos_root_layers)*mV%layer_thickness(nos_root_layers))+water_change) &
                                            / (mV%layer_thickness(nos_root_layers)+depth_change)

            ! explicitly update the soil profile if there has been rooting depth
            ! changes
            mV%layer_thickness(1) = top_soil_depth
            mV%layer_thickness(2) = mV%root_reach - top_soil_depth
            mV%layer_thickness(3) = mV%max_depth - sum(mV%layer_thickness(1:2))

            ! keep track of the previous rooting depth
            mV%previous_depth = mV%root_reach

        else if (depth_change < -0.05d0) then

            ! make positive to ensure easier calculations
            depth_change = -depth_change

            ! determine how much water is lost from the old volume of soil
            water_change = mV%soil_waterfrac(nos_root_layers) * depth_change
            ! now assign that new volume of water to the deep rooting layer
            mV%soil_waterfrac(nos_soil_layers) = ((mV%soil_waterfrac(nos_soil_layers)*mV%layer_thickness(nos_soil_layers))+water_change) &
                                            / (mV%layer_thickness(nos_soil_layers)+depth_change)

            ! explicitly update the soil profile if there has been rooting depth
            ! changes
            mV%layer_thickness(1) = top_soil_depth
            mV%layer_thickness(2) = mV%root_reach - top_soil_depth
            mV%layer_thickness(3) = mV%max_depth - sum(mV%layer_thickness(1:2))

            ! keep track of the previous rooting depth
            mV%previous_depth = mV%root_reach

        else

            ! keep track of the previous rooting depth
            mV%previous_depth = mV%previous_depth

        end if ! depth change

    else if (mV%root_reach < depth_change .and. mV%previous_depth > depth_change) then

        !!!!!!!!!!
        ! Model has explicitly contracted from the bucket layer
        !!!!!!!!!!

        ! In this circumstance we want to return the soil profile to it's
        ! default structure with a minimum sized third layer
        depth_change = mV%previous_depth - depth_change

        ! determine how much water is lost from the old volume of soil
        water_change = mV%soil_waterfrac(nos_root_layers) * depth_change
        ! now assign that new volume of water to the deep rooting layer
        mV%soil_waterfrac(nos_soil_layers) = ((mV%soil_waterfrac(nos_soil_layers)*mV%layer_thickness(nos_soil_layers))+water_change) &
                                        / (mV%layer_thickness(nos_soil_layers)+depth_change)

        ! explicitly update the soil profile if there has been rooting depth
        ! changes
        mV%layer_thickness(1) = top_soil_depth
        mV%layer_thickness(2) = min_layer
        mV%layer_thickness(3) = mV%max_depth - sum(mV%layer_thickness(1:2))

        ! keep track of the previous rooting depth
        mV%previous_depth = min_layer

    else ! root_reach > (top_soil_depth + min_layer)

        ! if we are outside of the range when we need to consider rooting depth changes keep track in case we move into a zone when we do
        mV%previous_depth = mV%previous_depth

    endif ! root reach beyond top layer

    ! Update soil water potential
    call soil_water_potential(mV)

!    ! check water balance
!    balance = (rainfall_in - corrected_ET - underflow - runoff) * days_per_step
!    balance = balance &
!            - (sum(soil_waterfrac(1:nos_soil_layers) * layer_thickness(1:nos_soil_layers) * 1d3) &
!            - initial_soilwater)
!
!    if (abs(balance) > 1d-6 .or. soil_waterfrac(1) < -1d-6) then
!        print*,"Soil water miss-balance (mm)",balance
!        print*,"Initial_soilwater (mm) = ",initial_soilwater
!        print*,"Final_soilwater (mm) = ",sum(soil_waterfrac(1:nos_soil_layers) * layer_thickness(1:nos_soil_layers) * 1d3)
!        print*,"State balance = ",sum(soil_waterfrac(1:nos_soil_layers)*layer_thickness(1:nos_soil_layers)*1d3)-initial_soilwater
!        print*,"Flux balance = ",(rainfall_in - corrected_ET - underflow - runoff) * days_per_step
!        print*,"Top soilwater (fraction)",soil_waterfrac(1)
!        print*,"Rainfall (mm/step)",rainfall_in,"ET",corrected_ET,"underflow",underflow,"runoff",runoff
!        print*,"Rainfall (kgH2O/m2/s)",rainfall
!        print*,"Soil Water Fraction = ",soil_waterfrac
!    end if ! abs(balance) > 1d-10

    ! explicit return needed to ensure that function runs all needed code
    return

  end subroutine calculate_update_soil_water
  !
  !-----------------------------------------------------------------
  !
  subroutine infiltrate(rainfall_in, mV)

    ! Takes surface_watermm and distributes it among top !
    ! layers. Assumes total infilatration in timestep.   !
    ! NOTE: Assumes that any previous water movement due to infiltration and evaporation
    !       has already been updated in soil mass balance

    implicit none

      type(model_working_variables) :: mV

    ! arguments
    double precision, intent(in) :: rainfall_in ! rainfall (kgH2O.m-2.day-1)

    ! local argumemts
    integer :: i
    double precision :: &
            pot_runoff, & ! Potential rate of runoff (MgH2O/m2/day)
                   add, & ! surface water available for infiltration (m)
                 wdiff    ! available space in a given soil layer for water to fill (m)

    ! Parameters
    double precision, parameter :: beta_s = 0.5d0 ! Infiltration enhancement factor (See Best et al., 2011, Table 6)
                                                  ! i.e. if this was == 1 then there would be more infiltration
    ! convert rainfall water from mm -> m (or kgH2O.m-2.day-1 -> MgH2O.m-2.day-1)
    add = rainfall_in * 1d-3

    ! Estimate the potential infiltration rate, assumed as half the conductivity rate at porosity.
    ! This is consistent with JULES, ORCHIDEE, CLM models as examples.
    ! The *0.5 is the Beta_s factor taken from JULES, but other values are used in the other models.
    call calculate_soil_conductivity(1,mV%porosity(1),pot_runoff, mV)
    ! Scaled to 2.5 hours, on the mean number of hours over which rainfall typically occurs.
    ! This is based on reported precipitation time series from global fluxnet2025 database.
    pot_runoff = pot_runoff * seconds_per_day * (2.5d0/24d0) * beta_s
    pot_runoff = add * exp(-(pot_runoff / add)) ! potential runoff
    ! Update cumulative runoff and substract from the rainfall addition
    mV%runoff = mV%runoff + (pot_runoff*1d3) ; add = add - pot_runoff ; pot_runoff = 0d0

    ! Loop through soil layers to drain
    do i = 1 , nos_soil_layers

       ! is the input of water greater than available space
       ! if so fill and subtract from input and move on to the next
       ! layer determine the available pore space in current soil layer
       wdiff = max(0d0,(mV%porosity(i)-mV%soil_waterfrac(i)) * mV%layer_thickness(i))

       if (add > wdiff) then
           ! if so fill and subtract from input and move on to the next layer
           mV%waterchange(i) = mV%waterchange(i) + wdiff
           add = add - wdiff
       else
           ! otherwise infiltate all in the current layer
           mV%waterchange(i) = mV%waterchange(i) + add
           add = 0d0 ; exit
       end if

    end do ! nos_soil_layers

    ! if after all of this we have some water left assume it is runoff (kgH2O.m-2.day-1)
    ! NOTE that runoff is reset outside of the daily soil loop
    mV%runoff = mV%runoff + (add * 1d3)
    mV%infiltrated = mV%infiltrated + (mV%waterchange(1:nos_soil_layers) * 1d3)

  end subroutine infiltrate
  !
  !-----------------------------------------------------------------
  !
!  subroutine gravitational_drainage(time_period_days)
!
!    ! Integrator for soil gravitational drainage.
!    ! Due to the longer time steps undertake by ACM / DALEC and the fact that
!    ! drainage is a concurrent processes we assume that drainage occurs at
!    ! the bottom of the column first creating space into which water can drain
!    ! from the top down. Therefore we draing from the bottom first and then the top.
!    ! NOTE: Assumes that any previous water movement due to infiltration and evaporation
!    !       has already been updated in soil mass balance
!
!    implicit none
!
!    ! arguments
!    integer, intent(in) :: time_period_days
!
!    ! local variables..
!    integer :: t
!    double precision, dimension(nos_soil_layers) :: dx, & ! range between the start and end points of the integration
!                                               halfway, & ! half way point between start and end point of integration
!                                                liquid, & ! liquid water in local soil layer (m3/m3)
!                                         avail_to_flow, & ! liquid content above field capacity (m3/m3)
!                                               iceprop, & ! fraction of soil layer which is ice
!                                          pot_drainage    ! estimats of time step potential drainage rate (m/s)
!    double precision  :: tmp1,tmp2,tmp3 &
!                                 ,unsat & ! unsaturated pore space in soil_layer below the current (m3/m3)
!                                ,change   ! absolute volume of water drainage in current layer (m3/day)
!
!    ! calculate soil ice proportion; at the moment
!    ! assume everything liquid
!    iceprop = 0d0
!
!    ! except the surface layer in the mean daily temperature is < 0oC
!    if (meant < 1d0) iceprop(1) = 1d0
!
!    ! zero water fluxes
!    waterchange = 0d0
!
!    ! underflow and water_grav_flow are tracked in kgH2O/m2/day but estimated here in MgH2O/m2/day
!    ! therefore we must convert
!    underflow = underflow * 1d-3
!    water_grav_flow = water_grav_flow * 1d-3
!
!    ! estimate potential drainage rate for the current time period
!    liquid = soil_waterfrac(1:nos_soil_layers) * ( 1d0 - iceprop(1:nos_soil_layers) )
!    ! estimate how much liquid is available to flow
!    avail_to_flow = liquid - field_capacity(1:nos_soil_layers)
!    ! trapezium rule scaler and the half-way point between current and field capacity
!    dx = avail_to_flow*0.5d0 ; halfway = liquid - dx
!    do t = 1, nos_soil_layers
!       if (avail_to_flow(t) > 0d0) then
!           ! Trapezium rule for approximating integral of drainage rate
!           call calculate_soil_conductivity(t,liquid(t),tmp1)
!           call calculate_soil_conductivity(t,field_capacity(t),tmp2)
!           call calculate_soil_conductivity(t,halfway(t),tmp3)
!           pot_drainage(t) = 0.5d0 * dx(t) * ((tmp1 + tmp2) + 2d0 * tmp3)
!       else
!           ! We are at field capacity currently even after rainfall has been infiltrated.
!           ! Assume that the potential drainage rate is that at field capacity
!           call calculate_soil_conductivity(t,field_capacity(t),pot_drainage(t))
!       endif ! water above field capacity to flow?
!    end do ! soil layers
!    ! Scale potential drainage from per second to per day
!    pot_drainage = pot_drainage * seconds_per_day
!
!    ! Integrate drainage over each day until time period has been reached or
!    ! each soil layer has reached field capacity
!    t = 1
!    do while (t < (time_period_days+1) .and. maxval(soil_waterfrac - field_capacity) > vsmall)
!
!       ! Estimate liquid content and how much is available to flow / drain
!       avail_to_flow = ( soil_waterfrac(1:nos_soil_layers) * (1d0 - iceprop(1:nos_soil_layers)) ) &
!                     - field_capacity(1:nos_soil_layers)
!
!       ! ...then from the top down
!       do soil_layer = 1, nos_soil_layers
!
!          ! initial conditions; i.e. is there liquid water and more water than
!          ! layer can hold
!          if (avail_to_flow(soil_layer) > 0d0 .and. soil_waterfrac(soil_layer+1) < porosity(soil_layer+1)) then
!
!              ! Unsaturated volume of layer below (m3 m-2)
!              unsat = ( porosity(soil_layer+1) - soil_waterfrac(soil_layer+1) ) &
!                    * layer_thickness(soil_layer+1) / layer_thickness(soil_layer)
!              ! Restrict potential rate calculate above for the available water
!              ! and available space in the layer below.
!              ! NOTE: * layer_thickness(soil_layer) converts units from m3/m2 -> (m3)
!              change = min(unsat,min(pot_drainage(soil_layer),avail_to_flow(soil_layer))) * layer_thickness(soil_layer)
!              ! update soil layer below with drained liquid
!              waterchange( soil_layer + 1 ) = waterchange( soil_layer + 1 ) + change
!              waterchange( soil_layer     ) = waterchange( soil_layer     ) - change
!              ! Also track only the positive flows from one layer to another (MgH2O/m2/day)
!              water_grav_flow(soil_layer) = water_grav_flow(soil_layer) + change
!
!          end if ! some liquid water and drainage possible
!
!       end do ! soil layers
!
!       ! update soil water profile
!       soil_waterfrac(1:nos_soil_layers) = soil_waterfrac(1:nos_soil_layers) &
!                                         + (waterchange(1:nos_soil_layers)/layer_thickness(1:nos_soil_layers))
!       ! estimate drainage from bottom of soil column (MgH2O/m2/day)
!       ! NOTES: that underflow is reset outside of the daily soil loop
!       underflow = underflow + waterchange(nos_soil_layers+1)
!
!       ! Reset now we have moves that liquid
!       waterchange = 0d0
!       ! integerate through time period
!       t = t + 1
!
!    end do ! while condition
!
!    ! convert underflow and water_grav_flow from MgH2O/m2/day -> kgH2O/m2/day
!    underflow = underflow * 1d3
!    water_grav_flow = water_grav_flow * 1d3
!
!  end subroutine gravitational_drainage
  !
  !-----------------------------------------------------------------
  !
  subroutine gravitational_drainage(time_period_days, mV)

    ! Integrator for soil gravitational drainage.
    ! Due to the longer time steps undertake by ACM / DALEC and the fact that
    ! drainage is a concurrent processes we assume that drainage occurs at
    ! the bottom of the column first creating space into which water can drain
    ! from the top down. Therefore we draing from the bottom first and then the top.
    ! NOTE: Assumes that any previous water movement due to infiltration and evaporation
    !       has already been updated in soil mass balance

    implicit none

      type(model_working_variables) :: mV

    ! arguments
    integer, intent(in) :: time_period_days

    ! local variables..
    integer :: t, d, s
    double precision, dimension(nos_soil_layers+1) :: soil_waterfrac_local ! local copy of soil water content (m3.m-3)
    double precision, dimension(nos_soil_layers) :: dx, & ! range between the start and end points of the integration
                                               halfway, & ! half way point between start and end point of integration
                                         avail_to_flow, & ! liquid content above field capacity (m3/m3)
                                       liquid_fraction    ! fraction of soil layer which is liquid
    double precision, parameter :: integration_step_seconds = 1800d0*16d0, &
                                   sixth = (1d0/6d0)
    double precision  :: tmp1,tmp2,tmp3, tmp4 &
                             ,pot_drainage_k0 & ! estimates of time step potential drainage rate (m/s)
                             ,pot_drainage_k1 &
                             ,pot_drainage_k2 &
                             ,pot_drainage_k3 &
                             ,pot_drainage_k4 &
                                      ,liquid & ! liquid water in local soil layer (m3/m3)
                                       ,unsat & ! unsaturated pore space in soil layer below the current (m3/m3)
                                      ,change   ! absolute volume of water drainage in current layer (m3/day)

    ! Reset
    liquid_fraction = 1d0 ; mV%waterchange = 0d0 ; d = 1

    ! calculate soil ice proportion;
    ! except the surface layer in the mean daily temperature is < 0oC
    if (mV%meant < 1d0) liquid_fraction(1) = 0d0

    ! underflow and water_grav_flow are tracked in kgH2O/m2/day but estimated here in MgH2O/m2/day
    ! therefore we must convert
    mV%underflow = mV%underflow * 1d-3
    mV%water_grav_flow = mV%water_grav_flow * 1d-3

    ! Estimate liquid content and how much is available to flow / drain
    avail_to_flow = (mV%soil_waterfrac(1:nos_soil_layers) * liquid_fraction(1:nos_soil_layers) ) &
                  - mV%field_capacity(1:nos_soil_layers)

    ! Integrate drainage over each 30 min within day until time period has been reached or
    ! each soil layer has reached field capacity
    do while (d < 4 .and. maxval(avail_to_flow) > vsmall)

        ! ...then from the top down
        do s = 1, nos_soil_layers

           ! Determine whethere we have any liquid water in the current layer available to flow and the layer below is
           ! able to accept any water (i.e. is less than porosity).
           if (avail_to_flow(s) > 0d0 .and. mV%soil_waterfrac(s+1) < mV%porosity(s+1)) then

               ! Reset the soil water change variable, this must be done at the beginning of the loop
               ! to ensure that its content are left for extracting the underflow flux after the loop.
               mV%waterchange = 0d0

               !! Implement an explicit 4th order Runge-Kutta approach, to estimate the aggregated flux
               !
               ! Mathematical basis
               ! ------------------
               ! RK4 approximates the solution of  dS/dt = f(t, S)  over a step [t, t+dt]
               ! by evaluating f at four points within the interval and forming a weighted
               ! average of the resulting slopes:
               !
               !   k1 = f(t,          S)                   slope at the start
               !   k2 = f(t + dt/2,   S + dt/2 * k1)       slope at the midpoint (Euler)
               !   k3 = f(t + dt/2,   S + dt/2 * k2)       slope at the midpoint (corrected)
               !   k4 = f(t + dt,     S + dt   * k3)       slope at the end
               !
               !   S_new = S + (dt/6) * (k1 + 2*k2 + 2*k3 + k4)
               !
               ! The weighting (1, 2, 2, 1)/6 gives Simpson's-rule-like accuracy.

               ! Load the current soil water content into a local variable to be updated
               soil_waterfrac_local = mV%soil_waterfrac
               ! Estimate the local liquid content in the current layer
               liquid = soil_waterfrac_local(s) * liquid_fraction(s)

               !! Estimate K1 - rate at the start
               ! Estimate the soil water conductance (k1) at the start of the step
               call calculate_soil_conductivity(s,liquid,pot_drainage_k1, mV)

               !! Estimate K2 - rate at the mid-point
               ! Increment the soil layer using the initial rate (k1) for half the time step
               pot_drainage_k0 = pot_drainage_k1 * (integration_step_seconds * 0.5d0)
               call gravitational_drainage_local_update(s,soil_waterfrac_local,pot_drainage_k0,liquid_fraction(s), &
                                                        mV%layer_thickness,mV%field_capacity(s),mV%porosity)
               ! Estimate the soil water conductance at this new state
               liquid = (soil_waterfrac_local(s) * liquid_fraction(s) )
               call calculate_soil_conductivity(s,liquid,pot_drainage_k2, mV)

               !! Estimate K3 - rate at the corrected mid-point
               ! Load the current soil water content into a local variable to be updated
               soil_waterfrac_local = mV%soil_waterfrac
               ! Increment the soil layer using the mid-point rate (k2) for half the time step
               pot_drainage_k0 = pot_drainage_k2 * (integration_step_seconds * 0.5d0)
               call gravitational_drainage_local_update(s,soil_waterfrac_local,pot_drainage_k0,liquid_fraction(s), &
                                                        mV%layer_thickness,mV%field_capacity(s),mV%porosity)
               ! Estimate the soil water conductance at this new state
               liquid = (soil_waterfrac_local(s) * liquid_fraction(s) )
               call calculate_soil_conductivity(s,liquid,pot_drainage_k3, mV)

               !! Estimate K4 - rate at the end
               ! Load the current soil water content into a local variable to be updated
               soil_waterfrac_local = mV%soil_waterfrac
               ! Increment the soil layer using the mid-point rate (k3) for the full time step
               pot_drainage_k0 = pot_drainage_k3 * integration_step_seconds
               call gravitational_drainage_local_update(s,soil_waterfrac_local,pot_drainage_k0,liquid_fraction(s), &
                                                        mV%layer_thickness,mV%field_capacity(s),mV%porosity)
               ! Estimate the soil water conductance at this new state
               liquid = (soil_waterfrac_local(s) * liquid_fraction(s) )
               call calculate_soil_conductivity(s,liquid,pot_drainage_k4, mV)

               ! Calculate the Simpson's rule weighted average of the rates to estimate the effective average
               ! Note the multiplication to the step time period.
               pot_drainage_k0 = integration_step_seconds * sixth &
                               * (pot_drainage_k1 + 2d0*pot_drainage_k2 + 2d0*pot_drainage_k3 + pot_drainage_k4)

               !! Now update soils for real

               ! Unsaturated volume of layer below (m3 m-2)
               unsat = ( mV%porosity(s+1) - mV%soil_waterfrac(s+1) ) * mV%layer_thickness(s+1) / mV%layer_thickness(s)
               ! Restrict potential rate calculate above for the available water
               ! and available space in the layer below.
               ! NOTE: * layer_thickness(s) converts units from m3/m2 -> (m3)
               change = min(unsat,min(pot_drainage_k0,avail_to_flow(s))) * mV%layer_thickness(s)
               ! update soil layer below with drained liquid
               mV%waterchange( s + 1 ) = mV%waterchange( s + 1 ) + change
               mV%waterchange( s     ) = mV%waterchange( s     ) - change
               ! Also track only the positive flows from one layer to another (MgH2O/m2/day)
               mV%water_grav_flow(s) = mV%water_grav_flow(s) + change

               ! Update the current and below layer, note to avoid a min() bound being used we are allowing the core layer to be updated to.
               ! This MUST be corrected outside of this loop back to the field capacity
               mV%soil_waterfrac(s:(s+1)) = mV%soil_waterfrac(s:(s+1)) + (mV%waterchange(s:(s+1))/mV%layer_thickness(s:(s+1)))

               ! Update the available liquid content for drainage
               avail_to_flow = (mV%soil_waterfrac(1:nos_soil_layers) * liquid_fraction(1:nos_soil_layers) ) &
                             - mV%field_capacity(1:nos_soil_layers)

           end if ! some liquid water and drainage possible

        end do ! soil layers

        ! Return the core layer back to field capacity
        mV%soil_waterfrac(nos_soil_layers+1) = mV%field_capacity(nos_soil_layers)
        ! Extract drainage from bottom of soil column (MgH2O/m2/day)
        ! NOTES: that underflow is reset outside of the daily soil loop
        mV%underflow = mV%underflow + mV%waterchange(nos_soil_layers+1)

        ! Reset now we have moves that liquid
        mV%waterchange = 0d0
        ! integerate through time period
        d = d + 1

    end do ! while condition

    ! convert underflow and water_grav_flow from MgH2O/m2/day -> kgH2O/m2/day
    mV%underflow = mV%underflow * 1d3
    mV%water_grav_flow = mV%water_grav_flow * 1d3

  end subroutine gravitational_drainage
  !
  !-----------------------------------------------------------------
  !
  subroutine gravitational_drainage_local_update(s,soil_waterfrac_local,pot_drainage,liquid_fraction, &
                                                 layer_thickness_local,field_capacity_local,porosity_local)

     ! Subroutine will update a local copy of the soil water fraction for each soil layer

     ! Arguments
     integer, intent(in) :: s
     double precision, intent(in) :: liquid_fraction, & ! Fraction of current layer assumed to be liquid (0-1)
                                        pot_drainage, & ! Proprosed drainage for current layer (m/step)
                                field_capacity_local    ! Field capacity of the current layer (m3/m3)
     double precision, dimension(nos_soil_layers+1), intent(inout) :: soil_waterfrac_local ! Local copy of the soil water fraction (m3/m3)
     double precision, dimension(nos_soil_layers+1), intent(in) :: layer_thickness_local, & ! Local copy of the soil layer thickness (m)
                                                                          porosity_local    ! Local copy of the soil layer porosities (m3/m3)

     ! Local variables
     double precision :: unsat, change, avail_to_flow
     double precision, dimension(nos_soil_layers+1) :: waterchange_local

     ! Initialise
     waterchange_local = 0d0 ; change = 0d0 ; unsat = 0d0 ; avail_to_flow = 0d0

     ! Determine how much liquid water is available to flow in the current profile
     avail_to_flow = (soil_waterfrac_local(s) * liquid_fraction ) - field_capacity_local

     ! Determine whethere we have any liquid water in the current layer available to flow and the layer below is
     ! able to accept any water (i.e. is less than porosity).
     if (avail_to_flow > 0d0 .and. soil_waterfrac_local(s+1) < porosity_local(s+1)) then

          ! Determine the unsaturated volume of layer below (m3 m-2)
          unsat = ( porosity_local(s+1) - soil_waterfrac_local(s+1) ) &
                * layer_thickness_local(s+1) / layer_thickness_local(s)
          ! Restrict potential rate calculate above for the available water
          ! and available space in the layer below.
          ! NOTE: * layer_thickness_local(s) converts units from m3/m2 -> (m3)
          change = min(unsat,min(pot_drainage,avail_to_flow)) * layer_thickness_local(s)
          ! update soil layer below with drained liquid
          waterchange_local( s + 1 ) = waterchange_local( s + 1 ) + change
          waterchange_local( s     ) = waterchange_local( s     ) - change

     end if ! some liquid water and drainage possible

    ! Update soil water profile, just for the current layer and below
    soil_waterfrac_local(s:nos_soil_layers) = soil_waterfrac_local(s:nos_soil_layers) &
                                            + (waterchange_local(s:nos_soil_layers)/layer_thickness_local(s:nos_soil_layers))

    ! Return back to user
    return

  end subroutine gravitational_drainage_local_update
  !
  !-----------------------------------------------------------------
  !
  subroutine soil_porosity(soil_frac_clay,soil_frac_sand, mV)

    ! Porosity is estimated from Saxton equations. !

    implicit none

      type(model_working_variables) :: mV

    ! arguments
    double precision, dimension(nos_soil_layers) :: soil_frac_clay &
                                                   ,soil_frac_sand
    ! local variables..
    double precision, parameter :: H = 0.332d0, &
                                 J = -7.251d-4, &
                                  K = 0.1276d0

    ! loop over soil layers..
    mV%porosity(1:nos_soil_layers) = H + J * soil_frac_sand(1:nos_soil_layers) + &
                                  K * log10(soil_frac_clay(1:nos_soil_layers))
    ! then assign same to core layer
    mV%porosity(nos_soil_layers+1) = mV%porosity(nos_soil_layers)

  end subroutine soil_porosity
  !
  !---------------------------------------------------------------------
  !
  subroutine initialise_soils(soil_frac_clay,soil_frac_sand, mV)

    !
    ! Subroutine calculate the soil layers field capacities and sets the initial
    ! soil water potential set to field capacity
    !

    implicit none

      type(model_working_variables) :: mV

    ! arguments
    double precision, dimension(nos_soil_layers) :: soil_frac_clay &
                                                   ,soil_frac_sand

    ! local variables
    integer :: i

    ! Include some hardcoded boundaries for the Saxton equations
    ! NOTE: do loop was found to be faster than 'where' for small vectors
    do i = 1, nos_soil_layers
       if (soil_frac_sand(i) < 5d0) soil_frac_sand(i) = 5d0
       if (soil_frac_clay(i) < 5d0) soil_frac_clay(i) = 5d0
       !if (soil_frac_sand(i) > 60d0) soil_frac_sand(i) = 60d0
       if (soil_frac_sand(i) > 70d0) soil_frac_sand(i) = 70d0
    end do
    ! calculate soil porosity (m3/m3)
    call soil_porosity(soil_frac_clay,soil_frac_sand, mV)
    ! calculate field capacity (m3/m-3)
    call calculate_field_capacity(mV)

    ! final sanity check for porosity
    do i = 1, nos_soil_layers+1
       if (mV%porosity(i) < (mV%field_capacity(i)+0.05d0)) mV%porosity(i) = mV%field_capacity(i) + 0.05d0
    end do

  end subroutine initialise_soils
  !
  !---------------------------------------------------------------------
  !
  subroutine update_soil_initial_conditions(input_soilwater_frac, mV)

    !
    ! Subroutine calculate the soil layers field capacities and sets the initial
    ! soil water potential set to field capacity
    !

    implicit none

      type(model_working_variables) :: mV

    ! arguments
    double precision, intent(in) :: input_soilwater_frac ! initial soil water status as fraction of field capacity

    ! local variables
    integer :: i

    ! Load initial soil water fraction to the dynamic layers
    mV%soil_waterfrac(1:nos_soil_layers) = input_soilwater_frac
    ! Assume that the 'core' soil layer is field capacity
    mV%soil_waterfrac(nos_soil_layers+1) = mV%field_capacity(nos_soil_layers)
    ! calculate initial soil water potential
    call soil_water_potential(mV)

    ! Seperately calculate the soil conductivity as this applies to each layer
    do i = 1, nos_soil_layers
       call calculate_soil_conductivity(i,mV%soil_waterfrac(i),mV%soil_conductivity(i), mV)
    end do ! soil layers
    ! but apply the lowest soil layer to the core as well in initial conditions
    mV%soil_conductivity(nos_soil_layers+1) = mV%soil_conductivity(nos_soil_layers)

  end subroutine update_soil_initial_conditions
  !
  !-----------------------------------------------------------------
  !
  subroutine calculate_soil_conductivity(soil_layer,waterfrac,conductivity, mV)

    ! Calculate the soil conductivity (m s-1) of water based on soil
    ! characteristics and current water content

    implicit none

      type(model_working_variables) :: mV

    ! arguments
    integer, intent(in) :: soil_layer
    double precision, intent(in) :: waterfrac
    double precision, intent(out) :: conductivity

    ! soil conductivity for the dynamic soil layers (i.e. not including core)
    conductivity = mV%cond1(soil_layer) * exp(mV%cond2(soil_layer)+mV%cond3(soil_layer)/waterfrac)

    ! protection against floating point error
    if (waterfrac < 0.05d0) conductivity = 1d-30

  end subroutine calculate_soil_conductivity
  !
  !------------------------------------------------------------------
  !
  subroutine saxton_parameters(soil_frac_clay,soil_frac_sand, mV)

    ! Calculate the key parameters of the Saxton, that is cond1,2,3 !
    ! and potA,B                                                    !

    implicit none

      type(model_working_variables) :: mV

    ! arguments
    double precision, dimension(nos_soil_layers) :: soil_frac_clay &
                                                   ,soil_frac_sand

    ! local variables
    double precision, parameter :: A = -4.396d0,  B = -0.0715d0,   CC = -4.880d-4, D = -4.285d-5, &
                                   E = -3.140d0,  F = -2.22d-3,     G = -3.484d-5, H = 0.332d0,   &
                                   J = -7.251d-4, K = 0.1276d0,     P = 12.012d0,  Q = -7.551d-2, &
                                   R = -3.895d0,  T = 3.671d-2,     U = -0.1103d0, V = 8.7546d-4, &
                                   mult1 = 100d0, mult2 = 2.778d-6

    ! layed out in this manor to avoid memory management issues in module
    ! variables
    mV%potA(1:nos_soil_layers) = A + (B * soil_frac_clay) + &
                             (CC * soil_frac_sand * soil_frac_sand) + &
                              (D * soil_frac_sand * soil_frac_sand * soil_frac_clay)
    mV%potA(1:nos_soil_layers) = exp(mV%potA(1:nos_soil_layers))
    mV%potA(1:nos_soil_layers) = mV%potA(1:nos_soil_layers) * mult1

    mV%potB(1:nos_soil_layers) = E + (F * soil_frac_clay * soil_frac_clay) + &
                                  (G * soil_frac_sand * soil_frac_sand * soil_frac_clay)

    mV%cond1(1:nos_soil_layers) = mult2
    mV%cond2(1:nos_soil_layers) = P + (Q * soil_frac_sand)
    mV%cond3(1:nos_soil_layers) = R + (T * soil_frac_sand) + (U * soil_frac_clay) + &
                                   (V * soil_frac_clay * soil_frac_clay)

    ! assign bottom of soil column value to core
    mV%potA(nos_soil_layers+1)  = mV%potA(nos_soil_layers)
    mV%potB(nos_soil_layers+1)  = mV%potB(nos_soil_layers)
    mV%cond1(nos_soil_layers+1) = mult2
    mV%cond2(nos_soil_layers+1) = mV%cond2(nos_soil_layers)
    mV%cond3(nos_soil_layers+1) = mV%cond3(nos_soil_layers)

  end subroutine saxton_parameters
  !
  !------------------------------------------------------------------
  !
  subroutine calculate_soil_conductance(lm,local_lai,canopy_decay, mV)

    ! proceedsure to solve for soil surface resistance based on Monin-Obukov
    ! similarity theory stability correction momentum & heat are integrated
    ! through the under canopy space and canopy air space to the surface layer
    ! references are Nui & Yang 2004; Qin et al 2002
    ! NOTE: conversion to conductance at end

    implicit none

      type(model_working_variables) :: mV

    ! declare arguments
    double precision, intent(in) :: lm, &
                             local_lai, &
                          canopy_decay    ! & ! canopy decay coefficient for soil exchange

    ! local variables
    double precision :: Kh_canht       ! eddy diffusivity at canopy height (m2.s-1)

    ! parameters
    double precision, parameter :: foliage_drag = 0.2d0 ! foliage drag coefficient

    ! calculate eddy diffusivity at the top of the canopy (m2.s-1)
    ! Kaimal & Finnigan 1994; for near canopy approximation
    Kh_canht = vonkarman*mV%ustar*(canopy_height-mV%displacement)

    ! approximation of integral for soil resistance (s/m) and conversion to
    ! conductance (m/s)
!    soil_conductance = ( canopy_height/(canopy_decay*Kh_canht) &
!                       * (exp(canopy_decay*(1d0-(soil_roughl/canopy_height)))- &
!                          exp(canopy_decay*(1d0-((roughl+displacement)/canopy_height)))) ) ** (-1d0)
    mV%soil_conductance = 1d0 / ( canopy_height/(canopy_decay*Kh_canht) &
                             * (exp(canopy_decay*(1d0-(soil_roughl/canopy_height)))- &
                                exp(canopy_decay*(1d0-((mV%roughl+mV%displacement)/canopy_height)))) )

    return

  end subroutine calculate_soil_conductance
  !
  !----------------------------------------------------------------------
  !
  subroutine soil_water_potential (mV)

    ! Find SWP without updating waterfrac yet (we do that in !
    ! waterthermal). Waterfrac is m3 m-3, soilwp is MPa.     !

    implicit none

      type(model_working_variables) :: mV

    integer :: i

    ! reformulation aims to remove if statement within loop to hopefully improve
    ! optimisation
    mV%SWP(1:nos_soil_layers) = -0.001d0 * mV%potA(1:nos_soil_layers) &
                           * mV%soil_waterfrac(1:nos_soil_layers)**mV%potB(1:nos_soil_layers)
    ! NOTE: profiling indiates that 'where' is slower for very short vectors
    do i = 1, nos_soil_layers
       if (mV%SWP(i) < -20d0 .or. mV%SWP(i) /= mV%SWP(i)) mV%SWP(i) = -20d0
    end do

  end subroutine soil_water_potential
  !
  !------------------------------------------------------------------
  !
  subroutine z0_displacement(ustar_Uh,local_lai, mV)

    ! dynamic calculation of roughness length and zero place displacement (m)
    ! based on canopy height and lai. Raupach (1994)

    implicit none

      type(model_working_variables) :: mV

    ! arguments
    double precision, intent(out) :: ustar_Uh ! ratio of friction velocity over wind speed at canopy top
    double precision, intent(in) :: local_lai
    ! local variables
    double precision  sqrt_cd1_lai
    double precision, parameter :: cd1 = 7.5d0,   & ! Canopy drag parameter; fitted to data
                                    Cs = 0.003d0, & ! Substrate drag coefficient
                                    Cr = 0.3d0,   & ! Roughness element drag coefficient
                          ustar_Uh_max = 0.35d0,  &
                          ustar_Uh_min = 0.05d0,  &
                                    Cw = 2d0,     &  ! Characterises roughness sublayer depth (m)
                                 phi_h = 0.19314718056d0 ! Roughness sublayer influence function;

    ! describes the departure of the velocity profile from just above the
    ! roughness from the intertial sublayer log law


    ! Estimate canopy drag coefficient
    sqrt_cd1_lai = sqrt(cd1 * local_lai)

    ! calculate estimate of ratio of friction velocity / canopy wind speed.
    ! NOTE: under current min LAI and fixed canopy height (9 m) this ratio is
    ! fixed at 0.3
    ustar_Uh = 0.3d0
!    ustar_Uh = max(ustar_Uh_min,min(sqrt(Cs+Cr*local_lai*0.5d0),ustar_Uh_max))
!    ustar_Uh = sqrt(Cs+Cr*local_lai*0.5d0)

    ! calculate displacement (m); assume minimum lai 1.0 or 1.5 as height is not
    ! varied
    mV%displacement = (1d0-((1d0-exp(-sqrt_cd1_lai))/sqrt_cd1_lai))*canopy_height

    ! calculate roughness sublayer influence function;
    ! this describes the departure of the velocity profile from just above the
    ! roughness from the intertial sublayer log law
    ! phi_h = log(Cw)-1d0+Cw**(-1d0) ! DO NOT FORGET TO UPDATE IF Cw CHANGES

    ! finally calculate roughness length, dependant on displacement, friction
    ! velocity and lai.
    mV%roughl = ((1d0-mV%displacement/canopy_height)*exp(-vonkarman*ustar_Uh-phi_h))*canopy_height

    ! sanity check
!    if (roughl /= roughl) then
!        write(*,*)"TLS:  ERROR roughness length calculations"
!        write(*,*)"Roughness lenght", roughl, "Displacement", displacement
!        write(*,*)"canopy height", canopy_height, "lai", lai
!    endif

  end subroutine z0_displacement
  !
  !------------------------------------------------------------------
  !
  subroutine plant_soil_flow(root_layer,root_length,root_mass &
                            ,demand,root_reach_in,transpiration_resistance &
                            ,Rtot_layer, mV)

    !
    ! Calculate soil layer specific water flow form the soil to canopy (mmolH2O.m-2.s-1)
    ! Accounting for soil, root and plant resistance, and canopy demand
    !

    ! calculate and accumulate steady state water flux in mmol.m-2.s-1
    ! From the current soil layer given an amount of root within the soil layer.

    implicit none

      type(model_working_variables) :: mV

    ! arguments
    integer, intent(in) :: root_layer
    double precision, intent(in) :: root_length, &
                                      root_mass, &
                                         demand, &
                                  root_reach_in, &
                       transpiration_resistance
    double precision, intent(out) :: Rtot_layer

    ! local arguments
    double precision :: soilR1, soilR2

    ! Estimate soil hydraulic resistance to water flow (MPa m2 s mmol-1)
    ! Note: 1) soil conductivity converted from m.s-1 -> m2.s-1.MPa-1 by head.
    !       2) soil resistance calculation in single line to reduce assignment costs
    soilR1 = ( log(root_radius_1*(root_length*pi)**(-0.5d0)) &
               /(two_pi*root_length*root_reach_in*(mV%soil_conductivity(root_layer)*head_1))) &
             * 1d-9 * mol_to_g_water
    ! Calculates root hydraulic resistance (MPa m2 s mmol-1) in a soil-root zone
    soilR2 = root_resist / (root_mass*root_reach_in)
    ! Estimate the total hydraulic resistance for the layer
    Rtot_layer = transpiration_resistance + soilR1 + soilR2
    ! Track for later diagnosis of the effective canopy leaf water potential
    if (demand > 0d0) mV%conductance_mmolH2OMPam2s(root_layer) = 1d0/Rtot_layer
    ! Estimate the soil to plant flow of water mmolH2O/m2/s
    mV%water_flux_mmolH2Om2s(root_layer) = demand/Rtot_layer

    ! return
    return

  end subroutine plant_soil_flow
  !
  !------------------------------------------------------------------
  !
  subroutine calc_pools_crops(DS_LRRT,LRRT, mV)

    ! Allocated GPP to NPP and various carbon pools. Based  !
    ! this on physiological responses to temperature        !
    ! vernalisation, and photoperiod. Note: this code has   !
    ! been reformulated to a daily time step as default     !
    ! rather SPAcrop which operated on a hourly time step.  !

    implicit none

      type(model_working_variables) :: mV

    ! arguments
    double precision, dimension(:), intent(in) :: DS_LRRT, & ! Development stage corresponding to loss rate of roots
                                                        LRRT    ! loss rate of roots array

    ! local variables
    double precision :: decomp_efficency

    ! turnover rate of fine roots is now equal to the
    ! loss rate of roots as a function of development stage
    ! (Penning de Vries, 1989).
    mV%turnover_rate_roots = interpolate( mV%DS , DS_LRRT , LRRT , 5 )

    ! if sown turn on labile / seed turnover for growth
    if ( mV%sown ) then
        ! turnover on
        mV%turnover_labile_switch = 1
    else
        ! turnover off
        mV%turnover_labile_switch = 0
    endif

    ! Initialise / reset
    mV%yield = 0d0 ; mV%BM_EX = 0d0
    mV%HARVESTextracted_foliage = 0d0
    mV%HARVESTextracted_stem = 0d0
    mV%HARVESTextracted_dead_foliage = 0d0
    mV%HARVESTextracted_labile = 0d0
    mV%HARVESTlitter_labile = 0d0
    mV%HARVESTlitter_foliage = 0d0
    mV%HARVESTlitter_stem = 0d0
    mV%HARVESTlitter_dead_foliage = 0d0
    mV%HARVESTlitter_resp_auto = 0d0
    mV%PLOUGHlitter_roots = 0d0

    ! Determine total allocation of labile C to NPP or Ra (gC.m-2.d-1)
    mV%alloc_from_labile = mV%turnover_rate_labile * mV%resp_rate * dble(mV%turnover_labile_switch)
    mV%alloc_from_labile = mV%stock_labile * (1d0-(1d0-mV%alloc_from_labile) ** mV%days_per_step) * mV%days_per_step_1
    ! Determine the respiratory cost of C transfer from labile pool to NPP (gC.m-2.d-1)
    mV%resp_cost_labile_to_npp = mV%alloc_from_labile * resp_cost_labile_trans
    ! Determine the remaining allocation of labile C to NPP (gC.m-2.d-1)
    mV%alloc_from_labile = mV%alloc_from_labile - mV%resp_cost_labile_to_npp

    ! Estimate allocation (gC/m2/day) of GPP to autotrophic respiration pool
    mV%alloc_to_resp_auto = mV%gpp_acm * mV%frac_GPP_resp_auto
    ! NPP as a fraction of GPP (1-.32=.68 or 68%) + allocation from labile
    mV%npp = mV%gpp_acm + mV%alloc_from_labile - mV%alloc_to_resp_auto

    ! Dertermine partitioning of NPP to biomass pools
    mV%alloc_to_roots    = mV%root_frac_intpol * mV%npp
    ! Calculate how much NPP is left after allocation to roots
    mV%npp_shoot         = mV%npp - mV%alloc_to_roots
    ! Partition the remaining NPP to foliage and stem (gC/m2/day)
    mV%alloc_to_foliage  = mV%fol_frac_intpol  * mV%npp_shoot
    mV%alloc_to_stem     = mV%stem_frac_intpol * mV%npp_shoot
    ! Remaining NPP is then allocated to storage organ
    mV%alloc_to_storage_organ = max(0d0,mV%npp_shoot - mV%alloc_to_foliage - mV%alloc_to_stem)

    ! Assuming allocation to storage organ is > 0 ensure flux is limited by
    ! maximum growth rate potential, i.e. growth potential increases with size
    ! existing yield
    mV%alloc_to_labile = 0d0 ; mV%resp_cost_npp_to_labile = 0d0
    if ( mV%alloc_to_storage_organ > 0d0 ) then
        mV%gso_max  = ( mV%stock_storage_organ + 0.5d0 ) * rel_gso_max
        ! Restrict allocation to storage organ based on available C and potential
        ! Where does any excess go?
        mV%alloc_to_storage_organ = min( mV%alloc_to_storage_organ , mV%gso_max )
        if ( mV%sown .and. mV%emerged) then
            ! Assuming we shown and emerged, the remainder is now allocated to labile,
            ! less the cost of transfer
            mV%alloc_to_labile = ( mV%npp_shoot - mV%alloc_to_foliage - mV%alloc_to_stem - mV%alloc_to_storage_organ )
            ! Having worked out the total available C, update based on cost of respiratory transfer
            mV%resp_cost_npp_to_labile =  mV%alloc_to_labile * resp_cost_labile_trans
            mV%alloc_to_labile = mV%alloc_to_labile - mV%resp_cost_npp_to_labile
        endif
    endif

    ! set switches to (de)activate leaf, root and stem remobliization
    mV%raso_old = mV%raso
    ! Running Average of growth rate of Storage Organ..
    mV%raso = ( mV%alloc_to_storage_organ + mV%alloc_to_storage_organ_old ) * 0.5d0
    mV%max_raso_old = mV%max_raso
    mV%max_raso = max( mV%raso , mV%max_raso_old )
    ! Stem remobilisation triggered once running average of storage organ growth declines
    ! Second part prevents premature remobilisation
    if ( ( mV%raso < mV%raso_old ) .and. &
         ( mV%alloc_to_storage_organ > ( mV%alloc_to_storage_organ_old + 0.5d0 ) ) ) then
        mV%stmob = 1
    else
        mV%stmob = 0
    endif

    ! Code for calculating relative death rate of leaves (RDR) as a
    !  function of shading (RDRSH) or developmental stage (RDRT).

    ! GT 0 if LAI GT 4; 0. < RDRSH < RDRSHMAX (usually ~0.03)
    mV%RDRSH = min( mV%RDRSHMAX , max( 0d0 , mV%RDRSHMAX * ( mV%lai - LAICR ) / LAICR ) )
    if ( mV%DS < 1d0 ) then
       mV%RDRDV = 0d0
    else
       ! RDRDV dependant on DR and DS, values range typically between 0.02 <
       ! RDRDV < 0.25
!print*,"!! What RDRDV to use? !!"
!!$      RDRDV = DR /( max( 0.1 , 2. - DS ) )
!!$      RDRDV = RDRDV / 24. ! to get hourly senescence rate
       ! TLS: think the source of this equation needs to be found
       mV%RDRDV = mV%turnover_rate_foliage * min(1d0, ( 1d0 / ( ( max( 2d0 - mV%DS , 0.1d0 ) ) * 8d0 ) ) ** 2)
    ENDIF

    ! Relative leaf death rate is the maximum value of the arguments RDRSH and
    ! RDRDV
    mV%RDR = max( mV%RDRSH , mV%RDRDV )

    ! remobilization of foliar C and allocation to dead leaves pool (gC.m-2.d-1)
    mV%litterfall_foliage = mV%stock_foliage * (1d0-(1d0-mV%RDR) ** mV%days_per_step) * mV%days_per_step_1
    ! remobilization of stem C (gC.m-2.d-1)
    mV%litterfall_stem    = mV%stock_stem    * (1d0-(1d0-(mV%DR*mV%turnover_rate_stem*dble(mV%stmob))) ** mV%days_per_step) * mV%days_per_step_1
    ! remobilization of fine roots C (gC.m-2.d-1)
    mV%litterfall_roots   = mV%stock_roots   * (1d0-(1d0-mV%turnover_rate_roots) ** mV%days_per_step) * mV%days_per_step_1

    ! remobilized C to NPP (from both leaves and stems) (gC.m-2.d-1)
    ! Assume that half the foliage litter C is available for remobilisation
    mV%foliage_to_labile   = ( mV%litterfall_foliage * 0.5d0) * ( 1d0 - resp_cost_labile_trans )
    mV%stem_to_labile      = ( mV%litterfall_stem ) * ( 1d0 - resp_cost_labile_trans )
    ! respiratory cost of C transfer (conversion from starch to photosynthates) (gC.m-2.d-1)
    mV%resp_cost_foliage_to_labile   = ( mV%litterfall_foliage * 0.5d0) * resp_cost_labile_trans
    mV%resp_cost_stem_to_labile      = mV%litterfall_stem * resp_cost_labile_trans

    ! litter decomposition to soil organic matter
    mV%decomposition = mV%stock_litter &
                  * (1d0-(1d0-(mV%decomposition_rate*mV%resp_rate)) ** mV%days_per_step) &
                  * mV%days_per_step_1

    ! heterotrophic respiration component 1: mineralisation of litter C pool (gC.m-2.d-1)
    mV%resp_h_litter = mV%stock_litter &
                  * (1d0-(1d0-(mV%mineralisation_rate_litter*mV%resp_rate)) ** mV%days_per_step) &
                  * mV%days_per_step_1
    ! heterotrophic respiration component 2:  mineralisation of organic matter C pool (gC.m-2.d-1)
    mV%resp_h_soilOrgMatter = mV%stock_soilOrgMatter &
                         * (1d0-(1d0-(mV%mineralisation_rate_soilOrgMatter * mV%resp_rate)) ** mV%days_per_step) &
                         * mV%days_per_step_1

    ! Autotrophic respiration allocated pool is a special case, where the total
    ! available for the time step is accumulated first then losses are determined.
    ! Add photosynthate allocated to autotrophic respiration (gC.m-2.d-1)
    mV%stock_resp_auto = mV%stock_resp_auto + (mV%alloc_to_resp_auto * mV%days_per_step)
    ! Determine autotrophic costs as loss term. This should probably have a temperature sensitivity
    ! or be removed to work as applied in other DALEC models? (gC.m-2.d-1)
    ! NOTE: that autotrophic respiration from other sources added below
    mV%resp_auto = mV%stock_resp_auto * (1d0-(1d0-mV%turnover_rate_resp_auto) ** mV%days_per_step) * mV%days_per_step_1
    ! Update stock
    mV%stock_resp_auto = max(0d0, mV%stock_resp_auto - (mV%resp_auto * mV%days_per_step))

    ! nee (gC.m-2.d-1)
    mV%nee_dalec = (mV%resp_auto + mV%resp_cost_labile_to_npp + mV%resp_cost_npp_to_labile + &
                 mV%resp_cost_foliage_to_labile + mV%resp_cost_stem_to_labile + &
                 mV%resp_h_litter + mV%resp_h_soilOrgMatter) - mV%gpp_acm

    ! Recalculate Physical Carbon Pools...
    ! TLS: Note that while max(0d0,... conditions have been applied to all pools, I hypothese that the critical
    ! restriction is applied to stock_resp_auto
    mV%stock_foliage       = max(0d0, mV%stock_foliage       + ((mV%alloc_to_foliage - mV%litterfall_foliage) * mV%days_per_step))
    mV%stock_stem          = max(0d0, mV%stock_stem          + ((mV%alloc_to_stem - mV%litterfall_stem) * mV%days_per_step))
    mV%stock_storage_organ = max(0d0, mV%stock_storage_organ + ((mV%alloc_to_storage_organ) * mV%days_per_step))
    mV%stock_roots         = max(0d0, mV%stock_roots         + ((mV%alloc_to_roots   - mV%litterfall_roots) * mV%days_per_step))
    mV%stock_litter        = max(0d0, mV%stock_litter        + ((mV%litterfall_roots - mV%resp_h_litter - mV%decomposition) * mV%days_per_step))
    mV%stock_soilOrgMatter = max(0d0, mV%stock_soilOrgMatter + ((mV%decomposition    - mV%resp_h_soilOrgMatter) * mV%days_per_step))
    mV%stock_dead_foliage  = max(0d0, mV%stock_dead_foliage  + ((mV%litterfall_foliage * 0.5d0) * mV%days_per_step))
    mV%stock_labile        = max(0d0, mV%stock_labile        + ((mV%alloc_to_labile  - mV%alloc_from_labile - &
                                                           mV%resp_cost_labile_to_npp + mV%foliage_to_labile + &
                                                           mV%stem_to_labile) * mV%days_per_step) )

    ! When GPP is higher than seed C content, remaining seed carbon enters litter
    ! C pool, as seedlings do not fully exhaust their seed (P. de Vries p 48)
    ! Moved to the end of the mass balance, to maintain balance with fluxes which
    ! are already concurrently occuring. In this codes previous position further
    ! up the code, resulted in C being both dumped to litter and the same carbon
    ! being allocated to respiration and NPP.
    if ( ( mV%gpp_acm > mV%alloc_from_labile ) .and. ( mV%use_seed_labile ) ) then
        mV%stock_litter = mV%stock_litter + mV%stock_labile
        mV%stock_labile = 0d0
        mV%use_seed_labile = .false.
    endif

  end subroutine calc_pools_crops
  !
  !--------------------------------------------------------------------------------------------------------------------------------!
  !
  subroutine carbon_alloc_fractions(DS_shoot,DS_root,fol_frac,stem_frac,root_frac, mV)

    ! Determines carbon allocation fractions as a function !
    ! of developmental stage (DS).  Allocation fractions   !
    ! are from tables published in Penning de Vries (1989) !

    implicit none

      type(model_working_variables) :: mV

    double precision, dimension(:), intent(in) ::   DS_shoot, & !
                                                        DS_root, & !
                                                       fol_frac, & !
                                                      stem_frac, & !
                                                      root_frac    !

    ! local variables..
    double precision,dimension(:),allocatable :: frac_shoot, frac_root

    if ( mV%sown ) then ! after sowing

       ! loop over three crop "organs": 1) foliage 2) stems 3) root
       ! not necessary for storage organs, as all remaining C is allocated to
       ! these

       ! use different input for foliage and stem fractions, as they are
       ! relative to the total shoot (or aboveground) allocation,
       ! root is relative to total plant (above- and belowground) allocation.

       ! leaf development stages and corresponding fractions..

       ! interpolate between PdV allocation values with reference to
       ! developmental stage (DS)..
       mV%fol_frac_intpol = min(1d0,max(0d0,interpolate( mV%DS , DS_shoot , fol_frac , size(DS_shoot) )))
       ! stem DS and fracs..
       mV%stem_frac_intpol = min(1d0,max(0d0,interpolate( mV%DS , DS_shoot , stem_frac , size(DS_shoot) )))
       ! root DS and fracs..
       mV%root_frac_intpol = min(1d0,max(0d0,interpolate( mV%DS , DS_root , root_frac , size(DS_root) )))

    endif ! after crop has been sown

  end subroutine carbon_alloc_fractions
  !
  !--------------------------------------------------------------------------------------------------------------------------------!
  !
  subroutine development_stage(days_in_step, mV)

    ! Based on modified Wang & Engel model (Streck et al., 2003), !
    ! but with only 2 sub-phases, vegetative and reproductive     !
    ! (i.e. only two different DRmax).   O. Sus, May 2010.        !

    implicit none

      type(model_working_variables) :: mV

    ! agruments
    double precision :: days_in_step

    ! local variables..
    double precision ::  dttmin,  & ! Difference between daily average and minimum temperatures
                         dttmin_v   ! Difference between daily average and minimum vernalization temperatures

    dttmin   = mV%meant - mV%tmin    ! difference between daily average and minimum cardinal temperatures
    dttmin_v = mV%meant - mV%tmin_v  ! difference between daily average and minimum vernalization temperatures

    ! Calculation of developmental function values: vernalization (fV),
    ! temperature (fT) and
    ! photoperiod (fP) these values are multiplicative factors of DRmax (maximum
    ! developmental
    ! rate), each ranging between 0 (no development) and 1 (unrestricted
    ! development).

    ! Summation of vernalization days (VD), not before sowing and only if
    ! average temperature is within min and max cardinal temperatures..
    if ( ( mV%meant > mV%tmin_v ) .and. ( mV%meant < mV%tmax_v ) .and. mV%sown ) then
        mV%fV = vernalization( mV%doptmin_v , mV%dmaxmin_v , dttmin_v , days_in_step , mV)
    endif

    ! Only calculate temperature coefficient if meant lies within (tmin,tmax)
    ! range. NOTE: (doptmin+1d0) < dmaxmin added to allow for EDC search period when "not
    ! allowed" parameter sets will be tried anyway
    if ( mV%meant > mV%tmin .and. mV%meant < mV%tmax .and. (mV%doptmin+1d0) < mV%dmaxmin ) then
        mV%fT = temperature_impact( mV%doptmin , mV%dmaxmin , dttmin )
    else
        mV%fT = 0d0
    endif

    ! calculation of photoperiod coefficient
    mV%fP = photoperiod_impact( mV%PHCR , mV%PHSC , mV)

    if ( mV%emerged .and. ( mV%DS < 2d0 ) ) then   ! sum up daily DR values between emergence and maturity (DS=2)

       if ( mV%DS < 1d0 ) then  ! in the vegetative phase (before flowering):

          mV%DR = mV%DR_pre * mV%fT * mV%fP   ! DR is affected by temperature, photoperiod...
          if ( mV%vernal_calcs ) mV%DR = mV%DR * mV%fV ! ...and vernalization (for winter cereals)
          mV%DS = mV%DS + (mV%DR * days_in_step)    ! developmental stage (DS), calculated as the sum of daily developmental rates

       else    ! in the reproductive phase (after flowering):

          mV%DR = mV%DR_post * mV%fT   ! DR is affected only by temperature
          mV%DS = mV%DS + (mV%DR * days_in_step)

       endif ! vegetative or reproductive phase

    endif ! emerged or not

  end subroutine development_stage
  !
  !--------------------------------------------------------------------------------------------------------------------------------!
  !
  subroutine management_dates(stock_seed_labile,days_in_step, mV)

    ! This routine should be called at the end of each day of a crops  !
    ! simulation.  It checks whether we should plough/sow/harvest, and !
    ! during the growing establishes when the crop will emerge after   !
    ! sowing, based on heat accumulation (Phenological Heat Units).    !

    implicit none

      type(model_working_variables) :: mV

    ! arguments
    double precision, intent(in) :: stock_seed_labile,days_in_step
    ! local variables
    double precision :: tmp
    logical :: plough_sanity,sow_sanity,harvest_sanity

    ! reset
    plough_sanity = .false. ; sow_sanity = .false. ; harvest_sanity = .false.
    !plough_sanity = .true. ; sow_sanity = .true. ; harvest_sanity = .true.

    ! spring crop
    if (mV%sow_day < mV%harvest_day .and. nint(mV%doy) < mV%harvest_day) sow_sanity = .true.
    if (mV%plough_day < mV%harvest_day .and. nint(mV%doy) < mV%harvest_day) plough_sanity = .true.
    if (mV%harvest_day > mV%sow_day) harvest_sanity = .true.
    ! winter crops
    if (mV%sow_day > mV%harvest_day) sow_sanity = .true.
    if (mV%plough_day > mV%harvest_day) plough_sanity = .true.
    if (mV%harvest_day < mV%plough_day .and. nint(mV%doy) < mV%plough_day) harvest_sanity = .true.

    if ( .not. mV%sown ) then

        ! fresh field...

        if ( plough_sanity .and. .not.mV%ploughed .and. nint(mV%doy) >= mV%plough_day ) then
            ! the field needs ploughing..
            call plough(mV)

        else if ( sow_sanity .and. nint(mV%doy) >= mV%sow_day ) then

            ! ensure that the field has indeed been ploughed
            if (.not.mV%ploughed) call plough(mV)
            ! the field needs sowing..
            mV%sown = .true.

            ! this switch controls whether the labile carbon within the seed is used
            ! for growth
            mV%use_seed_labile = .true.
            mV%stock_labile = stock_seed_labile

        endif ! plough or sow?

    else

        ! crop in field..

        ! calculate when crop emerges..
        if ( .not. mV%emerged ) then

            ! estimate emergence date based on the accumulated phenological heat
            ! units (PHU)
            ! where PHU is the (positive) heat over tmin..
            tmp = max( mV%meant - mV%tmin , 0d0 )*days_in_step
            mV%PHU = mV%PHU + tmp

            ! set the development stage and emergence..
            if ( mV%PHU >= mV%PHUem ) then
                mV%emerged = .true.
                mV%DS = 0d0
            else
                mV%emerged = .false.
                mV%DS = -1d0
            endif

        endif ! emerged or not

        ! note that in this case harvest day has been fixed relative to the sow
        ! day
        if ( harvest_sanity .and. nint(mV%doy) >= mV%harvest_day) then
            ! the field needs harvesting..
            call harvest(mV)
        endif

    endif ! sown or not

  end subroutine management_dates
  !
  !--------------------------------------------------------------------------------------------------------------------------------!
  !
  ! PROCEDURES BELOW ARE PRIVATE, IE THEIR USE IS LIMITED TO THIS MODULE
  !
  !--------------------------------------------------------------------------------------------------------------------------------!
  !
  subroutine harvest (mV)

    implicit none

      type(model_working_variables) :: mV

    ! Declare some local variables
    double precision :: tmp, Ctotal

    ! shoot biomass..
    mV%Cshoot = mV%stock_foliage + mV%stock_stem + mV%stock_storage_organ + mV%stock_labile
    ! Total biomass not including the storage organ
    Ctotal = mV%stock_foliage + mV%stock_stem + mV%stock_roots

    ! Can only work if there is biomass in existance
    if (Ctotal > 0d0) then

        ! determine harvest index..
        mV%HI = mV%stock_storage_organ / mV%Cshoot

        ! the stuff we actually want from the harvest...
        mV%yield = mV%stock_storage_organ * mV%days_per_step_1

        ! How much of each pool is extracted during harvest
        mV%HARVESTextracted_foliage      = mV%stock_foliage * ( 1d0 - lv_res ) * mV%days_per_step_1
        mV%HARVESTextracted_stem         = mV%stock_stem * ( 1d0 - st_res ) * mV%days_per_step_1
        mV%HARVESTextracted_dead_foliage = mV%stock_dead_foliage * ( 1d0 - lv_res ) * mV%days_per_step_1
        ! How much of each pool remains as litter after harvest
        mV%HARVESTlitter_foliage      = mV%stock_foliage * lv_res * mV%days_per_step_1
        mV%HARVESTlitter_stem         = mV%stock_stem * st_res * mV%days_per_step_1
        mV%HARVESTlitter_dead_foliage = mV%stock_dead_foliage * lv_res * mV%days_per_step_1
        mV%HARVESTlitter_resp_auto    = mV%stock_resp_auto * mV%days_per_step_1

        ! Labile is a special case due to being distributed within various tissues.
        ! NOTE that extracted is calculated then the litter component is estimates as residual.
        ! The time scale adjustment is applied last, rather than inline (as above)
        mV%HARVESTextracted_labile = (mV%stock_labile * (mV%stock_foliage / Ctotal) *  (1d0 - lv_res )) &
                                + (mV%stock_labile * (mV%stock_stem / Ctotal) *  (1d0 - st_res ))
        mV%HARVESTlitter_labile    =  mV%stock_labile - mV%HARVESTextracted_labile
        mV%HARVESTextracted_labile = mV%HARVESTextracted_labile * mV%days_per_step_1
        mV%HARVESTlitter_labile    = mV%HARVESTlitter_labile * mV%days_per_step_1

        ! the biomass that is harvested in addition to the storage-organ..
        mV%BM_EX  = mV%HARVESTextracted_foliage      &
               + mV%HARVESTextracted_stem         &
               + mV%HARVESTextracted_dead_foliage &
               + mV%HARVESTextracted_labile

        ! what's left (will fall to the ground as litter)..
        mV%stock_litter  = mV%stock_litter +               &
                       (mV%HARVESTlitter_foliage +      &
                        mV%HARVESTlitter_stem +         &
                        mV%HARVESTlitter_dead_foliage + &
                        mV%HARVESTlitter_resp_auto +    &
                        mV%HARVESTlitter_labile) * mV%days_per_step

    end if ! Ctotal > 0

    ! empty the plant stocks..
    mV%stock_storage_organ = 0d0
    mV%stock_foliage       = 0d0
    mV%stock_stem          = 0d0
    mV%stock_dead_foliage  = 0d0
    mV%stock_labile        = 0d0
    mV%stock_resp_auto     = 0d0

    ! roots stay in ground and slowly decompose (until/unless the field is
    ! ploughed)

    ! reset logical variables..
    mV%sown    = .false.
    mV%emerged = .false.
    mV%ploughed = .false.
    mV%DS = -1d0 ; mV%fV = 0d0 ; mV%fT = 0d0 ; mV%fP = 0d0 ; mV%VD = 0d0

  end subroutine harvest
  !
  !--------------------------------------------------------------------------------------------------------------------------------!
  !
  double precision function photoperiod_impact( PH_crit , PH_sens , mV)

    ! Function to determine the coefficient for !
    ! photoperiod impact on developmental rate. !
    ! From Streck et al., 2003                  !

    implicit none

      type(model_working_variables) :: mV

    ! arguments..
    double precision,intent(in) :: PH_crit, & ! critical photoperiod below which no development occurs
                                   PH_sens    ! photoperiod sensitivity

    photoperiod_impact = max(0d0, 1d0 - exp ( - PH_Sens * ( mV%dayl_hours - PH_crit ) ))

  end function photoperiod_impact
  !
  !--------------------------------------------------------------------------------------------------------------------------------!
  !
  subroutine plough (mV)

    ! this s/r will reset various carbon pools, to mimic the effect of the
    ! farmer ploughing. !

    implicit none

      type(model_working_variables) :: mV

    ! Track roots transition to litter here, as all others have been reset already
    mV%PLOUGHlitter_roots = mV%stock_roots * mV%days_per_step_1

    ! Move all plant stocks into the litter pool.
    ! ( many of these should already be empty after the harvest, )
    ! ( e.g. the stocks for labile, foliage, storage-organ stem. )
    mV%stock_litter        = mV%stock_litter + mV%stock_dead_foliage &
                          + mV%stock_foliage + mV%stock_labile    &
                           + mV%stock_roots + mV%stock_stem       &
                            + mV%stock_storage_organ

    mV%stock_dead_foliage  = 0d0
    mV%stock_foliage       = 0d0
    mV%stock_labile        = 0d0
    mV%stock_roots         = 0d0
    mV%stock_stem          = 0d0
    mV%stock_storage_organ = 0d0

    ! Reset the development stage & phenological heat units..
    mV%ploughed = .true. ; mV%DS = -1d0 ; mV%PHU = 0d0
    mV%max_raso = 0d0 ; mV%raso = 0d0 ; mV%max_raso_old = 0d0 ; mV%raso_old = 0d0
    mV%alloc_to_storage_organ_old = 0d0

  end subroutine plough
  !
  !------------------------------------------------------------------
  !
  !------------------------------------------------------------------
  ! Functions other than the primary ACM and ACM ET are stored
  ! below this line.
  !------------------------------------------------------------------
  !
  !------------------------------------------------------------------
  !
  !
  !------------------------------------------------------------------
  !
  pure function arrhenious( a , b , t )

    ! The equation is simply...                        !
    !    a * exp( b * ( t - 25.0 ) / ( t + 273.15 ) )  !
    ! However, precision in this routine matters as it !
    ! affects many others. To maximise precision, the  !
    ! calculations have been split.                    !

    implicit none

    ! arguments..
    double precision,intent(in) :: a , b , t
    double precision            :: arrhenious

    arrhenious = a * exp( b * (t - 25d0) / (t + freeze) )

  end function arrhenious
  !
  !----------------------------------------------------------------------
  !
  double precision function calculate_declination(doy)

    implicit none

     ! Declare arguments
     double precision, intent(in) :: doy

     ! Declination calculation
     ! NOTE: 0.002739726d0 = 1/365
     !    dec = - asin( sin( 23.45d0 * deg_to_rad ) * cos( 2d0 * pi * ( doy + 10d0 ) / 365d0 ) )
     !    dec = - asin( sin_dayl_deg_to_rad * cos( two_pi * ( doy + 10d0 ) / 365d0 ) )
     calculate_declination = - asin( sin_dayl_deg_to_rad * cos( two_pi * ( doy + 10d0 ) * 0.002739726d0 ) )

     ! return to user
     return

  end function calculate_declination
  !
  !----------------------------------------------------------------------
  !
  double precision function opt_max_scaling( max_val, min_val , optimum , kurtosis , current )

    ! Estimates a 0-1 scaling based on a skewed guassian distribution with a
    ! given optimum, maximum and kurtosis. Minimum is assumed to be at infinity
    ! (or near enough)

    implicit none

    ! arguments..
    double precision, intent(in) :: max_val, min_val, optimum, kurtosis, current

    ! Code with explicit min bound
    opt_max_scaling = exp( kurtosis * log((max_val-current)/(max_val-optimum)) * (max_val-optimum) ) &
                    * exp( kurtosis * log((current-min_val)/(optimum-min_val)) * (optimum-min_val) )
    ! Sanity check, allows for overlapping parameter ranges
    if (opt_max_scaling /= opt_max_scaling) opt_max_scaling = 0d0

  end function opt_max_scaling
  !
  !------------------------------------------------------------------
  !
  !
  !------------------------------------------------------------------
  !
  double precision function water_retention_saxton_eqns( xin , mV)

    ! field capacity calculations for saxton eqns !

    implicit none

      type(model_working_variables) :: mV

    ! arguments..
    double precision, intent(in) :: xin

    ! local variables..
    double precision :: soil_wp

    ! calculate the soil water potential (kPa)..
    soil_wp = -mV%potA(mV%water_retention_pass) * xin**mV%potB(mV%water_retention_pass)
    water_retention_saxton_eqns = soil_wp + 10d0    ! 10 kPa represents air-entry swp

    return

  end function water_retention_saxton_eqns
  !
  !--------------------------------------------------------------------------
  !
  double precision function linear_model_gradient(x,y,interval)

    ! Function to calculate the gradient of a linear model for a given dependent
    ! variable (y) based on predictive variable (x). The typical use of this
    ! function will in fact be to assume that x is time.

    implicit none

    ! declare input variables
    integer :: interval
    double precision, dimension(interval) :: x,y

    ! declare local variables
    double precision :: sum_x, sum_y, sumsq_x,sum_product_xy
    integer :: j

    ! single-pass accumulation loop replacing four separate sum() reductions
!    ! calculate the sum of x
!    sum_x = sum(x)
!    ! calculate the sum of y
!    sum_y = sum(y)
    sum_x = 0d0 ; sum_y = 0d0 ; sumsq_x = 0d0 ; sum_product_xy = 0d0
    do j = 1, interval
       sum_x          = sum_x          + x(j)
       sum_y          = sum_y          + y(j)
       sumsq_x        = sumsq_x        + x(j)*x(j)
       sum_product_xy = sum_product_xy + x(j)*y(j)
    end do
    ! calculate the sum of squares of x
    !sumsq_x = sum(x*x)
    ! calculate the sum of the product of xy
    !sum_product_xy = sum(x*y)
    ! calculate the gradient
    !linear_model_gradient = ( (dble(interval)*sum_product_xy) - (sum_x*sum_y) ) &
    !                      / ( (dble(interval)*sumsq_x) - (sum_x*sum_x) )
!    ! Linear regression done as single line to reduce assignment requirements
!    linear_model_gradient = ( (dble(interval)*sum(x*y)) - (sum_x*sum_y) ) &
!                          / ( (dble(interval)*sum(x*x)) - (sum_x*sum_x) )
    linear_model_gradient = ( (dble(interval)*sum_product_xy) - (sum_x*sum_y) ) &
                          / ( (dble(interval)*sumsq_x)        - (sum_x*sum_x) )

    ! for future reference here is how to calculate the intercept
!    intercept = ( (sum_y*sumsq_x) - (sum_x*sum_product_xy) ) &
!              / ( (dble(interval)*sumsq_x) - (sum_x*sum_x) )

    ! don't forget to return to the user
    return

  end function linear_model_gradient
  !
  !------------------------------------------------------------------
  !
  double precision function temperature_impact( doptmin , dmaxmin , dttmin )

    ! Function to determine the coefficent for  !
    ! temperature impact on developmental rate. !
    ! From Streck et al., 2003.                 !

    implicit none

    ! arguments..
    double precision,intent(in) :: doptmin , dmaxmin , dttmin   ! temperature differences

    ! local variables..
    double precision :: a , nmr , dnr

    a   = log( 2.d0 ) / ( log( ( dmaxmin ) / doptmin ) )

    nmr = 2.d0 * ( ( dttmin ) ** a ) * ( doptmin ** a ) - ( ( dttmin ) ** ( 2.d0 * a ) )

    dnr = doptmin ** ( 2.d0 * a )

    temperature_impact = nmr / dnr

  end function temperature_impact
  !
  !------------------------------------------------------------------
  !
  double precision function vernalization( doptmin_v , dmaxmin_v , dttmin_v , days_in_step , mV)

    ! Function to determine the coefficent for vernalization !
    ! impact on developmental rate. See Streck et al., 2003. !

    implicit none

      type(model_working_variables) :: mV

    ! arguments..
    double precision,intent(in) :: dmaxmin_v , doptmin_v , dttmin_v & ! temperature differences
                                  ,days_in_step

    ! local variables..
    double precision :: a , dnr , fvn , nmr , VD5

    a   = log( 2.d0 ) / ( log( ( dmaxmin_v ) / doptmin_v ) )
    nmr = 2.d0 * ( ( dttmin_v ) ** a ) * ( doptmin_v ** a ) - ( ( dttmin_v ) ** (2.d0 * a ) )
    dnr = doptmin_v ** ( 2.d0 * a )
    fvn = nmr / dnr

    mV%VD = mV%VD + (fvn*days_in_step)
    VD5 = mV%VD ** 5

    ! final output value..
    vernalization = max( 0d0 , min( 1d0 , VD5 / ( mV%VDh5 + VD5 ) ) )

  end function vernalization
  !
  !------------------------------------------------------------------
  !
  double precision function interpolate( x , reference_x , reference_y , row )

    ! Interpolation function.                    !
    ! x is input value, interpol is output value !
    ! reference_x/y are reference input data.    !

    implicit none

    ! arguments..
    integer, intent(in)                          :: row
    double precision, intent(in)                 :: x
    double precision, dimension(row), intent(in) :: reference_x , reference_y

    ! local variables..
    integer::i

    ! provide initial value
    interpolate = -9999d0
    do i = 1 , row

       if ( x .le. reference_x(1) ) then
          interpolate = reference_y(1)
          exit
       endif

       ! cycling means growth rate remains constant between DS levels
       if ( ( x .gt. reference_x(i) ) .and. ( i .lt. row ) ) cycle

       if ( x .eq. reference_x(i) ) then
          interpolate = reference_y(i)
          exit
       endif

       if ( x .lt. reference_x(i) ) then
          interpolate = reference_y(i-1) + ( x - reference_x(i-1) ) &
                       * ( reference_y(i) - reference_y(i-1) )      &
                       / ( reference_x(i) - reference_x(i-1) )
          exit
       else
          interpolate = reference_y(row)
       endif

    enddo

    ! explicit return to ser
    return

  end function interpolate
  !
  !------------------------------------------------------------------
  !
  !
  !------------------------------------------------------------------
  ! Generic mathematical functions such as bisection and intergrator proceedures
  ! are stored below here
  !------------------------------------------------------------------
  !
  !
  !------------------------------------------------------------------
  !
!
!--------------------------------------------------------------------
!
end module CARBON_MODEL_MOD
